use Collection;

bundle DB {
	enum Match {
		LESS,
		GREATER,
		LESS_EQUAL,
		GREATER_EQUAL,
		EQUAL,
		NOT_EQUAL,
		LIKE
	}
	
	class Table {
		@column_names : StringMap;
		@rows : List;
		@index_keys : Hash;
		@index : Int;
		
		New(column_names : String[], index : Int) {
			@column_names := StringMap->New();
			@rows := List->New();
			@index_keys := Hash->New();
			@index := index;
			
			each(i : column_names) {
				@column_names->Insert(column_names[i], IntHolder->New(i));
			};
		}
		
		method : public : AddRow(values : Compare[]) ~ Bool {
			if(@column_names->Size() <> values->Size()) {
				## TODO: error
				return false;
			};
			
			if(@index_keys->Has(values[@index])) {
				## TODO: error
				return false;
			};
			
			row := Row->New(values);
			@index_keys->Insert(values[@index], row);
			@rows->AddBack(row);
			
			return true;
		}
		
		method : public : Size() ~ Int {
			return @rows->Size();
		}
		
		method : public : Find(value : Compare, type : Match, column : String) ~ Vector {
			index := @column_names->Find(column)->As(IntHolder);
			if(index <> Nil) {
				return Find(value, type, index->Get());
			};
			
			return Vector->New();
		}
		
		method : public : native : Find(value : Compare, type : Match, index : Int) ~ Vector {
			matches := Vector->New();
			
			if(index < 0 | index >= @column_names->Size()) {
				return Nil;
			};		
					
			if(@index = index) {
				row := @index_keys->Find(value)->As(Row);
				if(row <> Nil) {
					matches->AddBack(row);
				};
			}
			else {
				@rows->Rewind();
				while(@rows->More()) {
					row := @rows->Get()->As(Row);
					if(row->Match(value, type, index)) {
						matches->AddBack(row);
					};
					@rows->Next();
				};
			};
			
			return matches;
		}
	}

	class Row {
		@values : Compare[];
		
		New(values : Compare[]) {
			@values := values;
		}
		
		method : public : Size() ~ Int {
			return @values->Size();
		}
		
		method : public : native : Match(right : Compare, type : Match, index : Int) ~ Bool {
			left := @values[index];
			select(type) {
				label Match->LESS: {
					return left->Compare(right) < 0;
				}
				
				label Match->GREATER: {
					return left->Compare(right) > 0;
				}
				
				label Match->LESS_EQUAL: {
					return left->Compare(right) < 0 | left->Compare(right) = 0;
				}
				
				label Match->GREATER_EQUAL: {
					return left->Compare(right) > 0 | left->Compare(right) = 0;
				}
				
				label Match->EQUAL: {
					return left->Compare(right) = 0;
				}
				
				label Match->NOT_EQUAL: {
					return left->Compare(right) <> 0;
				}
				
				label Match->LIKE: {
				}
			};
			
			return false;
		}
		
		method : public : Get(index : Int) ~ Compare {
			if(index < 0 | index >= @values->Size()) {
				return Nil;
			};
			
			return @values[index];
		}
		
		method : public : Set(value : Compare, index : Int) ~ Bool {
			return false;
		}
	}
}