use Collection;

bundle CodeGen {
	#~
	Source emitter
	~#
	class Emitter {
		function : Main(args : String[]) ~ Nil {
			if(args->Size() = 2) {
				this_name := ParseThisName(args[0]);
				template := EmitterTemplate->New();
				tokens := Scanner->New(args[1])->Scan();
				functions := Parser->New(tokens, this_name)->Parse();
				
				this_name->PrintLine();
				functions->Size()->PrintLine();
				each(i : functions) {
					parsed_function := functions->Get(i)->As(ParsedFunction);
					EmitObjeckCode(parsed_function);
					EmitCxxCode(parsed_function);
"---"->PrintLine();
				};
			};
		}
		
		function : ParseThisName(name : String) ~ String {
			if(name->StartsWith("SDL_")) {
				name := name->SubString(4, name->Size() - 4);
			};
			
			return name;
		}
		
		
		function : GetTypeString(type : ParsedType) ~ String{
			buffer := "";
			
			select(type->GetType()) {
				label ParsedType->Type->INT: {
					buffer->Append("Int");
				}
				
				label ParsedType->Type->BOOL: {
					buffer->Append("Bool");
				}
				
				label ParsedType->Type->CLASS: {
					buffer->Append(type->GetName());
				}
				
				label ParsedType->Type->VOID_STAR: {
					buffer->Append("Int");
				}
				
				label ParsedType->Type->CHAR_STAR: {
					buffer->Append("String");
				}
				
				label ParsedType->Type->VOID: {
					buffer->Append("Nil");
				}
			};
			
			return buffer;
		}
		
		function : GetParametersString(parameters : ParsedParameters) ~ String {
			buffer := "(";
			
			i := 0;
			if(parameters->Size() > 0) {
				first_parameter := parameters->Get(0);
				if(first_parameter->GetType()->GetType() = ParsedType->Type->CLASS & ParseThisName(GetTypeString(first_parameter->GetType()))->Equals("Window")) {
					i := 1;
				};
			};
			
			for(i := i; i < parameters->Size(); i += 1;) {
				parameter := parameters->Get(i);
				buffer += parameter->GetName();
				buffer += " : ";
				buffer += ParseThisName(GetTypeString(parameter->GetType()));
				
				if(i + 1 < parameters->Size()) {
					buffer += ", ";
				};
			};
			buffer += ")";
			
			return buffer;
		}
		
		function : GetCallingString(parameters : ParsedParameters) ~ String {
			buffer := "(";
			
			for(i := 0; i < parameters->Size(); i += 1;) {
				parameter := parameters->Get(i);
				buffer += parameter->GetName();
				
				if(i + 1 < parameters->Size()) {
					buffer += ", ";
				};
			};
			buffer += ");\n";
			
			return buffer;
		}
		
		function : EmitCxxCode(parsed_function : ParsedFunction) ~ Nil {
			buffer := "#ifdef _WIN32\n";
			buffer += "\t__declspec(dllexport)\n";
			buffer += "#endif\n";
			buffer += "\tvoid ";
			buffer += parsed_function->GetCxxName();
			buffer += "(VMContext& context) {\n";
			
			
			
			
			
			
			offset := 0;
			has_return := <>parsed_function->GetReturnType()->GetType() = ParsedType->Type->VOID;
			parameters := parsed_function->GetParameters();
			if(has_return) {
				offset += 1;
			};
			
			each(i : parameters) {
				parameter := parameters->Get(i);
				select(parameter->GetType()->GetType()) {
					label ParsedType->Type->INT:
					label ParsedType->Type->BOOL: {
						buffer += "\t\tconst int ";
						buffer += parameter->GetName();
						buffer += " = APITools_GetIntValue(context, ";
						buffer += offset;
						buffer += ");\n";
					}
	
					label ParsedType->Type->CLASS: {
						buffer += "\t\tconst long* ";
						buffer += parameter->GetName();
						buffer += " = (long*)APITools_GetObjectValue(context, ";
						buffer += offset;
						buffer += ");\n";
					}
					
					label ParsedType->Type->CHAR_STAR: {
						buffer += "\t\tconst wstring ";
						buffer += parameter->GetName();
						buffer += " = APITools_GetStringValue(context, ";
						buffer += offset;
						buffer += ");\n";
					}
					
#~					
					label ParsedType->Type->VOID_STAR: {
					}
~#					
				};
				## update
				offset++;
			};
			
			parameter := parsed_function->GetReturnType();
			select(parameter->GetType()) {
				label ParsedType->Type->INT:
				label ParsedType->Type->BOOL: {
					buffer += "\t\tint return_value = ";
					buffer += parsed_function->GetName();
					buffer += GetCallingString(parameters);
					buffer += "\t\tAPITools_SetIntValue(context, 0, return_value);\n";
				}

				label ParsedType->Type->CLASS: {
					buffer += "\t\tAPITools_SetObjectValue(context, 0, return_value);\n";
				}
				
				label ParsedType->Type->CHAR_STAR: {
					buffer += "\t\tAPITools_SetStringValue(context, 0, return_value);\n";
				}
				
#~					
				label ParsedType->Type->VOID_STAR: {
				}
~#					
			};
			
			
			
			
			
			
			buffer += "\t}\n";
			buffer->PrintLine();
		}
		
		function : EmitObjeckCode(parsed_function : ParsedFunction) ~ Nil {
			buffer := "method : public : ";
			buffer += parsed_function->GetObjeckName();
			buffer += GetParametersString(parsed_function->GetParameters());
			buffer += " ~ ";
			buffer += GetTypeString(parsed_function->GetReturnType());
			buffer += " {\n\t";
			
			offset := 0;
			parameters := parsed_function->GetParameters();
			if(parsed_function->GetReturnType()->GetType() = ParsedType->Type->VOID) {
				buffer += "array_args := Base->New[";
				buffer += parameters->Size();
				buffer += "];\n";
			}
			else {
				offset := 1;
				buffer += "array_args := Base->New[";
				buffer += parameters->Size() + 1;
				buffer += "];\n\t";
				buffer += "array_args[0] := IntHolder->New()\n";
			};
			
			each(i : parameters) {
				parameter := parameters->Get(i);
				if(i = 0) {
					buffer += "\tarray_args[";
					buffer += offset;
					buffer += "] := IntHolder->New(@";
					buffer += parameter->GetName();
					buffer += ");\n";
				}
				else {
					select(parameter->GetType()->GetType()) {
						label ParsedType->Type->INT: {
							buffer += "\tarray_args[";
							buffer += offset;
							buffer += "] := IntHolder->New(";
							buffer += parameter->GetName();
							buffer += ");\n";
						}
						
						label ParsedType->Type->BOOL: {
							buffer += "\tarray_args[";
							buffer += offset;
							buffer += "] := IntHolder->New(";
							buffer += parameter->GetName();
							buffer += " ? 0 : 1);\n";
						}
						
						label ParsedType->Type->CLASS:
						label ParsedType->Type->CHAR_STAR: {
							buffer += "\tarray_args[";
							buffer += offset;
							buffer += "] := ";
							buffer += parameter->GetName();
							buffer += ";\n";
						}
						
						label ParsedType->Type->VOID_STAR: {
						}
					};
				};
				offset += 1;
			};
			
			buffer += "\n\t@lib_proxy := Proxy->GetDllProxy();\n";
			buffer += "\t@lib_proxy->CallFunction(\"";
			buffer += parsed_function->GetCxxName();
			buffer += "\", array_args);\n\n";
			
			buffer += "\tvalue := array_args[0]->As(IntHolder);\n";
			buffer += "\treturn value->Get();\n";		
			buffer += "}\n";
			
			buffer->PrintLine();
		}
	}
	
	class EmitterTemplate {
		@name : String;
		
		New(name : String) {
			@name := name;
		}
	}
}