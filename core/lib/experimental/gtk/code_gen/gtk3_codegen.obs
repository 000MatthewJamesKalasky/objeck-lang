use Collection;
use System.IO.Filesystem;
use Data.XML;

class Gtk3Binder {
	@filename : String;
	@enum_inventory : Set<String>;
	@record_inventory : Set<String>;
	@union_inventory : Set<String>;
	@klass_inventory : Set<String>;

	@is_debug : Bool;

	function : Main(args : String[]) ~ Nil {
		show_usage := true;

		if(args->Size() = 1) {
			Gtk3Binder->New(args[0])->Emit();
			show_usage := false;
		};

		if(show_usage) {
			"args: filename, [list|emit]"->ErrorLine();
		};
	}

	New(filename : String) {
		@filename := filename;

		@enum_inventory := Set->New()<String>;
		@record_inventory := Set->New()<String>;
		@union_inventory := Set->New()<String>;
		@klass_inventory := Set->New()<String>;

		@is_debug := true;
	}

	method : Emit() ~ Nil {
		# read file
		data := FileReader->ReadFile(@filename);
		data_size := data->Size() / 1000;
		data_size_str := data_size->ToCommaString();
		
		"[File: name='{$@filename}', size={$data_size_str} kb]\n---"->PrintLine();

		# parse xml
		timer := System.Time.Timer->New();
		timer->Start();

		parser := XmlParser->New(data);
		if(parser->Parse()) {
			Inventory(parser);
#~
			enums_xml := parser->FindElements("/repository/namespace/enumeration")<XmlElement>;
			if(enums_xml <> Nil) {
				LoadEnums(enums_xml);
			};

			bitfields_xml := parser->FindElements("/repository/namespace/bitfield")<XmlElement>;
			if(bitfields_xml <> Nil) {
				LoadEnums(bitfields_xml);
			};
~#	
			records_xml := parser->FindElements("/repository/namespace/record")<XmlElement>;
			if(records_xml <> Nil) {
				LoadRecords(records_xml);
			};
#~
			unions_xml := parser->FindElements("/repository/namespace/union")<XmlElement>;
			if(unions_xml <> Nil) {
				LoadEnums(unions_xml, GtkModel->Type->UNION);
			};

			classes_xml := parser->FindElements("/repository/namespace/class")<XmlElement>;
			if(classes_xml <> Nil) {
				LoadClasses(classes_xml, GtkModel->Type->CLASS);
			};
~#			
		}
		else {
			">>> Unable to parser XML <<<"->ErrorLine();
		};
		
		timer->End();
		String->SetFloatPrecision(3);
		tick_secs := timer->GetElapsedTime();
		"---\nTime={$tick_secs} sec(s)\n---"->PrintLine();
	}

	method : Inventory(parser : XmlParser) ~ Nil {
		
		
		enums_xml := parser->FindElements("/repository/namespace/enumeration")<XmlElement>;
		if(enums_xml <> Nil) {
			each(enum_xml := enums_xml) {
				@enum_inventory->Insert(enum_xml->GetAttribute("name")->GetValue());
			};
		};

		bitfields_xml := parser->FindElements("/repository/namespace/bitfield")<XmlElement>;
		if(bitfields_xml <> Nil) {
			each(bitfield_xml := bitfields_xml) {
				@enum_inventory->Insert(bitfield_xml->GetAttribute("name")->GetValue());
			};
		};

		unions_xml := parser->FindElements("/repository/namespace/union")<XmlElement>;
		if(unions_xml <> Nil) {
			each(union_xml := unions_xml) {
				@union_inventory->Insert(union_xml->GetAttribute("name")->GetValue());
			};
		};

		records_xml := parser->FindElements("/repository/namespace/record")<XmlElement>;
		if(records_xml <> Nil) {
			each(record_xml := records_xml) {
				@record_inventory->Insert(record_xml->GetAttribute("name")->GetValue());
			};
		};

		klasses_xml := parser->FindElements("/repository/namespace/klass")<XmlElement>;
		if(klasses_xml <> Nil) {
			each(klass_xml := klasses_xml) {
				@klass_inventory->Insert(klass_xml->GetAttribute("name")->GetValue());
			};
		};

		GtkType->Init(@enum_inventory, @union_inventory, @record_inventory, @klass_inventory);
	}

	method : LoadRecords(records_xml : Vector<XmlElement>) ~ Bool {
		each(record_xml := records_xml) {
			name_xml := record_xml->GetAttribute("name");
			ctype_xml := record_xml->GetAttribute("type");
			doc_xml := record_xml->GetFirstChild("doc");

			if(name_xml <> Nil & ctype_xml <> Nil & doc_xml <> Nil) {
				name_value := name_xml->GetValue();
				ctype_value := ctype_xml->GetValue();
				doc_value := doc_xml->GetContent();

				record := GtkRecord->New(name_value, ctype_value, doc_value);

				fields_xml := record_xml->GetChildren("field")<XmlElement>;
				each(field_xml := fields_xml) {
					is_writable := false;
					writable_attrib := field_xml->GetAttribute("writable");
					if(writable_attrib <> Nil & writable_attrib->GetValue()->Equals("1")) {
						is_writable := true;
					};

					type_xml := field_xml->GetFirstChild("type");
					if(type_xml <> Nil) {
						ctype_xml := type_xml->GetAttribute("type");
						name_xml := type_xml->GetAttribute("name");

						if(ctype_xml <> Nil) {
							ctype_value := ctype_xml->GetValue();
						}
						else {
							ctype_value := name_xml->GetValue();
						};

						name_value := field_xml->GetAttribute("name")->GetValue();	
						doc_xml := field_xml->GetFirstChild("doc");
						if(doc_xml <> Nil) {
							record->AddField(name_value, ctype_value, is_writable, doc_xml->GetContent());
						}
						else {
							record->AddField(name_value, ctype_value, is_writable);
						};
					};
				};

				record->EmitObjeck()->PrintLine();
				record->EmitCxx()->PrintLine();
				"---"->PrintLine();
			};
		};

		return false;
	}

	method : LoadEnums(enums_xml : Vector<XmlElement>) ~ Bool {
		each(enum_xml := enums_xml) {
			type_name := enum_xml->GetName();

			if(type_name->Equals("bitfield") | type_name->Equals("enumeration")) {
				name_xml := enum_xml->GetAttribute("name");
				ctype_xml := enum_xml->GetAttribute("type");
				doc_xml := enum_xml->GetFirstChild("doc");

				if(name_xml <> Nil & ctype_xml <> Nil & doc_xml <> Nil) {
					name_value := name_xml->GetValue();
					ctype_value := ctype_xml->GetValue();
					doc_value := doc_xml->GetContent();
					const_type := GtkConst->New(name_value, ctype_value, doc_value);

					members_xml := enum_xml->GetChildren("member")<XmlElement>;
					each(member_xml := members_xml) {
						member_name := member_xml->GetAttribute("name")->GetValue();
						member_type := member_xml->GetAttribute("identifier")->GetValue();
						member_value := member_xml->GetAttribute("value")->GetValue();

						const_type->AddConst(member_name, member_type, member_value->ToInt());
					};

					const_type->ToString()->PrintLine();
				};
			};
		};

		return false;
	}

	method : LoadClasses(enums_xml : Vector<XmlElement>) ~ Bool {
		return false;
	}
}