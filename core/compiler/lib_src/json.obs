#~~
# Json parsing library
# Copyright (c) 2012,2021 Randy Hollines
~~#

use Collection.Generic;

#~
Support for parsing JSON strings and documents (-lib json.obl)
~#
bundle Data.JSON {
	#~
	Table driven JSON builder
	~#
	class JsonBuilder {
		@stack : Stack<JsonElement>;

		#~
		Constructor
		~#
		New() {
			@stack := Stack->New()<JsonElement>;
		}

		#~
		Size of element stack
		~#
		method : public : Size() ~ Int {
			return @stack->Size();
		}

		#~
		Pushes an object element on the stack
		~#
		method : public : PushObject() ~ JsonBuilder {
			if(@stack->IsEmpty()) {
				@stack->Push(JsonElement->New(JsonType->OBJECT));
			}
			else if(@stack->Top()->GetType() = JsonType->ARRAY) {
				elem := JsonElement->New(JsonType->OBJECT);
				@stack->Top()->Add(elem);
				@stack->Push(elem);
			};

			return @self;
		}

		#~
		Pushes an object element on the stack
		@param name object name
		~#
		method : public : PushObject(name : String) ~ JsonBuilder {
			if(@stack->IsEmpty()) {
				@stack->Push(JsonElement->New(JsonType->OBJECT, name));
			}
			else if(@stack->Top()->GetType() = JsonType->ARRAY) {
				elem := JsonElement->New(JsonType->OBJECT);
				@stack->Top()->Add(elem);
				@stack->Push(elem);
			}
			else if(@stack->Top()->GetType() = JsonType->OBJECT) {
				elem := @stack->Top()->AddChild(name);
				@stack->Top()->Add(elem);
				@stack->Push(elem);
			};

			return @self;
		}

		#~
		Pushes an array element on the stack
		~#
		method : public : PushArray() ~ JsonBuilder {
			if(@stack->IsEmpty()) {
				@stack->Push(JsonElement->New(JsonType->ARRAY));
			}
			else {
				elem := JsonElement->New(JsonType->ARRAY);
				@stack->Top()->Add(elem);
				@stack->Push(elem);
			};

			return @self;
		}

		#~
		Pops a value
		@return popped element
		~#
		method : public : Pop() ~ JsonElement {
			if(@stack->IsEmpty()) {
				return Nil;
			};

			elem := @stack->Top();
			@stack->Pop();
			return elem;
		}

		#~
		Pop all values
		@return last popped element
		~#
		method : public : PopAll() ~ JsonElement {
			if(@stack->IsEmpty()) {
				return Nil;
			};

			while(@stack->Size() <> 1) {
				@stack->Pop();
			};

			elem := @stack->Top();
			@stack->Pop();
			return elem;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : String) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->ARRAY) {
				return Nil;
			};

			@stack->Top()->Add(value);
			return @self;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : Int) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->ARRAY) {
				return Nil;
			};

			@stack->Top()->Add(value);
			return @self;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : Float) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->ARRAY) {
				return Nil;
			};

			@stack->Top()->Add(value);
			return @self;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : Bool) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->ARRAY) {
				return Nil;
			};

			@stack->Top()->Add(value);
			return @self;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : JsonElement) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->ARRAY) {
				return Nil;
			};

			@stack->Top()->Add(value);
			return @self;
		}

		#~
		Insert an element
		@param name element name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : Int) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->OBJECT) {
				return Nil;
			};

			@stack->Top()->Insert(name, value);
			return @self;
		}

		#~
		Insert an element
		@param name element name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : Float) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->OBJECT) {
				return Nil;
			};
			
			@stack->Top()->Insert(name, value);
			return @self;
		}

		#~
		Insert an element
		@param name element name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : String) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->OBJECT) {
				return Nil;
			};
			
			@stack->Top()->Insert(name, value);
			return @self;
		}

		#~
		Insert an element
		@param name element name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : Bool) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->OBJECT) {
				return Nil;
			};
			
			@stack->Top()->Insert(name, value);
			return @self;
		}

		#~
		Insert an element
		@param name element name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : JsonElement) ~ JsonBuilder {
			if(@stack->IsEmpty() | @stack->Top()->GetType() <> JsonType->OBJECT) {
				return Nil;
			};
			
			@stack->Top()->Insert(name, value);
			return @self;
		}
	}

	#~
	Support for Json parsing
	~#
	class JsonParser {
		@input : String;
		@tokens : Vector<Token>;
		@token_pos : Int;
		@cur_token : Token;
		@root : JsonElement;
		@error_msg : String;
		
		#~
		Creates a new parser
		@param input Json input string
		~#
		New(input : String) {
			@input := input;
			@tokens : Nil;
			@token_pos := 0;
		}

		method : GetToken(pos : Int) ~ Token {
			if(pos < @tokens->Size()) {
				return @tokens->Get(pos)->As(Token);
			};
			
			return Token->New(Token->Type->EOS);
		}
		
		method : Match(token : Token->Type) ~ Bool {
			return @cur_token->GetType() = token;
		}

		#~
		Parses a Json text
		@param text JSON text
		@return root document element
		~#
		function : TextToElement(text : String) ~ JsonElement {
			parser := JsonParser->New(text);
			if(parser->Parse()) {
				return parser->GetRoot();
			};
	
			return Nil;
		}
		
		#~
		Parses a Json string
		@return true is successful, false otherwise
		~#
		method : public : Parse() ~ Bool {
			@tokens := Scan();
			if(@tokens->Size() > 0) {
# DumpTokens(@tokens);
				NextToken();
				@root := ParseValue();
				return @root <> Nil;
			};
			
			return false;
    	}

    	#~
		Get the current parsing error
		@return parsing error, Nil if none
		~#
		method : public : GetError() ~ String {
			return @error_msg;
		}

    	#~
		Get document root
		@return root Json element
		~#
		method : public : GetRoot() ~ JsonElement {
			return @root;
		}
		
		method : NextToken() ~ Nil {
			if(@token_pos < @tokens->Size()) {
				@cur_token := @tokens->Get(@token_pos)->As(Token);
				@token_pos += 1;
			}
			else {
				@cur_token := Token->New(Token->Type->EOS);
			};
		}
		
		method : ParseValue() ~ JsonElement {
			element : JsonElement;
			
			select(@cur_token->GetType()) {
				label Token->Type->OBR: {
# "array"->PrintLine();
					element := ParseArray();
				}

				label Token->Type->OCBR: {
# "object"->PrintLine();				
					element := ParseObject();						
				}

				label Token->Type->IDENT: {
					element := JsonElement->New(@cur_token->GetValue());
# Standard->Print("string=")->PrintLine(@cur_token->GetValue());
					NextToken();
					
				}
				
				label Token->Type->INT_NUM:
				label Token->Type->FLOAT_NUM: {
					element := JsonElement->New(JsonType->NUMBER, @cur_token->GetValue());
# Standard->Print("number=")->PrintLine(@cur_token->GetValue());
					NextToken();
				}
				
				label Token->Type->TRUE: {
					element := JsonElement->New(JsonType->TRUE);
# "true"->PrintLine();
					NextToken();
				}
				
				label Token->Type->FALSE: {
					element := JsonElement->New(JsonType->FALSE);
# "false"->PrintLine();
					NextToken();
				}
				
				label Token->Type->NULL: {
					element := JsonElement->New(JsonType->NULL);
# "null"->PrintLine();
					NextToken();
				}
				
				other: {
					@error_msg := @error_msg := "*** Unexpected value *** ";
# @cur_token->GetType()->As(Int)->PrintLine();				
					return Nil;
				}
			};
			
			return element;
		}

		method : ParseObject() ~ JsonElement {
			elements := Map->New()<String, JsonElement>;
			
			NextToken();
			while(@cur_token->GetType() <> Token->Type->EOS & 
				@cur_token->GetType() <> Token->Type->CCBR) {
				
				# parse name
				elem_name : Token;
				if(Match(Token->Type->IDENT) = false) {
					@error_msg := "*** Expected identifier *** ";
# @cur_token->GetType()->As(Int)->PrintLine();				
					return Nil;
				};			
				elem_name := @cur_token;
				NextToken();
				
				if(Match(Token->Type->COLON) = false) {
					@error_msg := "*** Expected ':' *** ";
# @cur_token->GetType()->As(Int)->PrintLine();				
					return Nil;
				};
				NextToken();
				
				# parse value
				value := ParseValue();
				if(value = Nil) {				
					return Nil;
				};
# IO.Standard->Print("insert=")->PrintLine(elem_name->GetValue());		
				elements->Insert(elem_name->GetValue(), value);				
				
				# update
				if(Match(Token->Type->COMMA)) {
					NextToken();
# "comma"->PrintLine();
				};
			};
			
			if(Match(Token->Type->CCBR) = false) {
				@error_msg := "*** Expected '}' *** ";
# @cur_token->GetType()->As(Int)->PrintLine();				
				return Nil;
			};
			NextToken();
			
			return JsonElement->New(elements);
		}

		method : ParseArray() ~ JsonElement {
			elements := Vector->New()<JsonElement>;
			
			NextToken();
			while(@cur_token->GetType() <> Token->Type->EOS & 
				@cur_token->GetType() <> Token->Type->CBR) {
				value := ParseValue();
				if(value = Nil) {
					return Nil;
				};
				elements->AddBack(value);
				
				# update
				if(Match(Token->Type->COMMA)) {
					NextToken();
# "comma"->PrintLine();
				};
			};
			
			if(Match(Token->Type->CBR) = false) {
				@error_msg := "*** Expected ']' *** ";
# @cur_token->GetType()->As(Int)->PrintLine();				
				return Nil;
			};
			NextToken();
			
			return JsonElement->New(elements);
		}

		# scan Json tokens
		method : native : Scan() ~ Vector<Token> {
			tokens := Vector->New()<Token>;
			
			if(@input <> Nil) {
				input := @input->Trim()->ToCharArray();
				index := 0;
				# TODO: add-in line numbers
				while(index < input->Size()) {
					# skip whitespace
					if(index < input->Size()) {
						char := input[index];
						while(index < input->Size() & (char = '\r' | char = '\n' | char = '\t' | char = ' ' | char = '\f' | char = '\v')) {
							char := input[++index];
						};
					};
					# comment
					if(index + 1 < input->Size() & input[index] = '/' & input[index + 1] = '/') {
						index += 2;
						while(index < input->Size() & (input[index] <> '\r' & input[index] <> '\n')) {
							# update
							index += 1;
						};
					}
					# token
					else {
						# parse string
						if(index < input->Size() & (input[index] = '"' | input[index]->IsChar())) {
							len : Int; offset : Int;
							if(input[index] = '"') {
								index += 1;
								offset := index;
								end := false;
								escape := false;
								while(index < input->Size() & end = false) {
									if(input[index] = '"' & escape = false) {
										# done
										end := true;
									}
									else if(input[index] = '\\') {
										escape := true;
										# update
										index += 1;
									}
									else {
										escape := false;
										# update
										index += 1;
									};
								};
								len := index - offset;
								# update
								index += 1;
							}
							else {				
								offset := index;
								while(index < input->Size() & (input[index]->IsChar() | input[index] = '_')) {
									# update
									index += 1;
								};
								len := index - offset;
							};
							
							if(len > 0) {
								string := @input->SubString(offset, len);								
								if(string->Equals("true")) {
									tokens->AddBack(Token->New(Token->Type->TRUE));
								}
								else if(string->Equals("false")) {
									tokens->AddBack(Token->New(Token->Type->FALSE));
								}
								else if(string->Equals("null")) {
									tokens->AddBack(Token->New(Token->Type->NULL));
								}
								else {							
									tokens->AddBack(Token->New(Token->Type->IDENT, string));
								};
							}
							else {
								tokens->AddBack(Token->New(Token->Type->IDENT, ""));
							};
						}
						# parse number
						else if(index < input->Size() & (input[index]->IsDigit() | input[index] = '-' | input[index] = '.')) {
							offset := index;
							minus_count := 0;
							dot_count := 0;

							# TODO: e digits
							while(index < input->Size() & 
								(input[index]->IsDigit() | input[index] = '-' | input[index] = '.' | input[index] = 'e' | input[index] = 'E')) {
								if(input[index] = '-') {
									minus_count += 1;
								}
								else if(input[index] = '.') {
									dot_count += 1;
								};
								# update
								index += 1;
							};
							len := index - offset;

							if(len > 0) {
								string := @input->SubString(offset, len);
								if(dot_count = 1 & minus_count < 2) {
									tokens->AddBack(Token->New(Token->Type->FLOAT_NUM, string));
								}
								else if(dot_count = 0 & minus_count < 2) {
									tokens->AddBack(Token->New(Token->Type->INT_NUM, string));
								}
								else {
									tokens->AddBack(Token->New(Token->Type->BAD_NUM, string));
								};
							}
							else {
								tokens->AddBack(Token->New(Token->Type->BAD_NUM, ""));
							};
						}
						# parse character
						else if(index < input->Size()) {
							select(input[index]) {
								label '[': {
									tokens->AddBack(Token->New(Token->Type->OBR));
								}
		
								label ']': {
									tokens->AddBack(Token->New(Token->Type->CBR));
								}

								label '{': {
									tokens->AddBack(Token->New(Token->Type->OCBR));
								}
		
								label '}': {
									tokens->AddBack(Token->New(Token->Type->CCBR));
								}

								label ',': {
									tokens->AddBack(Token->New(Token->Type->COMMA));
								}

								label ':': {
									tokens->AddBack(Token->New(Token->Type->COLON));
								}

								other: {
									tokens->AddBack(Token->New(Token->Type->OTHER));
# IO.Standard->Print("other 1=")->PrintLine(input[index]->As(Int));
								}
							};
							# update
							index += 1;
						};
					};
				};
			};
			
			return tokens;
	 	}

		method : DumpTokens(tokens : Vector<Token>) ~ Nil {
			if(tokens->Size() > 0) {
				each(i : tokens) {
					select(tokens->Get(i)->As(Token)->GetType()) {
						label Token->Type->IDENT: {
							IO.Standard->Print("token=IDENT, value=")->PrintLine(tokens->Get(i)->As(Token)->GetValue());
						}

						label Token->Type->INT_NUM: {
							IO.Standard->Print("token=INTEGER, value=")->PrintLine(tokens->Get(i)->As(Token)->GetValue());
						}

						label Token->Type->FLOAT_NUM: {
							IO.Standard->Print("token=FLOAT, value=")->PrintLine(tokens->Get(i)->As(Token)->GetValue());
						}

						label Token->Type->OBR: {
							"token='['"->PrintLine();
						}

						label Token->Type->CBR: {
							"token=']'"->PrintLine();
						}            

						label Token->Type->OCBR: {
							"token='{'"->PrintLine();
						}

						label Token->Type->CCBR: {
							"token='}'"->PrintLine();
						}

						label Token->Type->PREN: {
							"token='''"->PrintLine();
						}

						label Token->Type->COLON: {
							"token=':'"->PrintLine();
						}

						label Token->Type->COMMA: {
							"token=','"->PrintLine();
						}

						label Token->Type->OTHER: 
						label Token->Type->BAD_NUM: {
							"token=OTHER"->PrintLine();
						}						
					};
				};
			};
		}
	}

	#~
	JSON value element
	~#
	class JsonElement {
		@type : JsonType;
		@value : String;
		@array_elems : Vector<JsonElement>;
		@map_elems : Map<String, JsonElement>;

		#~
		JSON element type
		@class JsonElement
		~#
		enum JsonType {
			STRING,
			NUMBER,
			TRUE,
			FALSE,
			NULL,
			ARRAY,
			OBJECT,
			OTHER
		}
		
		#~
		Constructor
		@param type Json element type
		~#
		New(type : JsonElement->JsonType) {
			select(type) {
				label JsonElement->JsonType->TRUE {
					@type := type;	
					@value := "true";
				}

				label JsonElement->JsonType->FALSE {
					@type := type;	
					@value := "false";
				}

				label JsonElement->JsonType->NULL {
					@type := type;	
					@value := "";
				}

				label JsonElement->JsonType->OBJECT {
					@type := type;
					@map_elems := Map->New()<String, JsonElement>;
				}

				label JsonElement->JsonType->ARRAY {
					@type := type;
					@array_elems := Vector->New()<JsonElement>;
				}   

				other {
					@type := JsonElement->JsonType->OTHER;
				}
			};
		}
		
		#~
		Constructor
		@param type Json element type
		@param value Json string value
		~#
		New(type : JsonElement->JsonType, value : String) {
			if(type = JsonElement->JsonType->STRING | type = JsonElement->JsonType->NUMBER) {
				@type := type;	
				@value := value;				
			}
			else {
				@type := JsonElement->JsonType->OTHER;
			};
		}

		#~
		Constructor
		@param value Json string value
		~#
		New(value : String) {
			@type := JsonElement->JsonType->STRING;	
			@value := value;
		}
		
		#~
		Constructor
		@param value Json integer value
		~#
		New(value : Int) {
			@type := JsonElement->JsonType->NUMBER;	
			@value := value->ToString();
		}

		#~
		Constructor
		@param value Json float value
		~#
		New(value : Float) {
			@type := JsonElement->JsonType->NUMBER;	
			@value := value->ToString();
		}

		#~
		Constructor
		@param array_elems Json array value
		~#
		New(array_elems : Vector<JsonElement>) {
			@type := JsonElement->JsonType->ARRAY;
			@array_elems := array_elems;
		}
		
		#~
		Constructor
		@param map_elems Json object (names/values)
		~#
		New(map_elems : Map<String, JsonElement>) {
			@type := JsonElement->JsonType->OBJECT;
			@map_elems := map_elems;
		}
		
		#~
		Gets the type
		@return type
		~#
		method : public : GetType() ~ JsonElement->JsonType {
			return @type;
		}

		#~
		Checks for 'null' JSON value
		@return true if 'null', false otherwise
		~#
		method : public : IsNull() ~ Bool {
			return @type = JsonType->NULL;
		}
		
		#~
		Checks for 'null' JSON value
		@param elem element to check
		@return true if 'null', false otherwise
		~#
		function : public : IsNull(elem : JsonElement) ~ Bool {
			if(elem = Nil | elem->GetType() = JsonType->NULL) {
				return true;
			};
			
			return false;
		}

		#~
		Gets the string value
		@return string value
		~#
		method : public : GetValue() ~ String {
			return GetString();
		}

		#~
		Gets the string value
		@return string value
		~#
		method : public : GetString() ~ String {
			if(@value <> Nil) {
				return @value;
			};
			
			return "";
		}

		#~
		Gets the integer value
		@return integer value
		~#
		method : public : GetInt() ~ Int {
			if(@value <> Nil) {
				return @value->ToInt();
			};
			
			return 0;
		}

		#~
		Gets the float value
		@return float value
		~#
		method : public : GetFloat() ~ Int {
			if(@value <> Nil) {
				return @value->ToFloat();
			};
			
			return 0;
		}

		#~
		Gets the float value
		@return float value
		~#
		method : public : GetBool() ~ Bool {
			if(@value <> Nil) {
				return @value->ToBool();
			};
			
			return false;
		}
		
		#~
		Gets an indexed value from an array type
		@param index index
		@return indexed value
		~#
		method : public : Get(index : Int) ~ JsonElement {
			if(@array_elems <> Nil & index < @array_elems->Size()) {
				return @array_elems->Get(index);
			};
			
			return Nil;
		}

		#~
		Check to see if the indexed value exists
		@param index index
		@return true if exists, false otherwise
		~#
		method : public : Has(index : Int) ~ Bool {
			return Get(index) = Nil ? false : true;
		}

		#~
		Gets a named value from an object type
		@param name element name
		@return element value
		~#
		method : public : Get(name : String) ~ JsonElement {
			if(@map_elems <> Nil) {
				return @map_elems->Find(name);
			};
			
			return Nil;
		}

		#~
		Check to see if the indexed value exists
		@param name element name
		@return true if exists, false otherwise
		~#
		method : public : Has(name : String) ~ Bool {
			return Get(name) = Nil ? false : true;
		}
		
		#~
		Adds an element
		@param elem element
		@return true if successful, false otherwise
		~#
		method : public : Add(elem : JsonElement) ~ Bool {
			if(@type = JsonElement->JsonType->ARRAY) {
				if(elem = Nil) {
					@array_elems->AddBack(JsonElement->New(JsonElement->JsonType->NULL));
				}
				else {
					@array_elems->AddBack(elem);
				};

				return true;
			};

			return false;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : String) ~ Bool {
			if(@type = JsonElement->JsonType->ARRAY) {
				@array_elems->AddBack(JsonElement->New(value));
				return true;
			};

			return false;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : Int) ~ Bool {
			if(@type = JsonElement->JsonType->ARRAY) {
				@array_elems->AddBack(JsonElement->New(value));
				return true;
			};

			return false;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : Float) ~ Bool {
			if(@type = JsonElement->JsonType->ARRAY) {
				@array_elems->AddBack(JsonElement->New(value));
				return true;
			};

			return false;
		}

		#~
		Adds a value
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Add(value : Bool) ~ Bool {
			if(@type = JsonElement->JsonType->ARRAY) {
				if(value) {
					@array_elems->AddBack(JsonElement->New(JsonElement->JsonType->TRUE));
				}
				else {
					@array_elems->AddBack(JsonElement->New(JsonElement->JsonType->FALSE));
				};

				return true;
			};

			return false;
		}

		#~
		Adds a class child node
		@param name class name
		@return child class
		~#
		method : public : AddChild(name : String) ~ JsonElement {        
			if(@type = JsonElement->JsonType->OBJECT) {
				elem := JsonElement->New(JsonElement->JsonType->OBJECT);
				@map_elems->Insert(name, elem);

				return elem;
			};

			return Nil;
		}

		#~
		Insert an element
		@param name element name
		@param elem element
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, elem : JsonElement) ~ Bool {        
			if(@type = JsonElement->JsonType->OBJECT) {
				if(elem = Nil) {
					@map_elems->Insert(name, JsonElement->New(JsonElement->JsonType->NULL));
				}
				else {
					@map_elems->Insert(name, elem);
				};

				return true;
			};

			return false;
		}

		#~
		Inserts a value
		@param name value name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : String) ~ Bool {        
			if(@type = JsonElement->JsonType->OBJECT) {
				@map_elems->Insert(name, JsonElement->New(value));
				return true;
			};

			return false;
		}

		#~
		Inserts a value
		@param name value name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : Int) ~ Bool {        
			if(@type = JsonElement->JsonType->OBJECT) {
				@map_elems->Insert(name, JsonElement->New(value));
				return true;
			};

			return false;
		}

		#~
		Inserts a value
		@param name value name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : Float) ~ Bool {        
			if(@type = JsonElement->JsonType->OBJECT) {
				@map_elems->Insert(name, JsonElement->New(value));
				return true;
			};

			return false;
		}

		#~
		Inserts a value
		@param name value name
		@param value value
		@return true if successful, false otherwise
		~#
		method : public : Insert(name : String, value : Bool) ~ Bool {        
			if(@type = JsonElement->JsonType->OBJECT) {
				if(value) {
					@map_elems->Insert(name, JsonElement->New(JsonElement->JsonType->TRUE));
				}
				else {
					@map_elems->Insert(name, JsonElement->New(JsonElement->JsonType->FALSE));
				};

				return true;
			};

			return false;
		}
        
		#~
		General decoding for Json strings		
		@param str encoded string
		@return decoded string
		~#
		function : native : Decode(str : String) ~ String {
			buffer := String->New();

			each(i : str) {
				c := str->Get(i);
				if(c = '\\' & i + 1 < str->Size()) {
					i += 1;
					select(str->Get(i)) {
						label 'b': {
							buffer->Append(0x10->As(Char));
						}

						label 'f': {
							buffer->Append(0x14->As(Char));
						}

						label 'n': {
							buffer->Append('\n');
						}

						label 'r': {
							buffer->Append('\r');
						}

						label 't': {
							buffer->Append('\t');
						}

						label '\\': {
							buffer->Append('\\');
						}

						label '/': {
							buffer->Append('/');
						}

						label '"': {
							buffer->Append('"');
						}

						label 'u': {
							i += 1;
							if(i + 4 < str->Size()) {
								str_code := str->SubString(i, 4);
								if(<>str_code->IsEmpty()) {
									buffer->Append("0x{$str_code}"->ToInt()->As(Char));
								};
								i += 3;
							}
							else {
								buffer->Append(str->Get(i));
							};
						}
					};
				}
				else {
					buffer->Append(c);
				};
			};

			return buffer;
		}

		#~
		General encoding for JSON strings		
		@param str decoded string
		@return encoding string
		~#
		function : native : Encode(str : String) ~ String {
			buffer := String->New();

			each(i : str) {
				c := str->Get(i);

				if(c > 127) {
					str_hex := c->As(Int)->ToHexString();
					buffer->Append("\\u{$str_hex}");
				}
				else {
					select(c) {
						label 0x10: {
							buffer->Append("\\b");
						}

						label 0x14: {
							buffer->Append("\\f");
						}

						label '\n': {
							buffer->Append("\\n");
						}

						label '\r': {
							buffer->Append("\\r");
						}

						label '\t': {
							buffer->Append("\\t");
						}

						label '\\': {
							buffer->Append("\\\\");
						}

						label '/': {
							buffer->Append("\\/");
						}

						label '"': {
							buffer->Append("\\\"");
						}

						other: {
							buffer->Append(c);
						}
					};
				};
			};

			return buffer;
		}
		
		#~
		Gets the names of object attributes
		@return object attribute names
		~#
		method : public : GetNames() ~ Vector<String> {
			if(@map_elems <> Nil) {
				return @map_elems->GetKeys()<String>;
			};
			
			return Nil;
		}
		
		#~
		Gets the size of an array or object value
		@return size of an array or object value
		~#
		method : public : Size() ~ Int {
			if(@array_elems <> Nil) {
				return @array_elems->Size();
			};
			
			if(@map_elems <> Nil) {
				return @map_elems->Size();
			};
			
			return 0;
		}

		#~
		Checks a node's type
		@param elem element to check
		@param type type to check
		@return true of matching, false otherwise
		~#
		function : public : MatchType(elem : JsonElement, type : JsonElement->JsonType) ~ Bool {
			if(elem <> Nil & elem->GetType() = type) {
				return true;
			};
			
			return false;
		}
		
		#~
		Checks a node's type
		@param elem element to check
		@param value value to check
		@return true of matching, false otherwise
		~#
		function : public : MatchValue(elem : JsonElement, value : String) ~ Bool {
			if(elem <> Nil & elem->GetType() = JsonElement->JsonType->STRING & elem->GetValue()->Equals(value)) {
				return true;
			};
			
			return false;
		}
		
		#~
		Format the element into a JSON string
		@return JSON string
		~#
		method : public : ToString() ~ String {
			output := String->New();
			Format(output);		
			return output;
		}
		
		method : Format(output : String) ~ Nil {
			select(@type) {
				label JsonElement->JsonType->STRING: {
					if(@value <> Nil) {
						output->Append('"');
						output->Append(@value);
						output->Append('"');
					};
				}

				label JsonElement->JsonType->NUMBER: {
					if(@value <> Nil) {
						output->Append(@value);
					};
				}
				
				label JsonElement->JsonType->TRUE: {
					output->Append("true");
				}

				label JsonElement->JsonType->FALSE: {
					output->Append("false");
				}
				
				label JsonElement->JsonType->NULL: {
					output->Append("null");
				}

				label JsonElement->JsonType->ARRAY: {
					if(@array_elems <> Nil) {
						output->Append('[');
						each(i : @array_elems) {
							@array_elems->Get(i)->Format(output);
							if(i + 1 < @array_elems->Size()) {
								output->Append(',');
							};
						};
						output->Append(']');
					};
				}

				label JsonElement->JsonType->OBJECT: {
					if(@map_elems <> Nil) {
						output->Append('{');
						keys := @map_elems->GetKeys()<String>;
						each(i : keys) {
							key := keys->Get(i);
							value := @map_elems->Find(key);							
							output->Append('"');
							output->Append(key);
							output->Append("\":");
							value->As(JsonElement)->Format(output);
							if(i + 1 < @map_elems->Size()) {
								output->Append(',');
							};
						};
						output->Append('}');
					};
				}
			};
		}
		
		#~
		Queries the object graph. Object attributes referenced by '/' while array elements are referenced by '[index]'. Example "cars/[last]/make".
		@param path query path
		@return matching element
		~#
		method : public : FindElements(path : String) ~ JsonElement {					
			filters := path->Split("/");
			if(filters->Size() > 0) {
				element := @self;
				for(i := 0; i < filters->Size() & element <> Nil; i += 1;) {
					element := FindElement(filters[i], element);
				};
				
				return element;
			}
			else {
				return FindElement(path, @self);
			};
			
			return Nil;
		}
		
		method : FindElement(filter : String, node : JsonElement) ~ JsonElement {
			# array
			if(filter->StartsWith('[') & filter->EndsWith(']') & node->GetType() = JsonElement->JsonType->ARRAY) {
				end := filter->Find(']');
				index := filter->SubString(1, end - 1);
				
				offset : Int;
				if(index->Equals("first")) {
					offset := 0;
				}
				else if(index->Equals("last")) {
					offset := node->Size() - 1;
				}
				else {
					offset := index->ToInt();
				};
				
				if(offset > -1 & offset < node->Size()) {
					return node->Get(offset);
				};
			}
			# object
			else if(node->GetType() = JsonElement->JsonType->OBJECT) {
				return node->Get(filter);
			};
			
			return Nil;
		}
	}
	
	# token class
	class Token {
		@type : Token->Type;
		@value : String;
		
		enum Type {
			IDENT,
			INT_NUM,
			FLOAT_NUM,
			BAD_NUM,
			TRUE,
			FALSE,
			NULL,
			COMMA,
			OBR,
			CBR,
			OCBR,
			CCBR,
			PREN,
			COLON,
			OTHER,
			EOS
		}
		
		New(type : Token->Type, value : String) {
			@type := type;
			@value := value;
		}
			
		New(type : Token->Type) {
			@type := type;
			@value := Nil;
		}

		method : public : GetType() ~ Token->Type {
			return @type;
		}

		method : public : GetValue() ~ String {
			return @value;
		}
	}
}
