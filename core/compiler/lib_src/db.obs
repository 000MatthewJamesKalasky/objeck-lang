#~~
# Simple tabular query library
# Copyright (c) 2017-2018 Randy Hollines
~~#

use Collection;
use System.Time;

bundle Query {
	class Table {
		@name : String;
		@keys : IntMap;
		@column_names : String[];
		@column_map : StringMap;
		@head : Row;
		@tail : Row;
		@count : Int;
		
		New(name : String, column_names : String[]) {
			@column_map := StringMap->New();
			@keys := IntMap->New();
		
			@name := name;
			@column_names := String->New[column_names->Size() + 1];
			@column_names[0] := "pk";
			each(i : column_names) {
				@column_names[i + 1] := column_names[i];
				@column_map->Insert(@column_names[i + 1], IntHolder->New(i + 1));
			};
		}
		
		method : public : GetName() ~ String {
			return @name;
		}
		
		method : public : native : GetIndex(n : String) ~ Int {
			found := @column_map->Find(n);
			if(found <> Nil) {
				return found->As(IntHolder)->Get();
			};
			
			return -1;
		}
		
		method : public : native : All() ~ Vector {
			matches := Vector->New();
			
			row := @head;
			while(row <> Nil) {
				matches->AddBack(row);
				row := row->Next();
			};
			
			return matches;
		}
		
		method : public : GetColumns() ~ String[] {
			return @column_names;
		}
		
		method : public : native : Filter(cond : Conditional) ~ Vector {
			matches := Vector->New();
			
			row := @head;
			while(row <> Nil) {
				left := row->Get(cond->GetLeft());
				if(left <> Nil) {
					right := cond->GetValue();
					if(right = Nil) {
						right := row->Get(cond->GetRight());
					};
					select(cond->GetQualifier()) {
						label Conditional->Qualifier->EQUAL: {
							if(left->Compare(right) = 0) {
								matches->AddBack(row);
							};
						}
						
						label Conditional->Qualifier->NOT_EQUAL: {
							if(left->Compare(right) <> 0) {
								matches->AddBack(row);
							};
						}
						
						label Conditional->Qualifier->GREATER: {
							if(left->Compare(right) > 0) {
								matches->AddBack(row);
							};
						}
						
						label Conditional->Qualifier->LESS: {
							if(left->Compare(right) < 0) {
								matches->AddBack(row);
							};
						}
						
						label Conditional->Qualifier->GREATER_EQUAL: {
							if(left->Compare(right) >= 0) {
								matches->AddBack(row);
							};
						}
						
						label Conditional->Qualifier->LESS_EQUAL: {
							if(left->Compare(right) <= 0) {
								matches->AddBack(row);
							};
						}
						
						label Conditional->Qualifier->LIKE: {
						}
					};
				};
				row := row->Next();
			};
			
			return matches;
		}
		
		method : public : native : Insert() ~ Row {
			pk := IntHolder->New(@count);
			values := Compare->New[@column_names->Size()];
			values[0] := pk;
			row := Row->New(@self, values);
			@keys->Insert(pk->Get(), row);
			@count += 1;
			
			if(@head = Nil) {
				@head := row;
				@tail := row;
			}
			else {
				@tail->Next(row);
				row->Prev(@tail);
				@tail := row;
			};
			
			return row;
		}
		
		method : public : Delete(n : String) ~ Bool {
			pk := GetIndex(n);
			if(pk < 0) {
				return false;
			};
			
			return Delete(pk);
		}
		
		method : public : native : Delete(pk : Int) ~ Bool {
			row := @keys->Find(pk)->As(Row);
			if(row <> Nil) {
				if(row = @head) {
					@head := row->Next();
					row->Next(Nil);
					@head->Prev(Nil);
				}
				else if(row = @tail) {
					@tail := row->Prev();
					row->Prev(Nil);
					@tail->Next(Nil);
				}
				else {
					row->Prev()->Next(row->Next());
					row->Next()->Prev(row->Prev());
					row->Next(Nil);
					row->Prev(Nil);					
				};
				
				@keys->Remove(pk);
				return true;
			};
			
			return false;
		}
		
		method : public : Size() ~ Int {
			return @count;
		}
		
		method : public : ToString() ~ String {
			buffer := "";
			
			row := @head;
			while(row <> Nil) {
				buffer += row->ToString();
				buffer += "\r\n";
				row := row->Next();
			};
			
			return buffer;
		}
	}
	
	class Conditional {
		@left : Int;
		@right : Int;
		@qualifier : Conditional->Qualifier;
		@value : Compare;
		
		enum Qualifier {
			EQUAL,
			NOT_EQUAL,
			GREATER,
			LESS,
			GREATER_EQUAL,
			LESS_EQUAL,
			LIKE
		}
		
		New(left : Int, qualifier : Conditional->Qualifier, value : Compare) {
			@left := left;
			@right := -1;
			@qualifier := qualifier;
			@value := value;
		}
		
		New(left : Int, qualifier : Conditional->Qualifier, right : Int) {
			@left := left;
			@right := right;
			@qualifier := qualifier;
			@value := Nil;
		}
		
		method : public : GetQualifier() ~ Qualifier {
			return @qualifier;
		}
		
		method : public : GetValue() ~ Compare {
			return @value;
		}
		
		method : public : GetLeft() ~ Int {
			return @left;
		}
		
		method : public : GetRight() ~ Int {
			return @right;
		}
	}
	
	class Row {
		@values : Compare[];
		@table : Table;
		@next : Row;
		@prev : Row;
		
		New(table : Table, values : Compare[]) {
			@table := table;
			@values := values;
		}
		
		method : public : Get(n : String) ~ Compare {
			return Get(@table->GetIndex(n));
		}
		
		method : public : Set(n : String, value : Compare) ~ Bool {
			return Set(@table->GetIndex(n), value);
		}
		
		method : public : Get(i : Int) ~ Compare {
			if(i < 0 | i >= @values->Size()) {
				return Nil;
			};
			
			return @values[i];
		}
		
		method : public : Set(i : Int, value : Compare) ~ Bool {
			if(i < 0 | i >= @values->Size()) {
				return false;
			};
			
			@values[i] := value;
			return true;
		}
		
		method : public : Next() ~ Row {
			return @next;
		}
		
		method : public : Next(next : Row) ~ Nil {
			@next := next;
		}
		
		method : public : Prev() ~ Row {
			return @prev;
		}
		
		method : public : Prev(prev : Row) ~ Nil {
			@prev := prev;
		}
		
		method : public : Size() ~ Int {
			return @values->Size();
		}
		
		method : public : ToString() ~ String {
			buffer := "";

			each(i : @values) {
				value := @values[i];
				
				if(value <> Nil) {
					if(value->TypeOf(IntHolder)) {
						buffer += value->As(IntHolder)->Get()->ToString();
					}
					else if(value->TypeOf(FloatHolder)) {
						buffer += value->As(FloatHolder)->Get()->ToString();
					}
					else if(value->TypeOf(Date)) {
						buffer += value->As(Date)->ToString();
					}
					else if(value->TypeOf(String)) {
						buffer += value->As(String);
					}
					else {
						buffer += value->GetClass()->GetName();
					};
				}
				else {
						buffer += "<Nil>";
				};	
					
				if(i + 1 < @values->Size()) {
					buffer += ", ";
				};
			};
			
			return buffer;
		}
	}
	
	class Query {
		@parser : Parser;
		@terms : Term;
		@table : Table;
		@conds : Vector;
		@from_results : Vector;
		
		New(line : String, table : Table) {
			@table := table;
			@conds := Vector->New();
			@parser := Parser->New(line);
		}
		
		method : public : Query() ~ Table {
			expr := @parser->Parse();
			if(expr = Nil) {
"** ERROR PARSE **"->PrintLine();
				return Nil;
			};
			
			if(expr->GetType() = Token->Type->SELECT) {
				results := ProcessSelect(expr);
				if(results = Nil) {
"** ERROR SELECT **"->PrintLine();
					return Nil;
				};
				
				return results;
			}
			else if(expr->GetType() = Token->Type->FROM) {
				results := ProcessFrom(expr);
				if(results = Nil) {
"** ERROR FROM **"->PrintLine();
					return Nil;
				};
				
				names := @table->GetColumns();
				column_names := String->New[names->Size() - 1];
				each(i : column_names) {
					column_names[i] := names[i + 1];
				};
				
				return BuildTable("<rs>", column_names, results);				
			}
			else {
"** ERROR OTHER **"->PrintLine();
				return Nil;
			};
			
			return Nil;
		}
		
		method : ProcessSelect(expr : Term) ~ Table {
## "== Select =="->PrintLine();
			results := ProcessFrom(expr->GetLeft());
			if(results = Nil) {
				return Nil;
			};
			columns := expr->GetTerms();
			
			column_names := String->New[columns->Size()];
			each(i : columns) {
				column_names[i] := columns->Get(i)->As(Term)->GetValue()->As(String);
			};
			
			return BuildTable("<rs>", column_names, results);
		}
		
		method : BuildTable(name : String, column_names : String[], from_results : Vector) ~ Table {
			table := Table->New(name, column_names);
			each(i : from_results) {
				row := from_results->Get(i)->As(Row);
				result_row := table->Insert();
				each(j : column_names) {
					column_name := column_names[j];
					result_row->Set(column_name, row->Get(column_name));
				};
			};

			return table;
		}
		
		method : ProcessFrom(expr : Term) ~ Vector {
## "== From =="->PrintLine();
		
			name := expr->GetValue()->As(String);
			if(<>name->Equals(@table->GetName())) {
				return Nil;
			};
## name->PrintLine();			
			
			## where
			left := expr->GetLeft();
			if(left <> Nil & left->GetType() = Token->Type->WHERE) {
				return ProcessTerm(left->GetLeft());
			};
			
			return Nil;
		}
		
		method : Dump(results : Vector) ~ Nil {
			"## DUMP ##"->PrintLine();
			count := results->Size();
			"count={$count}"->PrintLine();
			each(i : results) {
				row := results->Get(i)->As(Row);
				row->ToString()->PrintLine();
			};
		}
				
		method : ProcessTerm(expr : Term) ~ Vector {
			left := expr->GetLeft();
			right := expr->GetRight();
			if(left = Nil | right = Nil) {
 				return Nil;
 			};
			
			select(expr->GetType()) {
				label Token->Type->AND: {
## "== And =="->PrintLine();		
					left_result := ProcessTerm(left);
					right_result := ProcessTerm(right);
					interm := MergeAnd(left_result, right_result);
## interm->Size()->PrintLine();
## "---"->PrintLine();				
						return interm;					
				}
				
				label Token->Type->OR: {
## "== Or =="->PrintLine();		
					left_result := ProcessTerm(left);
					right_result := ProcessTerm(right);
					interm := MergeOr(left_result, right_result);
## interm->Size()->PrintLine();
## "---"->PrintLine();									
						return interm;
				}
				
				label Token->Type->EQUAL: {					
## "== Equal =="->PrintLine();
## left->GetValue()->As(String)->PrintLine();
					index := @table->GetIndex(left->GetValue()->As(String));
 					if(index > -1) {
						interm := @table->Filter(Conditional->New(index, Conditional->Qualifier->EQUAL, right->GetValue()));
## interm->Size()->PrintLine();
## "---"->PrintLine();									
						return interm;
 					};
				}
				
				label Token->Type->NOT_EQUAL: {
## "== Not equal =="->PrintLine();
## left->GetValue()->As(String)->PrintLine();
					index := @table->GetIndex(left->GetValue()->As(String));
 					if(index > -1) {
						interm := @table->Filter(Conditional->New(index, Conditional->Qualifier->NOT_EQUAL, right->GetValue()));
## interm->Size()->PrintLine();
## "---"->PrintLine();					
						return interm;
					};
				}
			};
			
			return Nil;
		}
		
		method : MergeAnd(left : Vector, right : Vector) ~ Vector {
			merged := Vector->New();
			
			each(i : right) {
				right_value := right->Get(i);
				
				found := false;
				each(j : left) {
					left_value := left->Get(j);
					if(left_value = right_value) {
						found := true;
					};
				};
				
				if(found) {
					merged->AddBack(right_value);
				};
			};
			
			return merged;
		}
		
		method : MergeOr(left : Vector, right : Vector) ~ Vector {			
			each(i : right) {
				right_value := right->Get(i);
				
				not_found := false;
				each(j : left) {
					left_value := left->Get(j);
					if(left_value <> right_value) {
						not_found := true;
					};
				};
				
				if(not_found) {
					left->AddBack(right_value);
				};
			};
			
			return left;
		}
	}
	
	class Parser {
		@line : String;
		@tokens : Vector;
		@tokens_index : Int;
		@cur_token : Token;
		
		New(line : String) {
			@line := line;
		}
		
		method : public : Parse() ~ Term {
			@tokens := Scan();
			if(@tokens = Nil)	{
				return Nil;
			};			
			NextToken();
			
			if(TokenType() = Token->Type->SELECT) {
				NextToken();
				return ParseSelect();
			}
			else if(TokenType() = Token->Type->FROM) {
				NextToken();
				return ParseFrom();
			};
			
			return Nil;
		}

		method : ParseSelect() ~ Term {
## "--- Select ---"->PrintLine();		
			term := Term->New(Token->Type->SELECT);
			term->SetValue(TokenValue());
			
			do {
				if(TokenType() <> Token->Type->REF) {
					return Nil;
				};
				name := Term->New(Token->Type->REF);
				name->SetValue(TokenValue());
				term->AddTerm(name);
				NextToken();
				
				if(TokenType() = Token->Type->COMMA) {
					NextToken();
				};
			}
			while(TokenType() <> Token->Type->EOS & TokenType() <> Token->Type->FROM);

			if(TokenType() <> Token->Type->FROM) {
				return Nil;
			};
			
			NextToken();
			left := ParseFrom();
			if(left = Nil) {			
				return Nil;
			};
			term->SetLeft(left);
			
			return term;
		}

		method : ParseFrom() ~ Term {
## "--- From ---"->PrintLine();

			if(TokenType() <> Token->Type->REF) {
				return Nil;
			};
			term := Term->New(Token->Type->FROM);
			term->SetValue(TokenValue());
			NextToken();
			
			if(TokenType() = Token->Type->WHERE) {
				NextToken();
				left := ParseWhere();
				if(left = Nil) {
					return Nil;
				};
				term->SetLeft(left);
			};
			
			term->SetRight(Term->New(Token->Type->EOS));
			return term;
		}
		
		method : ParseWhere() ~ Term {
## "--- Where ---"->PrintLine();
			
			where := Term->New(Token->Type->WHERE);
			
			left := ParseLogic();
			do {
				type := TokenType();
				if(type = Token->Type->AND | type = Token->Type->OR) {
					term := Term->New(type);
					term->SetValue(TokenValue());
					NextToken();
					
					right := ParseTerm();
					if(right = Nil) {
						return Nil;
					};
					
					term->SetLeft(left);
					term->SetRight(right);
					left := term;
				};
			}
			while(TokenType() <> Token->Type->EOS);
			
			where->SetLeft(left);
			return where;
		}
		
		method : ParseLogic() ~ Term {
			left := ParseTerm();
			if(left = Nil) {
				return Nil;
			};
			
			type := TokenType();
			if(type = Token->Type->AND | type = Token->Type->OR) {
## "--- Logic ---"->PrintLine();
			
				term := Term->New(type);
				term->SetValue(TokenValue());
				NextToken();
				
				right := ParseTerm();
				if(right = Nil) {
					return Nil;
				};
				
				term->SetLeft(left);
				term->SetRight(right);
				return term;
			};
			
			return left;
		}
		
		method : ParseTerm() ~ Term {
## "--- Term ---"->PrintLine();
			
			left : Term;
			select(TokenType()) {
				label Token->Type->REF:
				label Token->Type->DATE:
				label Token->Type->INT:
				label Token->Type->FLOAT: {				
					left := Term->New(TokenType());
					left->SetValue(TokenValue());
					NextToken();
				}
				
				label Token->Type->OPRN: {
					NextToken();
					
					left := ParseLogic();
					if(left = Nil | TokenType() <> Token->Type->CPRN) {
						return Nil;
					};
					NextToken();
										
					return left;
				}
				
				other: {
					return Nil;
				}
			};
			
			term : Term;
			select(TokenType()) {
				label Token->Type->EQUAL:
				label Token->Type->NOT_EQUAL: {				
					term := Term->New(TokenType());
					term->SetValue(TokenValue());
					NextToken();
				}
				
				other: {
					return Nil;
				}
			};
			
			right : Term;			
			select(TokenType()) {
				label Token->Type->REF:
				label Token->Type->DATE:
				label Token->Type->INT:
				label Token->Type->FLOAT: {			
					right := Term->New(TokenType());					
					right->SetValue(TokenValue());
					NextToken();
				}
				
				other: {
					return Nil;
				}
			};
		
			term->SetLeft(left);
			term->SetRight(right);
			return term;
		}
		
		method : NextToken() ~ Bool {
			if(@tokens_index < @tokens->Size()) {
				@cur_token := @tokens->Get(@tokens_index);
				@tokens_index += 1;
				return true;
			};
			
			@cur_token := Nil;
			return false;
		}
		
		method : TokenType() ~ Token->Type {
			if(@cur_token <> Nil) {
				return @cur_token->GetType();
			};
			
			return Token->Type->EOS;
		}
		
		method : TokenValue() ~ Compare {
			if(@cur_token <> Nil) {
				return @cur_token->GetValue();
			};
			
			return "";
		}
		
		method : native : Scan() ~ Vector {		
			reserved := StringMap->New();
			reserved->Insert("select", Token->New(Token->Type->SELECT));
			reserved->Insert("update", Token->New(Token->Type->UPDATE));
			reserved->Insert("from", Token->New(Token->Type->FROM));
			reserved->Insert("where", Token->New(Token->Type->WHERE));
			reserved->Insert("insert", Token->New(Token->Type->INSERT));
			reserved->Insert("into", Token->New(Token->Type->INTO));
			reserved->Insert("values", Token->New(Token->Type->VALUES));
			reserved->Insert("like", Token->New(Token->Type->LIKE));
			reserved->Insert("between", Token->New(Token->Type->BETWEEN));
			reserved->Insert("in", Token->New(Token->Type->IN));
			reserved->Insert("and", Token->New(Token->Type->AND));
			reserved->Insert("or", Token->New(Token->Type->OR));
			
			i := 0;
			tokens := Vector->New();
			while(i < @line->Size()) {  			  
				# skip whitespace
				while(i < @line->Size() & (@line->Get(i) = ' ' | @line->Get(i) = '\t')) {
					i += 1;					
				};
				
				# parse the @line
				if(i < @line->Size()) {
					# string
					if(@line->Get(i) = '\'') {
						string := "";
						i += 1;
						while(i < @line->Size() & @line->Get(i) <> '\'') {
							string->Append(@line->Get(i));
							i += 1;
						};
						i += 1;
						tokens->AddBack(Token->New(string));
## string->PrintLine();						
					}
					
					# date
					else if(@line->Get(i) = '#') {
						i += 1;					
						string := "";
						while(i < @line->Size() & @line->Get(i)->IsDigit() | @line->Get(i) = '/') {
							string->Append(@line->Get(i));
							i += 1;
						};
					
						if(string->Size() = 10) {
							month := string->SubString(0, 2)->ToInt();
							day := string->SubString(3, 2)->ToInt();
							year := string->SubString(6, 4)->ToInt();
							
							date := Date->New(month, day, year, false);
							tokens->AddBack(Token->New(date));
## "date: {$month}/{$day}/{$year}"->PrintLine();													
						}
						else {
							return Nil;
						};						
					}
					
					# word
					else if(@line->Get(i)->IsChar() = true) {
						string := "";
						while(i < @line->Size() & @line->Get(i)->IsChar() | @line->Get(i) = '_') {
							string->Append(@line->Get(i));
							i += 1;
						};
					
						result := reserved->Find(string->ToLower())->As(Token);
						if(result <> Nil) {
							tokens->AddBack(result);
						}
						else {
							tokens->AddBack(Token->New(string));
						};
					}
					# number
					else if(@line->Get(i)->IsDigit() = true) {
						number := "";
						is_dec := 0;
						while(i < @line->Size() & @line->Get(i)->IsDigit() = true | @line->Get(i) = '.') {
							if(@line->Get(i) = '.') {
								is_dec += 1;
							};
							number->Append(@line->Get(i));
							i += 1;
						};

						if(is_dec = 1) {
							tokens->AddBack(Token->New(number->ToFloat()));
						}
						else if(is_dec = 0) {
							tokens->AddBack(Token->New(number->ToInt()));
						}
						else {
							return Nil;
						};
					}
					# other
					else {
						select(@line->Get(i)) {
							label '*': {
								tokens->AddBack(Token->New(Token->Type->STAR));
							}

							label ',': {
								tokens->AddBack(Token->New(Token->Type->COMMA));
							}
							
							label '(': {
								tokens->AddBack(Token->New(Token->Type->OPRN));
							}
							
							label ')': {
								tokens->AddBack(Token->New(Token->Type->CPRN));
							}
							
							label '=': {
								tokens->AddBack(Token->New(Token->Type->EQUAL));
							}
							
							label '<': {
								if(@line->Get(i + 1) = '>') {
									i += 1;
									tokens->AddBack(Token->New(Token->Type->NOT_EQUAL));
								}
								else if(@line->Get(i + 1) = '=') {
									i += 1;
									tokens->AddBack(Token->New(Token->Type->LESS_EQUAL));
								}
								else {
									tokens->AddBack(Token->New(Token->Type->LESS));
								};
							}
							
							label '>': {
								if(@line->Get(i + 1) = '=') {
									i += 1;
									tokens->AddBack(Token->New(Token->Type->GREATER_EQUAL));
								}
								else {
									tokens->AddBack(Token->New(Token->Type->GREATER));
								};
							}
              
							other: {
								return Nil;
							}
						};
						i += 1;
					};
				};			
			};			
			tokens->AddBack(Token->New(Token->Type->EOS));

			return tokens;
		}
	}
	
	class Term {
		@type : Token->Type;
		@oper : Conditional->Qualifier;
		@left : Term;
		@right : Term;
		@terms : Vector;
		@value : Compare;
		
		New(type : Token->Type) {
			@type := type;
		}
		
		method : public : GetType() ~ Token->Type {
			return @type;
		}
		
		method : public : SetType(type : Token->Type) ~ Nil {
			@type := type;
		}
		
		method : public : GetOperator() ~ Conditional->Qualifier {
			return @oper;
		}
		
		method : public : SetOperator(oper : Conditional->Qualifier) ~ Nil {
			@oper := oper;
		}
		
		method : public : SetValue(value : Compare) ~ Nil {
			@value := value;
		}
		
		method : public : GetValue() ~ Compare {
			return @value;
		}
		
		method : public : SetLeft(left : Term) ~ Nil {
			@left := left;
		}
		
		method : public : GetLeft() ~ Term {
			return @left;
		}
		
		method : public : SetRight(right : Term) ~ Nil {
			@right := right;
		}
		
		method : public : GetRight() ~ Term {
			return @right;
		}
		
		method : public : AddTerm(term : Term) ~ Nil {
			if(@terms = Nil) {
				@terms := Vector->New();
			};
			
			@terms->AddBack(term);
		}
		
		method : public : GetTerms() ~ Vector {
			return @terms;
		}
	}
	
	class Token {
		@type : Type;
		@value : Compare;
		
		enum Type {
			SELECT,
			UPDATE,
			FROM,
			WHERE,
			INSERT,
			INTO,
			VALUES,
			LIKE,
			BETWEEN,
			IN,
			AND,
			OR,
			STAR,
			COMMA,
			OPRN,
			CPRN,
			EQUAL,
			NOT_EQUAL,
			LESS_EQUAL,
			LESS,
			GREATER_EQUAL,
			GREATER,
			REF,
			OPER,
			INT,
			FLOAT,
			DATE,
			EOS
		}
		
		New(value : String) {
			@type := Type->REF;
			@value := value;
		}
		
		New(value : Date) {
			@type := Type->DATE;
			@value := value;
		}
		
		New(type : Type) {
			@type := type;
			@value := "";
		}
		
		New(value : Int) {
			@type := Type->INT;
			@value := IntHolder->New(value);
		}
		
		New(value : Float) {
			@type := Type->FLOAT;
			@value := FloatHolder->New(value);
		}
		
		method : public : GetType() ~ Type {
			return @type;
		}
		
		method : public : GetValue() ~ Compare {
			return @value;
		}

		method : public : ToString() ~ String {
			buffer := "";

			select(@type) {
				label Type->SELECT: {
					buffer += "SELECT";
				}

				label Type->UPDATE: {
					buffer += "UPDATE";
				}

				label Type->FROM: {
					buffer += "FROM";
				}

				label Type->WHERE: {
					buffer += "WHERE";
				}

				label Type->INSERT: {
					buffer += "INSERT";
				}

				label Type->INTO: {
					buffer += "INTO";
				}

				label Type->VALUES: {
					buffer += "VALUES";
				}

				label Type->LIKE: {
					buffer += "LIKE";
				}

				label Type->BETWEEN: {
					buffer += "BETWEEN";
				}

				label Type->IN: {
					buffer += "IN";
				}

				label Type->AND: {
					buffer += "AND";
				}

				label Type->OR: {
					buffer += "OR";
				}

				label Type->STAR: {
					buffer += "STAR";
				}

				label Type->COMMA: {
					buffer += "COMMA";
				}

				label Type->OPRN: {
					buffer += "OPRN";
				}
				
				label Type->CPRN: {
					buffer += "CPRN";
				}

				label Type->EQUAL: {
					buffer += "EQUAL";
				}

				label Type->NOT_EQUAL: {
					buffer += "NOT_EQUAL";
				}

				label Type->LESS_EQUAL: {
					buffer += "LESS_EQUAL";
				}

				label Type->LESS: {
					buffer += "LESS";
				}

				label Type->GREATER_EQUAL: {
					buffer += "GREATER_EQUAL";
				}

				label Type->GREATER: {
					buffer += "GREATER";
				}

				label Type->REF: {
					buffer += "REF, value='";
					buffer += @value->As(String);
					buffer += '"';
				}

				label Type->INT: {
					buffer += "INT, value=";
					buffer += @value->As(IntHolder)->Get();
				}

				label Type->FLOAT: {
					buffer += "FLOAT, value=";
					f := @value->As(FloatHolder);
					buffer += f->Get();
				}

				label Type->EOS: {
					buffer += "EOS";
				}
			};

			return buffer;
		}
	}
}
