#~~
# Objeck encryption routines 
# Copyright (c) 2023 Randy Hollines
# 
# credits/contributor:
# Greg Canty - Base64 support (http://www.the9375.com/cc/base64/base64.cpp.html)
# Copyright (c) 2012 Greg Canty  
~~#	

use System.API;

#~
Provides encryption and encoding functionality (-lib encrypt)
~#
bundle Encryption {
	class Proxy {
		@lib_proxy : static : DllProxy;
		
		function : GetDllProxy() ~ DllProxy {
			if(@lib_proxy = Nil) {
				@lib_proxy := DllProxy->New("libobjk_openssl");
			};

			return @lib_proxy;
		}
	}
	
	#~
	Support for one-way hashes
	~#
	class Hash {
		#~
		Hash input using SHA-256
		@param input bytes to hashed
		@return hashed bytes
		~#
		function : SHA256(input : Byte[]) ~ Byte[] {
			if(input <> Nil) {
				array_args := Base->New[2];
				array_args[0] := ByteArrayHolder->New(Nil->As(Byte[]));
				array_args[1] := ByteArrayHolder->New(input);				
				@lib_proxy := Proxy->GetDllProxy();
				@lib_proxy->CallFunction("openssl_hash_sha256", array_args);
				
				holder := array_args[0]->As(ByteArrayHolder);
				return holder->Get();
			};
			
			return Nil;
		}

		#~
		Hash input using SHA-512
		@param input bytes to hashed
		@return hashed bytes
		~#
		function : SHA512(input : Byte[]) ~ Byte[] {
			if(input <> Nil) {
				array_args := Base->New[2];
				array_args[0] := ByteArrayHolder->New(Nil->As(Byte[]));
				array_args[1] := ByteArrayHolder->New(input);				
				@lib_proxy := Proxy->GetDllProxy();
				@lib_proxy->CallFunction("openssl_hash_sha512", array_args);
				
				holder := array_args[0]->As(ByteArrayHolder);
				return holder->Get();
			};
			
			return Nil;
		}
		
		#~
		Hash input using MD5
		@param input bytes to hashed
		@return hashed bytes
		~#
		function : MD5(input : Byte[]) ~ Byte[] {
			if(input <> Nil) {
				array_args := Base->New[2];
				array_args[0] := ByteArrayHolder->New(Nil->As(Byte[]));
				array_args[1] := ByteArrayHolder->New(input);				
				@lib_proxy := Proxy->GetDllProxy();
				@lib_proxy->CallFunction("openssl_hash_md5", array_args);
				
				holder := array_args[0]->As(ByteArrayHolder);
				return holder->Get();
			};
			
			return Nil;
		}
		
		#~
		Hash input using RIPEMD digest
		@param input bytes to hashed
		@return hashed bytes
		~#
		function : RIPEMD160(input : Byte[]) ~ Byte[] {
			if(input <> Nil) {
				array_args := Base->New[2];
				array_args[0] := ByteArrayHolder->New(Nil->As(Byte[]));
				array_args[1] := ByteArrayHolder->New(input);				
				@lib_proxy := Proxy->GetDllProxy();
				@lib_proxy->CallFunction("openssl_hash_ripemd160", array_args);
				
				holder := array_args[0]->As(ByteArrayHolder);
				return holder->Get();
			};
			
			return Nil;
		}
	}
	
	#~
	Encrypts data
	~#
	class Encrypt {
		#~
		Encrypt input using AES-256
		@param key encryption key
		@param input bytes to encrypted
		@return encrypted bytes
		~#
		function : AES256(key : Byte[], input : Byte[]) ~ Byte[] {
			if(key <> Nil & input <> Nil) {
				array_args := Base->New[3];
				array_args[0] := ByteArrayHolder->New(Nil->As(Byte[]));
				array_args[1] := ByteArrayHolder->New(key);
				array_args[2] := ByteArrayHolder->New(input);				
				@lib_proxy := Proxy->GetDllProxy();
				@lib_proxy->CallFunction("openssl_encrypt_aes256", array_args);
				
				holder := array_args[0]->As(ByteArrayHolder);
				return holder->Get();
			};
			
			return Nil;
		}

		#~
		Encode input using Base64
		@param in bytes to encoded
		@return encoded bytes
		~#
		function : native : Base64(in : String) ~ String {
			array_args := Base->New[2];
			array_args[0] := Nil;
			array_args[1] := in;

			Proxy->GetDllProxy()->CallFunction("openssl_encrypt_base64", array_args);

			return array_args[0]->As(String);
		}
	}
	
	#~
	Decrypts data
	~#
	class Decrypt {
		#~
		Decrypts input using AES-256
		@param key encryption key
		@param input bytes to decrypted
		@return decrypted bytes
		~#
		function : AES256(key : Byte[], input : Byte[]) ~ Byte[] {
			if(key <> Nil & input <> Nil) {
				array_args := Base->New[3];
				array_args[0] := ByteArrayHolder->New(Nil->As(Byte[]));
				array_args[1] := ByteArrayHolder->New(key);
				array_args[2] := ByteArrayHolder->New(input);				
				@lib_proxy := Proxy->GetDllProxy();
				@lib_proxy->CallFunction("openssl_decrypt_aes256", array_args);
				
				holder := array_args[0]->As(ByteArrayHolder);
				return holder->Get();
			};
			
			return Nil;
		}

		#~
		Decrypts input using Base64
		@param in input bytes to decrypted
		@return decrypted bytes
		~#
		function : native : Base64(in : String) ~ String {
			array_args := Base->New[2];
			array_args[0] := Nil;
			array_args[1] := in;

			Proxy->GetDllProxy()->CallFunction("openssl_decrypt_base64", array_args);

			return array_args[0]->As(String);
		}
	}
}

