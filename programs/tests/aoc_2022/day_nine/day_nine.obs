use Collection;

class AoC {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			inputs := ReadInput(args[0])<String>;

			count := 0;

			each(i : inputs) {
				elves := inputs->Get(i)->Split(",");
			
				elf_one := elves[0];
				elf_one_range := elf_one->Split("-");
				elf_one_pair := Pair->New(elf_one_range[0]->ToInt(), elf_one_range[1]->ToInt())<IntRef, IntRef>;

				elf_two := elves[1];
				elf_two_range := elf_two->Split("-");
				elf_two_pair := Pair->New(elf_two_range[0]->ToInt(), elf_two_range[1]->ToInt())<IntRef, IntRef>;

				if((IsContained(elf_one_pair, elf_two_pair) | IsContained(elf_two_pair, elf_one_pair))) {
					count += 1;
					"(*)"->PrintLine();
				};

				"---"->PrintLine();
			};

			"===\n{$count}"->PrintLine();
		};
	}

	function : IsContained(elf_one_pair : Pair<IntRef, IntRef>, elf_two_pair : Pair<IntRef, IntRef>) ~ Bool {
		elf_one_start := elf_one_pair->GetFirst()->Get();
		elf_one_end := elf_one_pair->GetSecond()->Get();

		elf_two_start := elf_two_pair->GetFirst()->Get();
		elf_two_end := elf_two_pair->GetSecond()->Get();

		start_check := elf_two_start >= elf_one_start;
		end_check := elf_two_end <= elf_one_end;
		
"{$elf_one_start},{$elf_one_end};{$elf_two_start},{$elf_two_end}"->PrintLine();

		return start_check & end_check;
	}

	function : ReadInput(file : String) ~ Vector<String> {
		inputs := Vector->New()<String>;

		reader := System.IO.Filesystem.FileReader->New(file);
		leaving {
			reader->Close();
		};

		line := reader->ReadLine();
		while(line <> Nil) {
			if(<>line->IsEmpty()) {
				inputs->AddBack(line);
			};
			line := reader->ReadLine();
		};

		return inputs;
	}
}