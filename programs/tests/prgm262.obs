use Web.HTTP, Collection;

class Test {
	function : Main(args : String[]) ~ Nil {
		urls := Vector->New()<Url>;
		urls->AddBack(Url->New("https://archive.archlinux.org/iso/0.5/arch-0.5-full-i686.iso"));
		urls->AddBack(Url->New("https://archive.archlinux.org/iso/0.3/arch-0.3-full-i686.iso"));

		headers := Map->New()<String, String>;
		headers->Insert("accept", "application/json");
		headers->Insert("Keep-Alive", "timeout=600");

		downloads := WebDownloader->Download(urls, headers)<Pair<Url, ByteArrayRef>>;
		each(download := downloads) {
			url := download->GetFirst()->As(Url);
			data := download->GetSecond()->As(ByteArrayRef)->Get();

			file_name := GetFilename(url);
			file_size := data->Size();
			file_written := System.IO.Filesystem.FileWriter->WriteFile(file_name, data);
			"file: name='{$file_name}', size={$file_size}, written={$file_written}"->PrintLine();
		};
	}

	function : GetFilename(url : Url) ~ String {
		name := url->GetUrl();
		index := name->FindLast('/');
		if(index < 0) {
			index := name->FindLast('\\');
		};

		if(index > -1) {
			index += 1;
			return name->SubString(index, name->Size() - index);
		};

		return "";
	}
}