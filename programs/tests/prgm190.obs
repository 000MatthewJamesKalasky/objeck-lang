use Collection.Generic;
use System.IO.Filesystem;

class Test {
    @debug : static : Bool;

    function : Main(args : String[]) ~ Nil {
        @debug := true;

        if(args->Size() = 1) {
            multipart_content := ParseMultipartEncoding(FileReader->ReadBinaryFile(args[0]));
            if(multipart_content <> Nil) {
                each(i : multipart_content) {
                    content_part := multipart_content->Get(i);
                    fields := content_part->GetHeader("Content-Disposition")->Split(';');

                    if(fields->Size() = 3) {
                        file_name_value := fields[2]->Split('=');
                        if(file_name_value->Size() = 2) {                        
                            file_name := file_name_value[1]->RemoveAll('"');
                            System.IO.Filesystem.FileWriter->WriteFile("data/" + file_name, content_part->GetContent());
                        };
                    };
                };
            };
        };
    }

    function : ParseMultipartEncoding(encoded : Byte[]) ~ Vector<ContentType> {
        multipart_content := Vector->New()<ContentType>;

        # read encoding string
        marker_bytes := ReadLine(0, encoded);
        if(marker_bytes = Nil) {
            return Nil;
        };

        encoding_size := encoded->Size();
        if(@debug) {
            encoding_size_hex := encoding_size->ToHexString();   
            str_marker := marker_bytes->ToString();
            "Multipart: size=0x{$encoding_size_hex}({$encoding_size}), marker=|{$str_marker}|"->Print();
        };

        # fetch all markers
        marker_indexes := Byte->FindAll(encoded, marker_bytes);
        if(marker_indexes = Nil) {
            return Nil;
        };

        if(@debug) {
            num_markers := marker_indexes->Size();
            ", marker count={$num_markers}\n---"->PrintLine();
        };

        boundry_index := 0;
        for(i := 0; i < marker_indexes->Size() - 1; i += 1;) {
            if(@debug) {
                "[Part {$i}]"->PrintLine();
            };

            boundary_bytes := ReadLine(marker_indexes[i], encoded);
            if(boundary_bytes = Nil) {
                return Nil;
            };
            boundry_index += boundary_bytes->Size();

            # read white space
            if(<>MatchSpace(boundry_index, encoded)) {
                return Nil;
            };
            boundry_index += 2;
 
            # read content headers
            done := false;
            content_headers := Map->New()<String, String>;
            do {
                # read content line
                header_bytes := ReadLine(boundry_index, encoded);
                if(header_bytes <> Nil) {            
                    boundry_index += header_bytes->Size();
                    content_str := header_bytes->ToString();
                    
                    # parser header name/value pair
                    content_str_index := content_str->Find(':');
                    if(content_str_index > 0) {
                        header_name := content_str->SubString(content_str_index);
                        content_str_index += 2;
                        header_value := content_str->SubString(content_str_index, content_str->Size() - content_str_index);
                        
                        if(@debug) {
                            "header: name=|{$header_name}|, value=|{$header_value}|"->PrintLine();
                        };

                        if(<>header_name->IsEmpty() & <>header_value->IsEmpty()) {
                            content_headers->Insert(header_name, header_value);
                        };
                    };
                }
                else {
                    done := true;
                };

                # read white space
                if(<>MatchSpace(boundry_index, encoded)) {
                    return Nil;
                };
                boundry_index += 2;
            }
            while(<>done);
            
            # read content
            content_size := marker_indexes[i + 1] - boundry_index - 2;
            if(@debug) {
                "body: start={$boundry_index}, length={$content_size}"->Print();
            };

            content_bytes := Byte->New[content_size];
            if(Runtime->Copy(content_bytes, 0, encoded, boundry_index, content_size)) {
                content_str := String->New(content_bytes);
                ", content: |{$content_str}|"->PrintLine();
            };
            boundry_index += content_bytes->Size();

            # read white space
            if(<>MatchSpace(boundry_index, encoded)) {
                return Nil;
            };
            boundry_index += 2;

            if(@debug) {
                boundry_index_hex := boundry_index->ToHexString();
                "ending=0x{$boundry_index_hex}"->PrintLine();
                "---"->PrintLine();
            };

            multipart_content->AddBack(ContentType->New(content_headers, content_bytes));
        };

        # read ending boundary
        boundary_bytes := ReadLine(marker_indexes[marker_indexes->Size() - 1], encoded);
        if(boundary_bytes = Nil) {
            return Nil;
        };
        boundry_index += boundary_bytes->Size();

        # read white space
        if(<>MatchSpace(boundry_index, encoded)) {
            return Nil;
        };
        boundry_index += 2;

        boundary_str := boundary_bytes->ToString();
        if(boundary_str->EndsWith("--") & encoding_size = boundry_index) {
            if(@debug) {
                boundry_index_hex := boundry_index->ToHexString();
                "Fin. ending=0x{$boundry_index_hex}"->PrintLine();
            };

            return multipart_content;
        };

        return Nil;
    }

    function : ReadLine(offset : Int, encoded : Byte[]) ~ Byte[] {
        i := offset;
        if(i < encoded->Size()) {
            done := false;
            read := 0;
            while(<>done & i < encoded->Size()) {
                byte := encoded[i++];
                if(byte = '\r') {
                    done := true;
                }
                else {
                    read += 1;
                };
            };

            line := Byte->New[read];
            if(Runtime->Copy(line, 0, encoded, offset, read)) {
                return line;
            };
        };

        return Nil;
    }

    function : MatchSpace(offset : Int, encoded : Byte[]) ~ Bool {
        if(offset + 1 < encoded->Size()) {
            return encoded[offset] = '\r' & encoded[offset + 1] = '\n';
        };

        return false;
    }

    function : ParseUrlEncoding(encoded : String) ~ Map<String, String> {
        decodings := Map->New()<String, String>;

        pairs :=  encoded->Split('&');
        if(pairs <> Nil) {
            each(i : pairs) {
                key_value := pairs[i]->Split('=');
                if(key_value <> Nil & key_value->Size() = 2) {
                    marker_bytes := Web.HTTP.Url->Decode(key_value[0])->ReplaceAll('+', ' ');
                    value := Web.HTTP.Url->Decode(key_value[1])->ReplaceAll('+', ' ');
                    decodings->Insert(marker_bytes, value);
                };
            };
        };

        return decodings;
    }
}

class ContentType {
    @content_headers : Map<String, String>;
    @content : Byte[];

    New(content_headers :  Map<String, String>, content : Byte[]) {
        @content_headers := content_headers;
        @content := content;
    }

    method : public : GetHeader(name : String) ~ String {    
        return @content_headers->Find(name);
    }

    method : public : GetContent() ~ Byte[] {
        return @content;
    }
}