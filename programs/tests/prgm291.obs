#~
Original implementation by rishabmalhdijo 
~#
class MinHeap { 
    @heap : Int[]; 
    @max : Int;
    @size : Int; 
    
    New(n : Int) { 
        @max := n; 
        @heap :=Int->New[@max]; 
        @size := 0; 
    }

    function : Main(args : String[]) ~ Nil {
        h := MinHeap->New(11); 
        h->Insert(1); 
        h->Insert(3); 
        h->Insert(6); 
        h->Insert(5); 
        h->Insert(9); 
        h->Insert(8); 
        
        Standard->Print(h->Pop())->Print(' ');
        Standard->Print(h->Peek())->PrintLine(' ');
        
        h->Decrease(0, 2);

        h->Peek()->PrintLine(); 
        Standard->Print(h->Pop())->Print(' ');
        Standard->Print(h->Pop())->Print(' ');
    }

    # Swapping using reference  
    method : Swap(arr : Int[], a : Int, b : Int) ~ Nil { 
        temp := arr[a]; 
        arr[a] := arr[b]; 
        arr[b] := temp; 
    } 

    # Get the GetParent index for the given index 
    method : GetParent(index : Int) ~ Int { 
        return (index - 1) / 2; 
    } 
      
    # Get the GetLeft Child index for the given index 
    method : GetLeft(index : Int) ~ Int {
        return 2 * index + 1; 
    } 
      
    # Get the GetRight Child index for the given index 
    method : GetRight(index : Int) ~ Int { 
        return 2 * index + 2; 
    }

    # Inserts a new index 
    method : public : Insert(index : Int) ~ Bool { 
        if(@size = @max) {
            # heap is full 
            return false; 
        } 
      
        # First insert the new index at the end  
        i := @size; 
        @heap[i] := index; 
        @size++; 
          
        # Fix the min heap property if it is violated  
        while(i <> 0 &  @heap[i] < @heap[GetParent(i)]) { 
            Swap(@heap, i, GetParent(i)); 
            i := GetParent(i); 
        } 
        return true; 
    }


    # Decreases value of given index to value.  
    # It is assumed that value is smaller  
    # than @heap[index].  
    method : Decrease(index : Int, value : Int) ~ Nil { 
        if(value < @heap[index]) {
            @heap[index] := value; 
      
            while(index <> 0 & @heap[index] < @heap[GetParent(index)]) { 
                Swap(@heap, index, GetParent(index)); 
                index := GetParent(index); 
            } 
        }
    }

    # Returns the minimum index (index at 
    # root) from min heap  
    method : public : Peek() ~ Int { 
        return @heap[0]; 
    } 
      
    # A recursive method to heapify a subtree with the root at given index 
    # This method assumes that the subtrees are already heapified 
    method : native : MinHeapify(i : Int) ~ Nil { 
        l := GetLeft(i); 
        r := GetRight(i);
        smallest := i; 

        if(l < @size & @heap[l] < @heap[i]) {
            smallest := l; 
        }
        else if(r < @size & @heap[r] < @heap[smallest]) {
            smallest := r;
        }

        if(smallest <> i) { 
            Swap(@heap, i, smallest); 
            MinHeapify(smallest); 
        } 
    } 
      
    # Method to remove minimum element  
    # (or root) from min heap  
    method : public : Pop() ~ Int { 
        if(@size <= 0) { 
            return Int->MaxSize(); 
        } 
        else if(@size = 1) { 
            @size--; 
            return @heap[0]; 
        } 
          
        # Store the minimum value,  
        # and remove it from heap  
        root := @heap[0]; 
  
        @heap[0] := @heap[@size - 1]; 
        @size--; 
        MinHeapify(0); 

        return root; 
    } 

    # This function deletes index at the  
    # given index. It first reduced value  
    # to minus infinite, then calls extractMin() 
    method : Delete(index : Int) ~ Nil {
        Decrease(index, Int->MinSize()); 
        Pop(); 
    }

    # Increases value of given index to value. 
    # It is assumed that value is greater  
    # than @heap[index].  
    # Heapify from the given index 
    method : public : Increase(index : Int, value : Int) ~ Nil {
        if(value > @heap[index]) {
            @heap[index] := value; 
            MinHeapify(index);
        };
    } 
      
    # Changes value on a index 
    method : public : Update(index : Int, value : Int) ~ Nil { 
        if(@heap[index] = value) { 
            return; 
        } 
        
        if(@heap[index] < value) { 
            Increase(index, value); 
        } 
        else { 
            Decrease(index, value); 
        } 
    } 
}