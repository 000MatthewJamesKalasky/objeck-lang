class vList {
    @base : vSeg;
    @offset : Int;

    New() {}

    New(base : vSeg, offset : Int) {
        @base := base;
        @offset := offset;
    }

    New(base : vSeg) {
        @base := base;
    }

    method : public : GetBase() ~ vSeg {
        return @base;
    }

    method : public : GetOffset() ~ Int {
        return @offset;
    }

    method : public : Cons(a : String) ~ vList {
        if(@base = Nil) {
            s := vSeg->New(a);
            return vList->New(s);
        }
        else if(@offset = 0) {
            l2 := @base->GetEle()->Size() * 2;
            ele := String->New[l2];
            ele[l2 - 1] := a;
            s := vSeg->New(@base, ele);
            return vList->New(s, l2 - 1);
        }
        else {
            @offset -= 1;
            ele := @base->GetEle();
            ele[@offset] := a;
            return @self;
        };
    }

    method : public : Cdr() ~ vList {
        Runtime->Assert(@base <> Nil);

        @offset += 1;
        if(@offset < @base->GetEle()->Size()) {
            return @self;
        }
        else {
            return vList->New(@base->GetNext(), 0);
        };
    }

    method : public : ToString() ~ String {
        if(@base = Nil) {
            return "[]";
        };

        return "";
    }
}
 
class vSeg {
    @next : vSeg;
    @ele : String[];

    New(next : vSeg, ele : String[]) {
        @next := next;
        @ele := ele;
    }

    New(s : String) {
        @ele := String->New[1];
        @ele[0] := s;
    }

    method : public : GetNext() ~ vSeg {
        return @next;
    }

    method : public : GetEle() ~ String[] {
        return @ele;
    }
}

class Test {
    function : PrintStructure(v : vList) ~ Nil {
        offset := v->GetOffset();
        "  offset: {$offset}"->PrintLine();
        
        for(sg := v->GetBase(); sg <> Nil; sg := sg->GetNext();) {
            values := sg->GetEle();
            "  ["->Print();
            each(i : values) {
                value := values[i];
                if(value <> Nil) {
                    "{$value}"->Print();
                }
                else {
                    "{Nil}"->Print();
                };

                if(i + 1 < values->Size()) {
                    ", "->Print();
                };
            };
            "]"->PrintLine();
        };
        ""->PrintLine();
    }

    function : Main(args : String[]) ~ Nil {
        v := vList->New();
        "zero value for type. empty vList: {$v}"->PrintLine();
        PrintStructure(v);

        for(a := '6'; a >= '1'; a -=1;) {
            v := v->Cons("{$a}");
        };
        "demonstrate cons. 6 elements added: {$v}"->PrintLine();
        PrintStructure(v);

        v := v->Cdr();
        "demonstrate cdr. 1 element removed: {$v}"->PrintLine();
        PrintStructure(v);
    }
}