use Game.SDL2;
use Game.Framework;

class Game {
	@framework : GameFramework;
	@quit : Bool;
	@gotchi : Tamagotchi;
	@hungry_img : ImageSprite;
	@bored_img : ImageSprite;
	@poop_img : ImageSprite;
	@sleep_img : ImageSprite;
	@status : Rectangle;
	@action : Rectangle;
	@action_count : Int;

	New() {
		@framework := GameFramework->New(Meta->SCREEN_WIDTH, Meta->SCREEN_HEIGHT, "Tamagotchi");
		@status := @framework->AddRectangle(32, 64);
		@action := @framework->AddRectangle(32, 64);
		@hungry_img := @framework->AddImageSprite("img/hungry.png");
		@bored_img := @framework->AddImageSprite("img/bored.png");
		@poop_img := @framework->AddImageSprite("img/poop.png");
		@sleep_img := @framework->AddImageSprite("img/sleep.png");
	}

	function : Main(args : String[]) ~ Nil {
		game := Game->New();
		game->Run();
	}

	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			@gotchi := Tamagotchi->New(@self);
			@status->SetFill(true);
			@action->SetFill(true);

			e := @framework->GetEvent();
			count := 0;
			while(<>@quit & @gotchi->IsAlive()) {
				Start();
			
				Input(e);
				if(count = Meta->TICKS) {
					@gotchi->Update();
					count := 0;
				};

				Draw();				
				count += 1;
				End();
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	method : Input(e : Event) ~ Nil {
		# process input
		while(e->Poll() <> 0) {
			if(e->GetType() = EventType->SDL_QUIT) {
				@quit := true;
			}
			else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				select(e->GetKey()->GetKeysym()->GetScancode()) {
					label Scancode->SDL_SCANCODE_F: {
						@gotchi->Input('f');
					}

					label Scancode->SDL_SCANCODE_P: {
						@gotchi->Input('p');
					}

					label Scancode->SDL_SCANCODE_C: {
						@gotchi->Input('c');
					}
				};
			};
		};
	}

	method : public : Draw() ~ Nil {
		select(@gotchi->GetState()) {
			label Tamagotchi->States->HUNGRY: {
				@hungry_img->Render();	
			}

			label Tamagotchi->States->BORED: {
				@bored_img->Render();	
			}

			label Tamagotchi->States->POOP: {
				@poop_img->Render();	
			}

			label Tamagotchi->States->SLEEP: {
				@sleep_img->Render();	
			}
		};

		neglect := @gotchi->GetNeglect();
		if(neglect < 1.0) {
			@status->SetColor(Color->New(0, 255, 0));
		}
		else if(neglect < 2.0) {
			@status->SetColor(Color->New(255, 255, 0));
		}
		else {
			@status->SetColor(Color->New(255, 0, 0));
		};
		@status->Render(192, 80);

		action := @gotchi->GetAction();
		if(action < 0) {
			@action->SetColor(Color->New(255, 0, 0));
		}
		else if(action > 0) {
			@action->SetColor(Color->New(0, 255, 0));
		}
		else {
			@action->SetColor(Color->New(192, 192, 192));
		};
		@action->Render(16, 80);
	}

	method : public : Update() ~ Nil {
		
	}

	method : Start() ~ Nil {
		@framework->FrameStart();
		@framework->Clear();
	}

	method : End() ~ Nil {
		@framework->Show();
		@framework->FrameEnd();
	}
}

class Tamagotchi  {
	@state : Int;
	@state_labels : String[];
	@age : Int;
	@hour : Int;
	@neglect : Float;
	@game : Game;
	@action : Int;

	enum States {
		HUNGRY, 
		BORED,
		POOP,
		SLEEP
	}

	New(game : Game) {
		@game := game;
		@state_labels := ["Hungry", "Bored", "Poop!", "Sleep..."];
		Update();
	}

	method : public : GetAction() ~ Int {
		if(@action < 0) {
			@action += 1;
		}
		else if(@action > 0) {
			@action -= 1;
		};

		return @action;
	}

	method : public : GetState() ~ Int {	
		return @state;
	}

	method : public : GetNeglect() ~ Float {	
		return @neglect;
	}

	method : public : Update() ~ Nil {
		NextState();
		ShowNeed();
		NextHour();				
	}

	method : ShowNeed() ~ Nil {
		state_lablel := @state_labels[@state];
		"{$state_lablel}, Neglect={$@neglect} [hour={$@hour}, age={$@age}]"->PrintLine();
	}

	method : public : IsAlive() ~ Bool {
		return @age < 4 & @neglect < 3.0;
	}

	method : public : Input(action : Char) ~ Nil {
		select(action) {
			label 'f': {
				if(@state = States->HUNGRY) {
					@neglect -= .5;
					@action := 15;
				}
				else {
					@action := -15;
				};
			}

			label 'p': {
				if(@state = States->BORED) {
					@neglect -= .25;
					@action := 15;
				}
				else {
					@action := -15;
				};
			}

			label 'c': {
				if(@state = States->POOP) {
					@neglect -= .75;
					@action := 15;
				}
				else {
					@action := -15;
				};
			}
		};
	}

	method : NextState() ~ Nil {
		if(<>IsAwake()) {
			@state := States->SLEEP;
			@neglect -= .1;
		}
		else {
			@state := Int->Random(States->SLEEP);
			select(@state) {
				label States->HUNGRY: {
					@neglect += .5;
				}

				label States->BORED: {
					@neglect += .25;
				}

				label States->POOP: {
					@neglect += .75;
				}
			};
		};

		if(@neglect < 0.0) {
			@neglect := 0.0;
		};
	}

	method : IsAwake() ~ Bool {
		return @hour > 7 & @hour < 23;
	}

	method : NextHour() ~ Nil {
		@hour += 1;
		if(@hour = 24) {
			@hour := 0;
			@age += 1;
		};
	}
}

consts Meta {
	SCREEN_WIDTH := 240,
	SCREEN_HEIGHT := 160,
	TICKS := 240
}