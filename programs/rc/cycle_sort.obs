class Test {
	function : Main(args : String[]) ~ Nil {
		arr := [5, 0, 1, 2, 2, 3, 5, 1, 1, 0, 5, 6, 9, 8, 0, 1];
		arr->ToString()->PrintLine();
		writes := CycleSort(arr);
		"writes: {$writes}"->PrintLine();
		arr->ToString()->PrintLine();		
	}
	
	function : CycleSort(a : Int[]) ~ Int {
		writes := 0;
 
		for(cycleStart := 0; cycleStart < a->Size() - 1; cycleStart+=1;) {
			val := a[cycleStart];
 
			pos := cycleStart;
			for(i := cycleStart + 1; i < a->Size(); i+=1;) {
				if(a[i] < val) {
					pos++;
				};
			};
		
			if(pos <> cycleStart) {
				while(val = a[pos]) {
					pos+=1;
				};
		
				tmp := a[pos];
				a[pos] := val;
				val := tmp;
				writes+=1;

				while(pos <> cycleStart) {
					pos := cycleStart;
					for(i := cycleStart + 1; i < a->Size(); i+=1;) {
						if(a[i] < val) {
							pos+=1;
						};
					};
		
					while(val = a[pos]) {
						pos++;
					};
		
					tmp := a[pos];
					a[pos] := val;
					val := tmp;
					writes++;
				};
			};
		};
		
		return writes;
	}
} 
		