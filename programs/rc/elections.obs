use Collection.Generic;
use Data.CSV;

class Test {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			input := System.IO.File.FileReader->ReadFile(args[0]);
			table := CsvTable->New(input, "\n");

			sorter := Sorter->New();
			if(table->IsParsed()) {
				records := CompareVector->New()<Record>;
				each(i : table) {
					row := table->Get(i);
					year := row->Get("year")->ToInt();
					state := row->Get("state");
					state_po := row->Get("state_po");
					candidate := row->Get("candidate");
					candidate_votes := row->Get("candidatevotes")->ToInt();
					total_votes := row->Get("totalvotes")->ToInt();
					records->AddBack(Record->New(year, state, state_po, candidate, candidate_votes, total_votes, sorter));
				};

				sorter->SetSortBy(Sorter->SortBy->CANDIDATE_VOTES);
				records->Sort();
				each(i : records) {
					records->Get(i)->ToString()->PrintLine();
				};
			};
		};
	}
}

class Sorter {
	@sort_by : SortBy;

	enum SortBy {
		CANDIDATE_VOTES,
		TOTAL_VOTES,
		STATE
	}

	New() {
		@sort_by := SortBy->STATE;
	}

	method : public : GetSortBy() ~ SortBy {
		return @sort_by;
	}

	method : public : SetSortBy(sort_by : SortBy) ~ Nil {
		@sort_by := sort_by;
	}
}

class Record implements Compare {
	@year : Int;
	@state : String;
	@state_po : String;
	@candidate : String;
	@candidate_votes : Int;
	@total_votes : Int;
	@sorter : Sorter;

	New(year : Int, state : String, state_po : String, candidate : String, candidate_votes : Int, total_votes : Int, sorter : Sorter) {
		@year := year;
		@state := state;
		@state_po := state_po;
		@candidate := candidate;
		@candidate_votes := candidate_votes;
		@total_votes := total_votes;
		@sorter := sorter;
	}

	method : GetTotalVotes() ~ Int {
		return @total_votes;
	}

	method : GeCandidateVotes() ~ Int {
		return @candidate_votes;
	}

	method : GetState() ~ String {
		return @state;
	}

	method : public : Clone() ~ Compare {
		return @self;
	}

	method : public : Compare(rhs : Compare) ~ Int {
		# check class type
		if(GetClassID() <> rhs->GetClassID()) {
			return -1;
		};

		record := rhs->As(Record);
		if(@sorter->GetSortBy() = SortBy->CANDIDATE_VOTES) {
			return @candidate_votes->Compare(record->GeCandidateVotes());
		}
		else if(@sorter->GetSortBy() = SortBy->TOTAL_VOTES) {
			return @total_votes->Compare(record->GetTotalVotes());
		}
		else if(@sorter->GetSortBy() = SortBy->STATE) {
			return @state->Compare(record->GetState());
		};

		return -1;
	}

	method : public : HashID() ~ Int {
		return ToString()->HashID();
	}

	method : public : ToString() ~ String {
		return "{$@year}|{$@state}|{$@state_po}|{$@candidate}|{$@candidate_votes}|{$@total_votes}";
	}
}