use Game.SDL2;
use Collection;
use Game.Framework;

class Game {
	@gotchi : Tamagotchi;

	enum Faces {
		BORED,
		POOP,
		HUNGRY,
		HAPPY,
		OK,
		SAD,
		SLEEP
	}

	enum DayTime {
		MORNING,
		EVENING,
		NIGHT,
		DAY
	}

	@framework : GameFramework;
	@quit : Bool;

	@faces : AnimatedImageSprite;
	@face_clips : Vector;

	@tod : AnimatedImageSprite;
	@tod_clips : Vector;

	@action_chunk : MixChunk;
	@sleep_chunk : MixChunk;
	@eat_chunk : MixChunk;
	@play_chunk : MixChunk;
	@clean_chunk : MixChunk;

	@age_text : TextSprite;
	@age : Int;

	@wait : Int;

	New(wait : Int) {
		@framework := GameFramework->New(Meta->SCREEN_WIDTH, Meta->SCREEN_HEIGHT, "Tamagotchi");
		@framework->SetClearColor(Color->New(240, 248, 255));
		@wait := wait * @framework->GetFps() * 60; # minutes

		@face_clips := Vector->New();
		@face_clips->AddBack(Rect->New(0, 0, 240, 160));
		@face_clips->AddBack(Rect->New(240, 0, 240, 160));
		@face_clips->AddBack(Rect->New(480, 0, 240, 160));
		@face_clips->AddBack(Rect->New(720, 0, 240, 160));
		@face_clips->AddBack(Rect->New(960, 0, 240, 160));
		@face_clips->AddBack(Rect->New(1200, 0, 240, 160));
		@face_clips->AddBack(Rect->New(1440, 0, 240, 160));
		@faces := @framework->AddAnimatedImageSprite("media/faces.png", @face_clips);

		@tod_clips := Vector->New();
		@tod_clips->AddBack(Rect->New(0, 0, 48, 48));
		@tod_clips->AddBack(Rect->New(48, 0, 48, 48));
		@tod_clips->AddBack(Rect->New(96, 0, 48, 48));
		@tod_clips->AddBack(Rect->New(144, 0, 48, 48));
		@tod := @framework->AddAnimatedImageSprite("media/tod.png", @tod_clips);
		@tod->SetScale(0.5);

		@action_chunk := MixChunk->New("media/action.wav");
		@sleep_chunk := MixChunk->New("media/sleep.wav");
		@eat_chunk := MixChunk->New("media/eat.wav");
		@play_chunk := MixChunk->New("media/play.wav");
		@clean_chunk := MixChunk->New("media/clean.wav");

		@age_text := @framework->AddTextSprite();
		@age_text->RenderedText("Age: 0");
	}

	function : Main(args : String[]) ~ Nil {
		wait : Int;
		if(args->Size() = 1) {
			wait := args[0]->ToInt();
			if(wait = 0) {
				wait := Meta->WAIT;
			};
		}
		else {
			wait := Meta->WAIT;
		};

		game := Game->New(wait);
		game->Run();
	}

	method : Run() ~ Nil {
		leaving {
			@action_chunk->Free();
			@sleep_chunk->Free();
			@eat_chunk->Free();
			@play_chunk->Free();
			@clean_chunk->Free();
			@framework->Quit();
		};

		if(@framework->IsOk()) {
			@gotchi := Tamagotchi->New(@self);
						
			e := @framework->GetEvent();
			count := 0;
			while(<>@quit & @gotchi->IsAlive()) {
				Start();
			
				Input(e);
				if(count = @gotchi->GetWait()) {
					@gotchi->Update();
					count := 0;
				};
				Draw();
				count += 1;

				End();
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};
	}

	method : public : GetWait() ~ Int {
		return @wait;
	}

	method : public : ActionSound() ~ Nil {
		@action_chunk->PlayChannel(-1, 0);
	}

	method : public : SleepSound() ~ Nil {
		@sleep_chunk->PlayChannel(-1, 0);
	}

	method : public : EatSound() ~ Nil {
		@eat_chunk->PlayChannel(-1, 0);
	}

	method : public : PlaySound() ~ Nil {
		@play_chunk->PlayChannel(-1, 0);
	}

	method : public : CleanSound() ~ Nil {
		@clean_chunk->PlayChannel(-1, 0);
	}

	method : Input(e : Event) ~ Nil {
		# process input
		while(e->Poll() <> 0) {
			if(e->GetType() = EventType->SDL_QUIT) {
				@quit := true;
			}
			else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				select(e->GetKey()->GetKeysym()->GetScancode()) {
					label Scancode->SDL_SCANCODE_F: {
						@gotchi->Input('f');
					}

					label Scancode->SDL_SCANCODE_P: {
						@gotchi->Input('p');
					}

					label Scancode->SDL_SCANCODE_C: {
						@gotchi->Input('c');
					}
				};
			};
		};
	}

	method : public : Draw() ~ Nil {
		action := @gotchi->GetAction();
		neglect := @gotchi->GetNeglect();
		
		if(@gotchi->GetState() = Tamagotchi->States->SLEEP) {
			@faces->Render(0, 40, Faces->SLEEP);
		}	
		else if(action) {
			select(@gotchi->GetState()) {
				label Tamagotchi->States->HUNGRY: {
					@faces->Render(0, 40, Faces->HUNGRY);
				}

				label Tamagotchi->States->BORED: {
					@faces->Render(0, 40, Faces->BORED);
				}

				label Tamagotchi->States->POOP: {
					@faces->Render(0, 40, Faces->POOP);
				}
			};
		}
		else if(neglect < 1.0) {
			@faces->Render(0, 40, Faces->HAPPY);
		}
		else if(neglect < 2.0) {
			@faces->Render(0, 40, Faces->OK);		}
		else {
			@faces->Render(0, 40, Faces->SAD);
		};

		age := @gotchi->GetAge();
		buffer := "Age: ";
		buffer += age;
		@age_text->RenderedText(buffer);
		@age_text->Render(10, 10);

		hour := @gotchi->GetHour();
		if(hour >= 6 & hour <= 10) {
			@tod->Render(208, 10, DayTime->MORNING);
		}
		else if(hour >= 10 & hour <= 18) {
			@tod->Render(208, 10, DayTime->DAY);
		}
		else if(hour >= 18 & hour <= 20) {
			@tod->Render(208, 10, DayTime->EVENING);
		}
		else {
			@tod->Render(208, 10, DayTime->NIGHT);
		};
	}

	method : Start() ~ Nil {
		@framework->FrameStart();
		@framework->Clear();
	}

	method : End() ~ Nil {
		@framework->Show();
		@framework->FrameEnd();
	}
}

class Tamagotchi  {
	@state : Int;
	@age : Int;
	@hour : Int;
	@neglect : Float;
	@game : Game;
	@action : Bool;
	@wait : Int;

	enum States {
		HUNGRY, 
		BORED,
		POOP,
		SLEEP
	}

	New(game : Game) {
		@game := game;
		@hour := Int->Random(24);
		Update();
	}

	method : public : GetHour() ~ Int {
		return @hour;
	}

	method : public : GetWait() ~ Int {
		return @wait;
	}

	method : public : GetAge() ~ Int {
		return @age;
	}

	method : public : GetAction() ~ Bool {
		return @action;
	}

	method : public : GetState() ~ Int {	
		return @state;
	}

	method : public : GetNeglect() ~ Float {	
		return @neglect;
	}

	method : public : Update() ~ Nil {
		NextState();
		NextHour();	
	}

	method : public : IsAlive() ~ Bool {
		return @age < 4 & @neglect < 3.0;
	}

	method : public : Input(action : Char) ~ Nil {
		select(action) {
			label 'f': {
				if(@state = States->HUNGRY) {
					@neglect -= .6;
					@game->EatSound();
				};
				@action := false;
			}

			label 'p': {
				if(@state = States->BORED) {
					@neglect -= .35;
					@game->PlaySound();
				};
				@action := false;
			}

			label 'c': {
				if(@state = States->POOP) {
					@neglect -= .85;
					@game->CleanSound();
				};
				@action := false;
			}
		};
	}

	method : NextState() ~ Nil {
		@state := Int->Random(States->SLEEP);
		if(<>IsAwake() | @state = States->SLEEP) {
			@state := States->SLEEP;
			@neglect -= .1;
			@action := false;

			@game->SleepSound();
		}
		else {
			select(@state) {
				label States->HUNGRY: {
					@neglect += .5;
					@action := true;
				}

				label States->BORED: {
					@neglect += .25;
					@action := true;
				}

				label States->POOP: {
					@neglect += .75;
					@action := true;
				}
			};

			@game->ActionSound();
		};

		if(@neglect < 0.0) {
			@neglect := 0.0;
		};
		# "hour={$@hour}, neglect={$@neglect}"->PrintLine();
	}

	method : IsAwake() ~ Bool {
		return @hour > 7 & @hour < 23;
	}

	method : NextHour() ~ Nil {
		@hour += 1;
		if(@hour = 24) {
			@hour := 0;
			@age += 1;
		};
		wait := @game->GetWait();
		@wait := Int->Random(wait - wait / 3, wait + wait / 3);
	}
}

consts Meta {
	SCREEN_WIDTH := 240,
	SCREEN_HEIGHT := 200,
	WAIT := 5,
	BEAT := 30
}