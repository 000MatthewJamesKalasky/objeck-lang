use Collection.Generic;
use Data.JSON;
use Web.HTTP;

#~
URL shortner

- curl -k -X POST https://localhost:60013 -H "Content-Type: application/json" -d "{ \"long\": \"https://www.cockroachlabs.com/docs/stable/build-a-go-app-with-cockroachdb.html\" }"
- objeck_lsp.obe objk_apis.json 6013 debug
- ..\..\..\..\objeck-lsp\server
~#
class Test {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 4) {
			cert := args[0];
			cert_key := args[1];
			cert_key_passwd := args[2];
			port := args[3]->ToInt();
			
			MyHandler->Init();
			WebServer->ServeSecure(MyHandler->New()->GetClass(), port, cert, cert_key, cert_key_passwd, true);
		}
		else if(args->Size() = 1) {
			port := args[0]->ToInt();

			MyHandler->Init();
			WebServer->Serve(MyHandler->New()->GetClass(), port, true);
		};
	}
}

#~
My handler
~#
class MyHandler from HttpRequestHandler {
	@url_mapping : static : Hash<String, String>;

	function : Init() ~ Nil {
		@url_mapping := Hash->New()<String, String>;
	}

	New() {
		Parent();
	}

	method : ProcessGet(request_url : String, request_headers : Map<String, String>, response_headers : Map<String, String>) ~ Response {
		request_url->PrintLine();

		long_url := @url_mapping->Find(request_url);
		if(long_url <> Nil) {
			response_headers->Insert("Content-type", "application/json");
			response := "{ \"long\": \"{$long_url}\" }"->ToByteArray();
			return Response->New(200, response);
		};

		return Response->New(404);
	}
	
	method : ProcessPost(buffer : Byte[], request_url : String, request_headers : Map<String, String>, response_headers : Map<String, String>) ~ Response {
		response : Byte[];
		
		json := JsonParser->New(String->New(buffer));
		if(json->Parse()) {
			url_json := json->GetRoot()->Get("long");
			long_url := url_json->GetValue();
			long_url->PrintLine();

			post_fix := "/";
			each(i : 6)	{
				post_fix += Int->Random('a', 'z')->As(Char);
			};

			short_url := "http://localhost:60013{$post_fix}";
			@url_mapping->Insert(post_fix, long_url);

			response_headers->Insert("Content-type", "application/json");
			response := "{ \"short\": \"{$short_url}\" }"->ToByteArray();
		};

		return Response->New(200, response);
	}
}