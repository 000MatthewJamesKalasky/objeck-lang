#~
Lesson 38
http://lazyfoo.net/tutorials/SDL/38_particle_engines/index.php
~#

use Game.SDL2;

class LTexture {
	@renderer : Renderer;
	@font : Font;
	@texture : Texture;
	@render_quad : Rect;

	New(renderer : Renderer, font : Font := Nil) {
		@renderer := renderer;
		@font := font;
		@render_quad := Rect->New();
	}

	method : public : LoadFromRenderedText(textureText : String, textColor : Color) ~ Bool {
		Free();

		# Render text surface
		textSurface := @font->RenderTextSolid(textureText, textColor);
		if(textSurface->IsNull()) {
			"Unable to render text surface!"->ErrorLine();
			return false;
		};

		texture := textSurface->CreateTexture(@renderer);
		@render_quad->SetW(textSurface->GetW());
		@render_quad->SetH(textSurface->GetH());
		
		@texture := texture;
		return @texture <> Nil;
	}

	# Loads image at specified path
	method : public : LoadFromFile(path : String) ~ Bool {
		Free();

		# Load image at specified path
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return false;
		};

		loadedSurface->SetColorKey(true, loadedSurface->GetPixelFormat()->MapRGB(0, 0xFF, 0xFF));

		texture := loadedSurface->CreateTexture(@renderer);
		if(texture->IsNull()) {
			"Unable to create texture!"->ErrorLine();
			return false;
		};
		@render_quad->SetW(loadedSurface->GetW());
		@render_quad->SetH(loadedSurface->GetH());
		
		loadedSurface->Free();

		@texture := texture;
		return @texture <> Nil;
	}

	# Deallocates texture
	method : public : Free() ~ Nil {
		if(@texture <> Nil)	{
			@texture->Destroy();
		};
	}

	method : public : SetColor(red : Int, green : Int, blue : Int) ~ Nil {
		@texture->SetColorMod(red, green, blue);
	}

	method : public : SetBlendMode(blending : BlendMode) ~ Nil {
		# Set blending function
		@texture->SetBlendMode(blending);
	}
	
	method : public : SetAlpha(alpha : Int) ~ Nil {
		# Modulate texture alpha
		@texture->SetAlphaMod(alpha );
	}

	# Renders texture at given point
	method : public : Render(x : Int, y : Int, clip : Rect := Nil, 
			angle : Float := 0.0, center : Point := Nil, flip : RendererFlip := RendererFlip->SDL_FLIP_NONE) ~ Nil {
		@render_quad->SetX(x);
		@render_quad->SetY(y);

		if(clip <> Nil)	{
			@render_quad->SetW(clip->GetW());
			@render_quad->SetH(clip->GetH());
		};

		@renderer->CopyEx(@texture, clip, @render_quad, angle, center, flip);
	}

	# Gets image dimensions
	method : public : GetWidth() ~ Int {
		return @render_quad->GetW();
	}

	method : public : GetHeight() ~ Int {
		return @render_quad->GetH();
	}
}

class Particle {
	# Offsets
	@posX : Int;
	@posY : Int;

	# Current frame of animation
	@frame : Int;

	# Type of particle
	@texture : LTexture;
	@dot_texture : static : LTexture;
	@redTexture : static : LTexture;
	@greenTexture : static : LTexture;
	@blueTexture : static : LTexture;
	@shimmerTexture : static : LTexture;

	function : Init(redTexture : LTexture, greenTexture : LTexture, 
			blueTexture : LTexture, shimmerTexture : LTexture ) ~ Nil {
		@redTexture := redTexture;
		@greenTexture := greenTexture;
		@blueTexture := blueTexture;
		@shimmerTexture := shimmerTexture;
	}

	# Initialize position and animation
	New(  x : Int, y : Int ) {
		# Set offsets
	    @posX := x - 5 + ( rand() % 25 );
	    @posY := y - 5 + ( rand() % 25 );

	    # Initialize animation
	    @frame := rand() % 5;

	    # Set type
	    select( rand() % 3 ) {
	        label 0: { @texture := @redTexture; }
	        label 1: { @texture := @greenTexture; }
	        label 2: { @texture := @blueTexture; }
	    };
	}

	method : rand() ~ Int {
		return Int->Random(0, 32767);
	}

	# Shows the particle
	method : public : Render() ~ Nil {
		# Show image
		@texture->Render( @posX, @posY );

	    # Show shimmer
	    if( @frame % 2 = 0 ) {
			@shimmerTexture->Render( @posX, @posY );
	    };

	    # Animate
	    @frame++;
	}

	# Checks if particle is dead
	method : public : IsDead() ~ Bool {
		return @frame > 10;
	}
}

class Dot {
	@dot_texture : LTexture;
	@posX : Int;
	@posY : Int;
	@velX : Int; 
	@velY : Int;
	@particles : Particle[];

	New(x : Int, y : Int, dot_texture : LTexture) {
		# Initialize the offsets
	    @posX := x;
	    @posY := y;
	    @dot_texture := dot_texture;

	    # Initialize the velocity
	    @velX := 0;
	    @velY := 0;

	    # Initialize particles
	    @particles := Particle->New[Consts->TOTAL_PARTICLES];
	    for( i := 0; i < Consts->TOTAL_PARTICLES; i++; ) {
	        @particles[ i ] := Particle->New( @posX, @posY );
	    };
	}

	# Takes key presses and adjusts the dot's velocity
	method : public : HandleEvent(e : Event) ~ Nil {
		# If a key was pressed
		if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
	        # Adjust the velocity
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_UP: {
	            	@velY -= Consts->DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_DOWN: {
	            	@velY += Consts->DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@velX -= Consts->DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@velX += Consts->DOT_VEL;
	            }        	            
	        };
	    }
	    # If a key was released
	    else if(e->GetType() = EventType->SDL_KEYUP & e->GetKey()->GetRepeat() = 0) {
	        # Adjust the velocity
	        select(e->GetKey()->GetKeysym()->GetScancode()) {
	        	label Scancode->SDL_SCANCODE_UP: {
	            	@velY += Consts->DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_DOWN: {
	            	@velY -= Consts->DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@velX += Consts->DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@velX -= Consts->DOT_VEL;
	            }
	        };
	    };   
	}

	method : public : GetPosX() ~ Int {
		return @posX;
	}

	method : public : GetPosY() ~ Int {
		return @posY;
	}

	# Moves the dot
	method : public : Move() ~ Nil {
		# Move the dot left or right
	    @posX += @velX;

	    # If the dot went too far to the left or right
	    if( ( @posX < 0 ) | ( @posX + @dot_texture->GetWidth() > Consts->SCREEN_WIDTH ) ) {
	        # Move back
	        @posX -= @velX;
	    };

	    # Move the dot up or down
	    @posY += @velY;

	    # If the dot went too far up or down
	    if( ( @posY < 0 ) | ( @posY + @dot_texture->GetHeight() > Consts->SCREEN_HEIGHT ) ) {
	        # Move back
	        @posY -= @velY;
	    };
	}

	# Show the dot
	method : public : Render() ~ Nil {
		# Show the dot
		@dot_texture->Render( @posX, @posY );

		# Show particles on top of dot
		RenderParticles();
	}

	method : RenderParticles() ~ Nil {
		# Go through particles
	    for( i := 0; i < Consts->TOTAL_PARTICLES; ++i; ) {
	        # Delete and replace dead particles
	        if( @particles[ i ]->IsDead() ) {
	            @particles[ i ] := Particle->New( @posX, @posY );
	        };
	    };

	    # Show particles
	    for( i := 0; i < Consts->TOTAL_PARTICLES; ++i; ) {
	        @particles[ i ]->Render();
	    };
	}

	method : public : Free() ~ Nil {
		@dot_texture->Free();
	}
}

class Test {
	@window : static : Window;
	@renderer : static : Renderer;
	@dot_texture : static : LTexture;
	@dot : static : Dot;
	@redTexture : static : LTexture;
	@greenTexture : static : LTexture;
	@blueTexture : static : LTexture;
	@shimmerTexture : static : LTexture;
	
	function : Main(args : String[]) ~ Nil {
		# Start up SDL and create window
		if( <>Init() ) {
			"Failed to initialize!"->ErrorLine();
			return;
		};
				
		# Load media
		if( <>LoadMedia() ) {
			"Failed to load media!"->ErrorLine();
			return;
		};		

		FRAME_RATE := 1000 / Consts->FPS;
					
		# Main loop flag
		quit := false;

		# Event handler
		e := Event->New();

		# While application is running
		while(<>quit) {
			start := Timer->GetTicks();

			# Handle events on queue
			while( e->Poll() <> 0 ) {
				# User requests quit
				if(e->GetType() = EventType->SDL_QUIT) {
					quit := true;
				};

				@dot->HandleEvent( e );
			};

			# Move the dot and check collision
			@dot->Move();

			# Clear screen
			@renderer->SetDrawColor(0xFF, 0xFF, 0xFF, 0xFF);
			@renderer->Clear();

			# Render objects
			@dot->Render();

			# Update Consts
			@renderer->Present();

			end := start - Timer->GetTicks();
			if(end < FRAME_RATE) {
				Timer->Delay(FRAME_RATE - end);
			};
		};

		e->Free();
		Close();
	}
	
	function : Init() ~ Bool {
		SCREEN_WIDTH := 640;
		SCREEN_HEIGHT := 480;

		# Initialize SDL
		if(Core->Init(HardwareFlags->SDL_INIT_VIDEO) < 0) {
			"SDL could not initialize!"->ErrorLine();
			return false;
		};

		if(Font->Init() < 0) {
			"SDL_ttf could not initialize!"->ErrorLine();
			return false;
		};
		
		if(<>Hints->Set("SDL_RENDER_SCALE_QUALITY", "1")) {
			"Warning: Linear texture filtering not enabled!"->ErrorLine();
			return false;
		};

		# Create window
		@window := Window->New("SDL Tutorial", 
			WindowFlags->SDL_WINDOWPOS_UNDEFINED, WindowFlags->SDL_WINDOWPOS_UNDEFINED, 
			Consts->SCREEN_WIDTH, Consts->SCREEN_HEIGHT, WindowFlags->SDL_WINDOW_SHOWN);
		if(@window->IsNull()) {
			"Cannot create window!"->ErrorLine();
			return false;
		};

		# Get window surface
		@renderer := Renderer->New(@window, -1, 
			RendererFlags->SDL_RENDERER_ACCELERATED and RendererFlags->SDL_RENDERER_PRESENTVSYNC);
		if(@renderer->IsNull()) {
			"Cannot create renderer!"->ErrorLine();
			return false;
		};
		@renderer->SetDrawColor(0xFF, 0xFF, 0xFF, 0xFF);

		img_flags := ImageFlags->IMG_INIT_PNG->As(Int);
		if((Image->Init(img_flags) and img_flags) = 0) {
			"SDL image could not initialize!"->ErrorLine();
			return false;
		};

		@redTexture := LTexture->New(@renderer);
		@greenTexture := LTexture->New(@renderer);
		@blueTexture := LTexture->New(@renderer);
		@shimmerTexture := LTexture->New(@renderer);
		
		Particle->Init(@redTexture, @greenTexture, @blueTexture, @shimmerTexture);

		@dot_texture := LTexture->New(@renderer);
		@dot := Dot->New(0, 0, @dot_texture);

		return true;
	}
	
	function : LoadMedia() ~ Bool {
		if(<>@dot_texture->LoadFromFile("images/dot.bmp")) {
			"Failed to load image"->ErrorLine();
			return false;
		};

		if(<>@redTexture->LoadFromFile("images/red.bmp")) {
			"Failed to load image"->ErrorLine();
			return false;
		};

		if(<>@greenTexture->LoadFromFile("images/green.bmp")) {
			"Failed to load image"->ErrorLine();
			return false;
		};

		if(<>@blueTexture->LoadFromFile("images/blue.bmp")) {
			"Failed to load image"->ErrorLine();
			return false;
		};

		if(<>@shimmerTexture->LoadFromFile("images/shimmer.bmp")) {
			"Failed to load image"->ErrorLine();
			return false;
		};

		# Set texture transparency
		@redTexture->SetAlpha( 192 );
		@greenTexture->SetAlpha( 192 );
		@blueTexture->SetAlpha( 192 );
		@shimmerTexture->SetAlpha( 192 );

		return true;
	}
	
	function : LoadTexture(path : String) ~ Texture {
		# Load image at specified path
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return Nil;
		};

		texture := loadedSurface->CreateTexture(@renderer);
		if(texture->IsNull()) {
			"Unable to create texture!"->ErrorLine();
			return Nil;
		};

		loadedSurface->Free();
		
		return texture;
	}
	
	function : Close() ~ Nil {
		@dot_texture->Free();
		@redTexture->Free();
		@greenTexture->Free();
		@blueTexture->Free();
		@shimmerTexture->Free();

		@renderer->Destroy();
		@window->Destroy();

		Image->Quit();
		Core->Quit();
	}
}

consts Consts {
	SCREEN_WIDTH := 640,
	SCREEN_HEIGHT := 480,
	TOTAL_PARTICLES := 20,
	DOT_WIDTH := 20,
	DOT_HEIGHT := 20,
	DOT_VEL := 5,
	FPS := 60
}