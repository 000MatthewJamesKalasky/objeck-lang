#~
Lesson 43
http://lazyfoo.net/tutorials/SDL/43_render_to_texture/index.php
~#

use Game.SDL2;

class LTexture {
	@renderer : Renderer;
	@font : Font;
	@texture : Texture;
	@render_quad : Rect;
	@window : Window;
	@pixels : PixelData;
	@pitch : Int;
	@width : Int;
	@height : Int;

	New(renderer : Renderer, window : Window, font : Font := Nil) {
		@renderer := renderer;
		@window := window;
		@font := font;
		@render_quad := Rect->New();
	}

	method : public : LoadFromRenderedText(textureText : String, textColor : Color) ~ Bool {
		Free();

		# Render text surface
		textSurface := @font->RenderTextSolid(textureText, textColor);
		if(textSurface->IsNull()) {
			"Unable to render text surface!"->ErrorLine();
			return false;
		};

		texture := textSurface->CreateTexture(@renderer);
		@render_quad->SetW(textSurface->GetW());
		@render_quad->SetH(textSurface->GetH());
		
		@texture := texture;
		return @texture <> Nil;
	}

	# Loads image at specified path
	method : public : LoadFromFile(path : String) ~ Bool {
		Free();

		# Load image at specified path
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return false;
		};

		formattedSurface := loadedSurface->ConvertFormat(@window->GetPixelFormat(), 0);
		if(formattedSurface->IsNull()) {
			"Unable to convert loaded surface to display format!"->ErrorLine();
			return false;
		};

		newTexture := @renderer->CreateTexture(@window->GetPixelFormat(), 
			TextureAccess->SDL_TEXTUREACCESS_STREAMING, 
			formattedSurface->GetW(), formattedSurface->GetH() );
		if(newTexture->IsNull()) {
			"Unable to create blank texture!"->ErrorLine();
			return false;
		};

		@pixels := newTexture->Lock(Nil);
		@pitch := @pixels->GetPitch();
		surface_pixels := formattedSurface->GetPixels();
		@pixels->Copy(surface_pixels);
		newTexture->Unlock();
		@pixels := Nil;

		@width := formattedSurface->GetW();
		@height := formattedSurface->GetH();

		formattedSurface->Free();
		loadedSurface->Free();

		@texture := newTexture;
		return @texture <> Nil;
	}

	# Loads image at specified path
	method : public : LoadFromFileKeyed(path : String) ~ Bool {
		Free();

		# Load image at specified path
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return false;
		};

		loadedSurface->SetColorKey(true, loadedSurface->GetPixelFormat()->MapRGB(0, 0xFF, 0xFF));

		texture := loadedSurface->CreateTexture(@renderer);
		if(texture->IsNull()) {
			"Unable to create texture!"->ErrorLine();
			return false;
		};
		@render_quad->SetW(loadedSurface->GetW());
		@render_quad->SetH(loadedSurface->GetH());
		
		loadedSurface->Free();

		@texture := texture;
		return @texture <> Nil;
	}

	method : public : Lock() ~ Bool {
		# Texture is already locked
		if( @pixels <> Nil )
		{
			"Texture is already locked"->PrintLine();
			return false;
		}
		# Lock texture
		else
		{
			@pixels :=@texture->Lock(Nil);
			if(@pixels->IsNull())
			{
				"Unable to lock texture!"->PrintLine();
				return false;
			};
			@pitch := @pixels->GetPitch();
		};

		return true;
	}

	method : public : Unlock() ~ Bool {
		# Texture is not locked
		if( @pixels = Nil )
		{
			"Texture is not locked!"->PrintLine();
			return false;
		}
		# Unlock texture
		else
		{
			@texture->Unlock(  );
			@pixels := Nil;
			@pitch := 0;
		};

		return true;
	}

	method : public : GetWidth() ~ Int {
		return @width;
	}

	method : public : GetHeight() ~ Int {
		return @height;
	}

	method : public : GetPitch() ~ Int {
		return @pitch;
	}

	method : public : GetPixels() ~ PixelData {
		return @pixels;
	}

	method : public : GetPixel(x : Int, y : Int) ~ Int {
		return @pixels->Get(x ,y);
	}

	method : public : Copy(pixels : PixelData) ~ Nil {
		@pixels->Copy(pixels);
	}
	
	method : public : CreateBlank(width : Int, height : Int, 
			access : TextureAccess := TextureAccess->SDL_TEXTUREACCESS_STREAMING) ~ Bool {
		@texture := Texture->New(@renderer, PixelValue->SDL_PIXELFORMAT_RGBA8888, access, width, height);
		if(@texture->IsNull()) {
			"Unable to create blank texture!"->ErrorLine();
			return false;
		};

		@width := width;
		@height := height;

		return true;
	}

	method : public : SetTarget() ~ Nil {
		@renderer->SetTarget(@texture);
	}

	# Deallocates texture
	method : public : Free() ~ Nil {
		if(@texture <> Nil)	{
			@texture->Destroy();
		};
	}

	method : public : SetColor(red : Int, green : Int, blue : Int) ~ Nil {
		@texture->SetColorMod(red, green, blue);
	}

	method : public : SetBlendMode(blending : BlendMode) ~ Nil {
		# Set blending function
		@texture->SetBlendMode(blending);
	}
	
	method : public : SetAlpha(alpha : Int) ~ Nil {
		# Modulate texture alpha
		@texture->SetAlphaMod(alpha );
	}

	# Renders texture at given point
	method : public : Render(x : Int, y : Int, clip : Rect := Nil, 
			angle : Float := 0.0, center : Point := Nil, 
			flip : RendererFlip := RendererFlip->SDL_FLIP_NONE) ~ Nil {
		@render_quad->SetX(x);
		@render_quad->SetY(y);
		@render_quad->SetW(@width);
		@render_quad->SetH(@height);

		if(clip <> Nil)	{
			@render_quad->SetW(clip->GetW());
			@render_quad->SetH(clip->GetH());
		};

		@renderer->CopyEx(@texture, clip, @render_quad, angle, center, flip);
	}
}

class Test {
	@window : static : Window;
	@renderer : static : Renderer;
	@target_texture : static : LTexture;

	function : Main(args : String[]) ~ Nil {
		# Start up SDL and create window
		if( <>Init() ) {
			"Failed to initialize!"->ErrorLine();
			return;
		};
		
		# Load media
		if( <>LoadMedia() ) {
			"Failed to load media!"->ErrorLine();
			return;
		};

		FRAME_RATE := 1000 / Consts->FPS;
			
		# Main loop flag
		quit := false;

		# Event handler
		e := Event->New();

		angle := 0.0;
		screenCenter := Point->New(Consts->SCREEN_WIDTH / 2, Consts->SCREEN_HEIGHT / 2);

		# While application is running
		while(<>quit) {
			start := Timer->GetTicks();

			# Handle events on queue
			while( e->Poll() <> 0 ) {
				# User requests quit
				if(e->GetType() = EventType->SDL_QUIT) {
					quit := true;
				};
			};

			# rotate
			angle += 2;
			if( angle > 360 )
			{
				angle -= 360;
			};

			# Set self as render target
			@target_texture->SetTarget();

			# Clean screen
			@renderer->SetDrawColor(0xFF, 0xFF, 0xFF, 0xFF);
			@renderer->Clear();

			# Render red filled quad
			fillRect := Rect->New(Consts->SCREEN_WIDTH / 4, Consts->SCREEN_HEIGHT / 4, 
				Consts->SCREEN_WIDTH / 2, Consts->SCREEN_HEIGHT / 2);
			@renderer->SetDrawColor( 0xFF, 0x00, 0x00, 0xFF );		
			@renderer->FillRect( fillRect );

			# Render green outlined quad
			outlineRect := Rect->New(Consts->SCREEN_WIDTH / 6, Consts->SCREEN_HEIGHT / 6, 
				Consts->SCREEN_WIDTH * 2 / 3, Consts->SCREEN_HEIGHT * 2 / 3 );
			@renderer->SetDrawColor( 0x00, 0xFF, 0x00, 0xFF );		
			@renderer->FillRect( outlineRect );

			# Draw blue horizontal line
			@renderer->SetDrawColor( 0x00, 0x00, 0xFF, 0xFF );		
			@renderer->DrawLine( 0, Consts->SCREEN_HEIGHT / 2, Consts->SCREEN_WIDTH, 
				Consts->SCREEN_HEIGHT / 2 );

			# Draw vertical line of yellow dots
			@renderer->SetDrawColor( 0xFF, 0xFF, 0x00, 0xFF );
			for( i := 0; i < Consts->SCREEN_HEIGHT; i += 4; )
			{
				@renderer->DrawPoint( Consts->SCREEN_WIDTH / 2, i );
			};

			# Reset render target
			@renderer->SetTarget(Nil);

			# Show rendered to texture
			@target_texture->Render( 0, 0, Nil, angle, screenCenter );

			# Update screen
			@renderer->Present();

			end := start - Timer->GetTicks();
			if(end < FRAME_RATE) {
				Timer->Delay(FRAME_RATE - end);
			};
		};

		e->Free();
		Close();
	}
	
	function : Init() ~ Bool {
		# Initialize SDL
		if(Core->Init(HardwareFlags->SDL_INIT_VIDEO) < 0) {
			"SDL could not initialize!"->ErrorLine();
			return false;
		};
		
		if(<>Hints->Set("SDL_RENDER_SCALE_QUALITY", "1")) {
			"Warning: Linear texture filtering not enabled!"->ErrorLine();
			return false;
		};

		# Create window
		@window := Window->New("SDL Tutorial", 
			WindowFlags->SDL_WINDOWPOS_UNDEFINED, WindowFlags->SDL_WINDOWPOS_UNDEFINED, 
			Consts->SCREEN_WIDTH, Consts->SCREEN_HEIGHT, WindowFlags->SDL_WINDOW_SHOWN);
		if(@window->IsNull()) {
			"Cannot create window!"->ErrorLine();
			return false;
		};

		# Get window surface
		@renderer := Renderer->New(@window, -1, 
			RendererFlags->SDL_RENDERER_ACCELERATED and RendererFlags->SDL_RENDERER_PRESENTVSYNC);
		if(@renderer->IsNull()) {
			"Cannot create renderer!"->ErrorLine();
			return false;
		};
		@renderer->SetDrawColor(0xFF, 0xFF, 0xFF, 0xFF);

		img_flags := ImageFlags->IMG_INIT_PNG->As(Int);
		if((Image->Init(img_flags) and img_flags) = 0) {
			"SDL image could not initialize!"->ErrorLine();
			return false;
		};

		@target_texture := LTexture->New(@renderer, @window);
				
		return true;
	}
	
	function : native : LoadMedia() ~ Bool {
		# Load texture target
		if(<>@target_texture->CreateBlank(Consts->SCREEN_WIDTH, Consts->SCREEN_HEIGHT, 
				TextureAccess->SDL_TEXTUREACCESS_TARGET)) {
			"Failed to create target texture!"->ErrorLine();
			return false;
		};

		return true;
	}
	
	function : Close() ~ Nil {
		@target_texture->Free();
		@renderer->Destroy();
		@window->Destroy();

		Image->Quit();
		Core->Quit();
	}
}

consts Consts {
	SCREEN_WIDTH := 640,
	SCREEN_HEIGHT := 480,
	FPS := 60
}