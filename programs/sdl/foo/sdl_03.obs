#~
Lesson 03
Event Driven Programming

http://lazyfoo.net/tutorials/SDL/03_event_driven_programming/index.php
~#

use Game.SDL2;

class Test {
	@window : static : Window;
	@screen_surface : static : Surface;
	@x_out : static : Surface;
	
	function : Main(args : String[]) ~ Nil {
		# Start up SDL and create window
		if( <>Init() ) {
			"Failed to initialize!"->ErrorLine();
		}
		else {
			# Load media
			if( <>LoadMedia() ) {
				"Failed to load media!"->PrintLine();
			}
			else {			
				# Main loop flag
				quit := false;
				
				# Event handler
				e := Event->New();

				# While application is running
				while( <>quit )
				{
					# Handle events on queue
					while( e->Poll() <> 0 ) {
						# User requests quit
						if( e->GetType() = EventType->SDL_QUIT ) {
							quit := true;
						};
					};

					# Apply the image
					@x_out->UpperBlit( Nil, @screen_surface, Nil );
				
					# Update the surface
					@window->UpdateSurface();
				};
			};
		};
		
		Close();
	}
	
	function : Init() ~ Bool {
		# Initialization flag
		success := true;
		
		# Initialize SDL
		if(Core->Init(HardwareFlags->SDL_INIT_VIDEO->As(Int)) < 0) {
			"SDL could not initialize!"->ErrorLine();
			success := false;
		}
		else {
			# Create window
			@window := Window->New( "SDL Tutorial", WindowFlags->SDL_WINDOWPOS_UNDEFINED, 
				WindowFlags->SDL_WINDOWPOS_UNDEFINED, 640, 480, WindowFlags->SDL_WINDOW_SHOWN);
			if( @window = Nil )	{
				"Window could not be created!"->ErrorLine();
				success := false;
			} 
			else {
				# Get window surface
				@screen_surface := @window->GetSurface();
			};
		};
		
		return success;
	}
	
	function : LoadMedia() ~ Bool {
		# Loading success flag
		success := true;

		# Load splash image
		image := "images/x.bmp";
		@x_out := Surface->LoadBMP( image );
		if(@x_out = Nil ) {
			"Unable to load image {$image}"->PrintLine();
			success := false;
		};
		
		return success;
	}
	
	function : Close() ~ Nil {
		# Deallocate surface
		@x_out->Free();
		@x_out := Nil;

		# Destroy window
		@window->Destroy();
		@window := Nil;

		# Quit SDL subsystems
		Core->Quit();
	}
}
