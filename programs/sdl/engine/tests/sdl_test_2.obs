#~
Game demo v0
link: https://github.com/objeck/objeck-lang/blob/master/programs/sdl/engine/tests/sdl_test_2.obs
~#

use SDL2;
use SDL2.Game;

class PlatformDemo {
	SCREEN_WIDTH : static : Int;
	SCREEN_HEIGHT : static : Int;
	BLOCK_WIDTH : static : Int;
	BLOCK_HEIGHT : static : Int;

	@game : GameEngine;
	@game_map : Int[,];
	@game_frame : Int;

	@background : ImageSprite;
	@background_x : Int;

	@player_walking : AnimatedImageSprite;
	@player_walk_frame : Int;
	@player_jumping : AnimatedImageSprite; 
	@player_falling : AnimatedImageSprite;
	@player : AnimatedImageSprite;
	@player_flip : Bool;

	@block1 : ImageSprite;

	@obstacles : Rectangle[];
	
	@player_x_direction : Int;
	@player_x_position : Int;
	@player_y_position : Int;
	@player_y_jumping : Bool;
	@player_y_jump_height : Int;

	@debug_text : TextSprite;
	@debug_string : String;
	@text_color : Color;

	@left_blocks : Rect[];
	@right_blocks : Rect[];

	function : Main(args : String[]) ~ Nil {
		platform := PlatformDemo->New();
		platform->Run();
	}

	New() {
		SCREEN_WIDTH := 640;
		SCREEN_HEIGHT := 480;
		BLOCK_WIDTH := 32;
		BLOCK_HEIGHT := 32;

		@game := GameEngine->New(SCREEN_WIDTH, SCREEN_HEIGHT, "Engine Zero");

		@background := @game->AddImageSprite("../../../../programs/sdl/engine/tests/media/images/hills.png");
		@block1 := @game->AddImageSprite("../../../../programs/sdl/engine/tests/media/images/block1.png");
		@player_walking := @game->AddAnimatedImageSprite("../../../../programs/sdl/engine/tests/media/images/walk.png", 6);
		@player_jumping := @game->AddAnimatedImageSprite("../../../../programs/sdl/engine/tests/media/images/jump.png", 3);
		@player_falling := @game->AddAnimatedImageSprite("../../../../programs/sdl/engine/tests/media/images/fall.png", 3);
		@player := @player_walking;

		@player_y_position := SCREEN_HEIGHT - BLOCK_WIDTH * 4;
		
		@obstacles := Rectangle->New[8];
		each(i : @obstacles) {
			@obstacles[i] := @game->AddRectangle(BLOCK_WIDTH / 2, BLOCK_HEIGHT / 2);
			@obstacles[i]->SetFill(true);
			@obstacles[i]->SetColor(Color->New(139, 69, 19));
		};

		@block1 := @game->AddImageSprite("../../../../programs/sdl/engine/tests/media/images/block1.png");

		@debug_text := @game->AddTextSprite();
		@text_color := Color->New();

		@game_map := CreateMap();
		map_dims := @game_map->Size();

		@left_blocks := Rect->New[map_dims[0]];
		each(i : @left_blocks) {
			@left_blocks[i] := Rect->New(BLOCK_WIDTH, BLOCK_HEIGHT);
		};

		@right_blocks := Rect->New[map_dims[0]];
		each(i : @right_blocks) {
			@right_blocks[i] := Rect->New(BLOCK_WIDTH, BLOCK_HEIGHT);
		};
	}

	method : Run() ~ Nil {
		if(@game->IsOk()) {
			e := @game->GetEvent();
			quit := false;
			while(<>quit) {
				@game->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					};
					ProcessInput(e);
				};

				UpdateEvents();
				Render();

				@game->FrameEnd();
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@game->Quit();
		};
	}
	
	method : public : ProcessInput(e : Event) ~ Nil {
		if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_SPACE: {
	            	if(@player_y_jump_height = 0) {
	            		@player_y_jumping := true;
	            	};
	            }

	            label Scancode->SDL_SCANCODE_LEFT: {
		            @player_walking->SetFlip(RendererFlip->SDL_FLIP_HORIZONTAL);
					@player_jumping->SetFlip(RendererFlip->SDL_FLIP_HORIZONTAL);
					@player_falling->SetFlip(RendererFlip->SDL_FLIP_HORIZONTAL);
					@player_flip := true;
	            	@player_x_direction := -1;
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	if(@player_flip) {
	            		@player_walking->SetFlip(RendererFlip->SDL_FLIP_NONE);
						@player_jumping->SetFlip(RendererFlip->SDL_FLIP_NONE);
						@player_falling->SetFlip(RendererFlip->SDL_FLIP_NONE);
	            		@player_flip := false;
	            	};

	            	@player_x_direction := 1;
	            }

	            label Scancode->SDL_SCANCODE_DOWN: {
	            }
	        };
	    }
	    else if(e->GetType() = EventType->SDL_KEYUP & e->GetKey()->GetRepeat() = 0) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {
	        	label Scancode->SDL_SCANCODE_LEFT:
	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@player_x_direction := 0;
	            }

	            label Scancode->SDL_SCANCODE_DOWN: {
	            }
	        };
	    };   
	}

	method : native : UpdateEvents() ~ Nil {		
		@game_frame += 1;

		if(@game_frame <= @game->GetFps()) {
			# update movement
			if(@game_frame % 2 = 0) {
				# jump
				if(@player_y_jumping) {
					@player_y_jump_height += BLOCK_HEIGHT / 8;					
					if(@player_y_jump_height >= 1.5 * BLOCK_HEIGHT) {
						@player_y_jumping := false;
						@player_y_jump_height -= BLOCK_HEIGHT / 8;
					};
				}
				else if(@player_y_jump_height <> 0) {
					@player_y_jump_height -= BLOCK_HEIGHT / 8;
				};	

				# move left
				if(@player_x_direction = -1) {
					if(<>HitLeft()) {
						@background_x += @background->GetWidth() / 50;
						if(@background_x > 0) {
							@background_x := -1 * @background->GetWidth();
						};

						@player_x_position -= BLOCK_WIDTH / 8;
						PlayerJumpLeft();
					};
				}
				# move right
				else if(@player_x_direction = 1) {
					if(<>HitRight()) {
						@background_x -= @background->GetWidth() / 50;
						if(@background_x < -1 * @background->GetWidth()) {
							@background_x := 0;
						};

						@player_x_position += BLOCK_WIDTH / 8;
						PlayerJumpRight();
					};
				};

				PlayerFalling();
				HitTop();
			};

			# walk frames
			if(@game_frame % 5 = 0) {
				if(@player_x_direction <> 0) {
					if(@player_y_jump_height = 0) {
						@player_walk_frame += 1;
						if(@player_walk_frame >= @player_walking->GetClipCount()) {
							@player_walk_frame := 0;
						};
					};
				};
			};
		}
		else {
			@game_frame := 1;
		};
	}

	method : native : HitTop() ~ Nil {
		left_bottom := -1;
		left_block := MinLeftBlock();
		if(left_block <> Nil) {
			left_bottom := left_block->GetY() + left_block->GetH();
		};

		right_bottom := -1;
		right_block := MinRightBlock();
		if(right_block <> Nil) {
			right_bottom := right_block->GetY() + right_block->GetH();
		};

		block_bottom := Int->Max(left_bottom, right_bottom);
		if(block_bottom > -1) {
			player_top := @player->GetRect()->GetY();
			if(player_top < block_bottom) {
				@player_y_jumping := false;
				@player_y_jump_height := 0;
			};
		};
	}

	method : MinLeftBlock() ~ Rect {
		return MinTopBlock(@left_blocks);
	}

	method : MinRightBlock() ~ Rect {
		return MinTopBlock(@right_blocks);
	}

	method : native : MinTopBlock(blocks : Rect[]) ~ Rect {
		each(i : blocks) {
			block := blocks[i];

			block_top := block->GetY();
			block_bottom := block_top + block->GetH();
			
			player_top := @player->GetRect()->GetY();
			player_bottom := player_top + @player->GetHeight();

			if(block_top > -1 & block_top < player_top & player_top < block_bottom) {
				return block;
			};
		};

		return Nil;
	}

	method : native : HitRight() ~ Bool {
		if(@player_x_position / BLOCK_WIDTH > 35) {
			return true;
		};

		player_left := @player->GetRect()->GetX();
		player_right := @player->GetRect()->GetX() + @player->GetWidth();
		player_top := @player->GetRect()->GetY();
		player_bottom := player_top + @player->GetHeight();

		each(i : @right_blocks) {
			block := @right_blocks[i];

			block_left := block->GetX();
			if(block_left > -1) {
				block_right := block->GetX() + block->GetW();
				block_top := block->GetY();
				block_bottom := block->GetY() + block->GetH();

				# check right x-axis
				if(block_right >= player_left & block_bottom > player_top & block_top < player_bottom) {
					@player_x_position -= BLOCK_HEIGHT / 16;
					return true;
				};
			};
		};

		return false;
	}

	method : native : HitLeft() ~ Bool {
		if(@player_x_position <= 8) {
			return true;
		};

		player_left := @player->GetRect()->GetX();
		player_right := @player->GetRect()->GetX() + @player->GetWidth();
		player_top := @player->GetRect()->GetY();
		player_bottom := player_top + @player->GetHeight();

		each(i : @left_blocks) {
			block := @left_blocks[i];

			block_left := block->GetX();
			if(block_left > -1) {
				block_right := block->GetX() + block->GetW();
				block_top := block->GetY();
				block_bottom := block->GetY() + block->GetH();

				# check left x-axis
				if(block_right > player_left & block_bottom > player_top & block_top < player_bottom) {
					@player_x_position += BLOCK_HEIGHT / 16;
					return true;
				};
			};
		};

		return false;
	}

	method : PlayerJumpLeft() ~ Nil {
		if(@player_y_jump_height > 0) {
			player_bottom := @player->GetRect()->GetY() + @player->GetHeight();

			block := MaxLeftBlock();
			if(block <> Nil) {
				block_top := block->GetY();

				if(@player_y_jump_height > BLOCK_HEIGHT & block_top - player_bottom < BLOCK_HEIGHT & player_bottom <= block_top) {
					@player_y_position -= BLOCK_HEIGHT;
					@player_y_jumping := false;
					@player_y_jump_height := 0;
				};
			};
		};
	}

	method : PlayerJumpRight() ~ Nil {
		if(@player_y_jump_height > 0) { 
			player_bottom := @player->GetRect()->GetY() + @player->GetHeight();

			block := MaxRightBlock();
			if(block <> Nil) {
				block_top := block->GetY();

				if(@player_y_jump_height > BLOCK_HEIGHT & block_top - player_bottom < BLOCK_HEIGHT & player_bottom <= block_top) {
					@player_y_position -= BLOCK_HEIGHT;
					@player_y_jumping := false;
					@player_y_jump_height := 0;
				};
			};
		};
	}

	method : PlayerFalling() ~ Nil {
		if(@player_y_jump_height = 0) {
			player_bottom := @player->GetRect()->GetY() + @player->GetHeight();
			
			left_block := MaxLeftBlock();
			right_block := MaxRightBlock();
						
			if(left_block <> Nil & right_block <> Nil) {
				block_top := Int->Min(left_block->GetY(), right_block->GetY());

				if(block_top - player_bottom >= BLOCK_HEIGHT) {				
					@player_y_position += BLOCK_HEIGHT;
					@player_y_jumping := false;
					@player_y_jump_height := BLOCK_HEIGHT;
				};
			};
		} ;
	}

	method : native : MaxLeftBlock () ~ Rect {
		return MaxBottomBlock(@left_blocks);
	}

	method : MaxRightBlock () ~ Rect {
		return MaxBottomBlock(@right_blocks);
	}

	method : native : MaxBottomBlock(blocks : Rect[]) ~ Rect {
		each(i : blocks) {
			block := blocks[i];

			block_y := block->GetY();
			player_y := @player_y_position + BLOCK_HEIGHT;

			if(block_y > -1 & block_y >= player_y) {
				return block;
			};
		};

		return Nil;
	}

	method : Render() ~ Nil {
		@game->Clear();

		#background
		@background->Render(@background_x, 0);
		@background->Render(@background_x + @background->GetWidth(), 0);

		RenderPlatform();

		if(@player_y_jump_height = 0) {
			@player := @player_walking;
			@player->Render(BLOCK_WIDTH * 2, @player_y_position - @player_y_jump_height, @player_walk_frame);			
		}
		else if(@player_y_jumping) {
			@player := @player_jumping;
			@player_jumping->Render(BLOCK_WIDTH * 2, @player_y_position - @player_y_jump_height, 0);
		}
		else {
			@player := @player_falling;
			@player->Render(BLOCK_WIDTH * 2, @player_y_position - @player_y_jump_height, 0);
		};

#~
		each(i : @obstacles) {
			@obstacles[i]->Render(BLOCK_WIDTH * (i + 3), SCREEN_HEIGHT - BLOCK_HEIGHT * (i + 3));
		};
~#
		@debug_text->RenderedText("Pos: ({$@player_x_position},{$@player_y_position})", @text_color);
		@debug_text->Render(8, 8);
		
		@game->Show();
	}

	method : native : RenderPlatform() ~ Nil {
		dims := @game_map->Size();

		view_x_offset := @player_x_position / BLOCK_WIDTH;
		view_y_offset := ((@player_y_position - @player_y_jump_height) / BLOCK_WIDTH) + 1;

		y_end := dims[0];
		x_end := view_x_offset + 21;
		if(x_end > dims[1]) {			
			x_end := dims[1];	
		};

		for(x := view_x_offset; x < x_end; x += 1;) {
			pixel_x := x * BLOCK_WIDTH - @player_x_position;

			for(y := 0; y < y_end; y += 1;) {
				pixel_y := y * BLOCK_HEIGHT;

				if(x - view_x_offset = 2) {
					if(@game_map[y, x] > 0) {
						@left_blocks[y]->SetX(pixel_x);
						@left_blocks[y]->SetY(pixel_y);
					}
					else {
						@left_blocks[y]->SetX(-1);
						@left_blocks[y]->SetY(-1);
					};
				};

				if(x - view_x_offset = 3) {
					if(@game_map[y, x] > 0) {
						@right_blocks[y]->SetX(pixel_x);
						@right_blocks[y]->SetY(pixel_y);
					}
					else {
						@right_blocks[y]->SetX(-1);
						@right_blocks[y]->SetY(-1);
					};
				};

				if(@game_map[y, x] > 0) {
					@block1->Render(pixel_x, pixel_y);
				};
			};
		};
	}

	method : native : CreateMap() ~ Int[,] {
		game_map := [
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 1, 0, 1, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0,   1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0,   1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 1, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 1, 1, 1, 1]
			[1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1,   1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1]
		];

		return game_map;
	}
}