use Game.SDL2;
use Game.Framework;
use Collection;

class Demo {
	@framework : GameFramework;
	@player : Player;
	@platforms : Vector;

	function : Main(args : String[]) ~ Nil {
		Demo->New()->Run();
	}

	New() {
		@framework := GameFramework->New(Game->SCREEN_WIDTH, Game->SCREEN_HEIGHT, "Accel");
		@framework->SetClearColor(Color->New(0, 0, 255));
		@platforms := Vector->New();
	}

	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			@player := Player->New(@framework->AddRectangle(40, 60), @platforms);
			@platforms->AddBack(Platform->New(420, 535, @framework->AddRectangle(110, 35)));
			@platforms->AddBack(Platform->New(300, 475, @framework->AddRectangle(110, 35)));
			@platforms->AddBack(Platform->New(220, 375, @framework->AddRectangle(110, 35)));


			quit := false;
			e := @framework->GetEvent();
			while(<>quit) {
				@framework->FrameStart();
				@framework->Clear();
				
				# process input
				while(e->Poll() <> 0) {
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					};

					ProcessInput(e);
				};

				# update
				@player->Update();

				# draw
				@player->Draw();
				each(i : @platforms) {
					@platforms->Get(i)->As(Platform)->Draw();
				};
				
				# show
				@framework->Show();
				@framework->FrameEnd();
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	method : public : ProcessInput(e : Event) ~ Nil {
		if(e->GetType() = EventType->SDL_KEYDOWN) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@player->GoLeft();
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@player->GoRight();
	            }

	            label Scancode->SDL_SCANCODE_UP: {
	            	@player->Jump(true);
	            }

	            label Scancode->SDL_SCANCODE_D: {
	           		@player->Dump();
	           	}
	        };
	    }
	    else if(e->GetType() = EventType->SDL_KEYUP) {
	       	select(e->GetKey()->GetKeysym()->GetScancode()) {
		       	label Scancode->SDL_SCANCODE_LEFT:
		        label Scancode->SDL_SCANCODE_RIGHT: {
		        	@player->Stop();
		        }

		        label Scancode->SDL_SCANCODE_UP: {
		        	@player->Jump(false);
		       	}
		    };
	    };
	}

	function : Collisions(player : Player, obstacles : Vector) ~ Vector {
		collides := Vector->New();

		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Platform);
        	if(obstacle->Collides(player->GetRect())) {
        		collides->AddBack(obstacle);
        	};
        };

        return collides;
    }
}

class Player {
	@rect : Rectangle;
	@loc : Vector2;
	@vel : Vector2;
	@acc : Vector2;
	@jump : Bool;
	@jump_limit : Int;
	@y_dir : Int;

	@platforms : Vector;
	
	New(rect : Rectangle, platforms : Vector) {
		@rect := rect;
		@rect->SetFill(true);
		@rect->SetColor(Color->New(255, 0, 0));

		@loc := Vector2->New(Game->SCREEN_WIDTH / 2, Game->SCREEN_HEIGHT / 2);
		@vel := Vector2->New();
		@acc := Vector2->New();

		@platforms := platforms;
	}

	method : public : GetRect() ~ Rectangle {
		return @rect; 
	}

	method : public : GetLeft() ~ Int {
		return @rect->GetLeft();
	}

	method : public : GetRight() ~ Int {
		return @rect->GetRight();
	}

	method : public : ChangeLeft()  ~ Int {
		return @acc->GetX();
	}

	method : public : GoLeft()  ~ Nil {
		@acc->SetX(-0.75);
	}

	method : public : GoRight()  ~ Nil {
		@acc->SetX(0.75);
	}

	method : public : Stop()  ~ Nil {
	}

	method : public : Jump(state : Bool)  ~ Nil {
		@jump := state;
	}

	method : public : Update()  ~ Nil {
		if(@jump & @jump_limit < 4) {
			@acc->SetY(-5.3);
			@jump_limit += 1;		
			@y_dir := 1;
		}
		else {
			@acc->SetY(0.65);
		};

		# motion
		@acc->Add(Vector2->Mul(@vel, -0.037));
		@vel->Add( Vector2->Mul(@acc, 0.85) );
		@loc->Add(@vel);
		@acc->Mul(0);
		
		# reposition x
		@rect->SetCenterX(@loc->GetX());
		collides := Demo->Collisions(@self, @platforms);
        each(i : collides) {
        	platform := collides->Get(i)->As(Platform);
			if(@vel->GetX() > 0.01) {
    			@rect->SetRight(platform->GetLeft());
    			@loc->SetX(platform->GetLeft() - @rect->GetWidth() / 2);
    			@vel->MulX(0);
            }
    		else if(@vel->GetX() < -0.01) {
            	@rect->SetLeft(platform->GetRight());
            	@loc->SetX(platform->GetRight() + @rect->GetWidth() / 2);
            	@vel->MulX(0);
            };
        };

		# reposition y
		@rect->SetBottom(@loc->GetY() - @rect->GetHeight() / 2);
		each(i : @platforms) {
        	platform := @platforms->Get(i)->As(Platform);
        	if(platform->Collides(@rect)) {
        		if(@vel->GetY() < 0.5) {
					@rect->SetTop(platform->GetBottom() + 2);
                	@loc->SetY(platform->GetBottom() + @rect->GetHeight() / 2 + 2);
       				@vel->MulY(-5.3);
					@jump_limit := 4;


	            }
	            else if(@vel->GetY() > 0.5) {
        			@rect->SetBottom(platform->GetTop());
        			@loc->SetY(platform->GetTop() + @rect->GetHeight() / 2);
					@vel->MulY(0);
					@jump_limit := 0;
                };
	        };
	    };

	    # wrap around side
		if(@loc->GetX() > Game->SCREEN_WIDTH) {
			@loc->SetX(0);
		}
		else if(@loc->GetX() < 0) {
			@loc->SetX(Game->SCREEN_WIDTH->As(Float));
		};

		# hit floor
		if(@rect->GetBottom() > Game->SCREEN_HEIGHT) {
			@rect->SetBottom(Game->SCREEN_HEIGHT);
        	@loc->SetY(Game->SCREEN_HEIGHT + @rect->GetHeight() / 2);
			@jump_limit := 0;
			@vel->MulY(0);
		}
		else if(@loc->GetY() < 0) {
			@loc->SetY(0);
			@vel->MulY(0);
		};
	}

	method : public : Dump() ~ Nil {
		ax := @acc->GetX();
		ay := @acc->GetY();

		vx := @vel->GetX();
		vy := @vel->GetY();

		lx := @loc->GetX();
		ly := @loc->GetY();
				
		"x=[acc={$ax}, vel={$vx}, loc={$lx}]"->PrintLine();
		"y=[acc={$ay}, vel={$vy}, loc={$ly}]"->PrintLine();
		"--------"->PrintLine();
	}

	method : public : Draw()  ~ Nil {
		@rect->Render();
	}
}

class Platform {
	@rect : Rectangle;

	New(x : Int, y : Int, rect : Rectangle) {
		@rect := rect;
		@rect->SetFill(true);
		@rect->SetColor(Color->New(0, 255, 0));
		@rect->Set(x, y);
	}

	method : public : AddX(x : Int)  ~ Nil {
		@rect->AddX(x);
	}

	method : public : GetLeft()  ~ Int {
		return @rect->GetLeft();
	}

	method : public : GetRight()  ~ Int {
		return @rect->GetRight();
	}

	method : public : GetTop()  ~ Int {
		return @rect->GetTop();
	}

	method : public : GetBottom()  ~ Int {
		return @rect->GetBottom();
	}

	method : public : Draw()  ~ Nil {
		@rect->Render();
	}

	method : public : Collides(rect : Rectangle) ~ Bool {
		return @rect->GetPosition()->Overlaps(rect->GetPosition());
	}
}

consts Game {
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 600
}