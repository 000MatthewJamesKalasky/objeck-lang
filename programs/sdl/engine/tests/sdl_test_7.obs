use Game.SDL2;
use Game.Framework;
use Collection;

class Demo {
	@framework : GameFramework;
	@player : Player;
	@background : ImageSprite;
	@level : Level;

	function : Main(args : String[]) ~ Nil {
		Demo->New()->Run();
	}

	New() {
		@framework := GameFramework->New(Game->SCREEN_WIDTH, Game->SCREEN_HEIGHT, "Accel");
		@framework->SetClearColor(Color->New(0, 0, 255));

		background_path := "C:\\Users\\objec\\Documents\\Code\\objeck-lang\\programs\\sdl\\engine\\tests\\media\\images\\background.png";
		@background := @framework->AddImageSprite(background_path);
	}

	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			@player := Player->New(@framework);
			@level := Level->New(@player, @framework);
			
			quit := false;
			e := @framework->GetEvent();
			while(<>quit) {
				@framework->FrameStart();
				@framework->Clear();
				
				# process input
				while(e->Poll() <> 0) {
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					};

					ProcessInput(e);
				};

				# update
				@player->Update();
				@level->Update();

				shift := @level->GetShift();
				if(@player->GetRight() >= Game->PLAYER_BOX_MAX) {
					diff := @player->GetRight() - Game->PLAYER_BOX_MAX;
					@player->SetRight(Game->PLAYER_BOX_MAX);
					if(shift > Game->WIDTH_MAX) {
						@level->ShiftWorld(-1 * diff);
					};
		        };

		        if(@player->GetLeft() <= Game->PLAYER_BOX_MIN) {
					diff := Game->PLAYER_BOX_MIN - @player->GetLeft();
					@player->SetLeft(Game->PLAYER_BOX_MIN);
					if(shift < 0) {
						@level->ShiftWorld(diff);
					};
				};

				# draw
				Background();
				@player->Draw();
				@level->Draw();
				
				# show
				@framework->Show();
				@framework->FrameEnd();
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	method : public : Background() ~ Nil {
		shift := @level->GetShift() / 3;
		@background->Render(shift, 0);
		@background->Render(shift + @background->GetWidth(), 0);	
	}

	method : public : ProcessInput(e : Event) ~ Nil {
		if(e->GetType() = EventType->SDL_KEYDOWN) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@player->GoLeft();
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@player->GoRight();
	            }

	            label Scancode->SDL_SCANCODE_UP: {
	            	@player->Jump(true);
	            }

	            label Scancode->SDL_SCANCODE_D: {
	           		@player->Dump();
	           	}
	        };
	    }
	    else if(e->GetType() = EventType->SDL_KEYUP) {
	       	select(e->GetKey()->GetKeysym()->GetScancode()) {
		       	label Scancode->SDL_SCANCODE_LEFT:
		        label Scancode->SDL_SCANCODE_RIGHT: {
		        	@player->Stop();
		        }

		        label Scancode->SDL_SCANCODE_UP: {
		        	@player->Jump(false);
		       	}
		    };
	    };
	}

	function : Collisions(player : Player, obstacles : Vector) ~ Vector {
		collides := Vector->New();

		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Platform);
        	if(obstacle->Collides(player->GetRect())) {
        		collides->AddBack(obstacle);
        	};
        };

        return collides;
    }
}

class Player {
	@rect : AnimatedImageSprite;
	@loc : Vector2;
	@vel : Vector2;
	@acc : Vector2;
	@jumped : Bool;
	@jump_max : Int;
	@hit_top : Bool;
	@level : Level;
	
	New(framework : GameFramework) {
		clips := Vector->New();
		clips->AddBack(Rect->New(0, 0, 32, 52));
		clips->AddBack(Rect->New(32, 0, 32, 52));
		clips->AddBack(Rect->New(64, 0, 32, 52));
		clips->AddBack(Rect->New(96, 0, 32, 52));
		clips->AddBack(Rect->New(128, 0, 32, 52));
		clips->AddBack(Rect->New(160, 0, 32, 52));
		@rect := framework->AddAnimatedImageSprite("C:\\Users\\objec\\Documents\\Code\\objeck-lang\\programs\\sdl\\engine\\tests\\media\\images\\player.png", clips);
		
		@loc := Vector2->New(Game->SCREEN_WIDTH / 2, Game->SCREEN_HEIGHT / 2);
		@vel := Vector2->New();
		@acc := Vector2->New();
	}

	method : public : SetLevel(level : Level) ~ Nil {
		@level := level;
	}

	method : public : GetRect() ~ AnimatedImageSprite {
		return @rect;
	}

	method : public : GetLeft() ~ Int {
		return @loc->GetX() - @rect->GetWidth() / 2;
	}

	method : public : GetRight() ~ Int {
		return @loc->GetX() + @rect->GetWidth() / 2;
	}

	method : public : SetLeft(x : Int) ~ Nil {
		@loc->SetX(x + @rect->GetWidth() / 2);
	}

	method : public : SetRight(x : Int) ~ Nil {
		@loc->SetX(x - @rect->GetWidth() / 2);
	}

	method : public : GoLeft()  ~ Nil {
		if(@rect->GetFlip() = RendererFlip->SDL_FLIP_NONE) {
			@rect->SetFlip(RendererFlip->SDL_FLIP_HORIZONTAL);
		};

		@acc->SetX(-0.75);
	}

	method : public : GoRight()  ~ Nil {
		if(@rect->GetFlip() = RendererFlip->SDL_FLIP_HORIZONTAL) {
			@rect->SetFlip(RendererFlip->SDL_FLIP_NONE);
		};

		@acc->SetX(0.75);
	}

	method : public : Stop()  ~ Nil {
	}
	
	method : public : Jump(jumped : Bool)  ~ Nil {
		@jumped := jumped;
	}

	method : public : Update()  ~ Nil {
		if(@hit_top) {
			@acc->SetY(120);
			@hit_top := false;
		}
		else {
			if(@jumped & @jump_max < 3) {
				@acc->SetY(-5.3);
				@jump_max += 1;
			}
			else {
				@acc->SetY(0.65);
			};
		};

		# movement equations
		@acc->Add(Vector2->Mul(@vel, -0.0625));
		@vel->Add(Vector2->Mul(@acc, 0.75));
		@loc->Add(@vel);
		@acc->Zero();
		
		# reposition 'x'
		platforms := @level->GetPlatforms();

		@rect->SetCenterX(@loc->GetX());
		collides := Demo->Collisions(@self, platforms);
        each(i : collides) {
        	platform := collides->Get(i)->As(Platform);
			if(@vel->GetX() > 0.01) {
    			@rect->SetRight(platform->GetLeft());
    			@loc->SetX(platform->GetLeft() - @rect->GetWidth() / 2);
    			@vel->MulX(0);
            }
    		else if(@vel->GetX() < -0.01) {
            	@rect->SetLeft(platform->GetRight());
            	@loc->SetX(platform->GetRight() + @rect->GetWidth() / 2);
            	@vel->MulX(0);
            };
        };

		# reposition 'y'
		@rect->SetBottom(@loc->GetY() - @rect->GetHeight() / 2);
		each(i : platforms) {
        	platform := platforms->Get(i)->As(Platform);
        	if(platform->Collides(@rect)) {
        		if(@vel->GetY() < 0.01) {
					@rect->SetTop(platform->GetBottom());
                	@loc->SetY(platform->GetBottom() + @rect->GetHeight() / 2);
					@jump_max := 3;
					@hit_top := true;
	            }
	            else if(@vel->GetY() > 0.01) {
        			@rect->SetBottom(platform->GetTop());
        			@loc->SetY(platform->GetTop() + @rect->GetHeight() / 2);
					@vel->SetY(0);
					@jump_max := 0;
                };
	        };
	    };

	    # wrap around side
		if(@loc->GetX() > Game->SCREEN_WIDTH) {
			@loc->SetX(0);
		}
		else if(@loc->GetX() < 0) {
			@loc->SetX(Game->SCREEN_WIDTH->As(Float));
		};

		# hit floor
		if(@rect->GetBottom() > Game->SCREEN_HEIGHT) {
			@rect->SetBottom(Game->SCREEN_HEIGHT);
        	@loc->SetY(Game->SCREEN_HEIGHT + @rect->GetHeight() / 2);
			@jump_max := 0;
			@vel->SetY(0);
		}
		else if(@loc->GetY() < 0) {
			@loc->SetY(0);
			@vel->SetY(0);
		};
	}

	method : public : Dump() ~ Nil {
		ax := @acc->GetX();
		ay := @acc->GetY();

		vx := @vel->GetX();
		vy := @vel->GetY();

		lx := @loc->GetX();
		ly := @loc->GetY();
				
		"x=[acc={$ax}, vel={$vx}, loc={$lx}]"->PrintLine();
		"y=[acc={$ay}, vel={$vy}, loc={$ly}]"->PrintLine();
		"--------"->PrintLine();
	}

	method : public : Draw()  ~ Nil {
		if(@vel->GetX() > 0.075 | @vel->GetX() < -0.075) {
			offset := @level->GetShift() + @loc->GetX();
			index := Int->Abs((offset / 15)->As(Int) % 4);
			@rect->Render(index);
		}
		else {
			@rect->Render(4);
		};
	}
}

class Platform {
	@rect : Rectangle;

	New(x : Int, y : Int, rect : Rectangle) {
		@rect := rect;
		@rect->SetFill(true);
		@rect->SetColor(Color->New(50, 51, 52));
		@rect->Set(x, y);
	}

	method : public : AddX(x : Int)  ~ Nil {
		@rect->AddX(x);
	}

	method : public : GetLeft()  ~ Int {
		return @rect->GetLeft();
	}

	method : public : GetRight()  ~ Int {
		return @rect->GetRight();
	}

	method : public : GetTop()  ~ Int {
		return @rect->GetTop();
	}

	method : public : GetBottom()  ~ Int {
		return @rect->GetBottom();
	}

	method : public : Draw()  ~ Nil {
		@rect->Render();
	}

	method : public : Collides(rect : AnimatedImageSprite) ~ Bool {
		return @rect->GetPosition()->Overlaps(rect->GetPosition());
	}
}

class Level {
	@player : Player;
	@platforms : Vector;
	@shift_x : Int;

	New(player : Player, framework : GameFramework) {
		@player := player;
		@platforms := Vector->New();

#		@platforms->AddBack(MovingPlatform->New(200, 150, framework->AddRectangle(75, 35), player, @self));		
		@platforms->AddBack(Platform->New(620, 535, framework->AddRectangle(110, 20)));
		@platforms->AddBack(Platform->New(700, 485, framework->AddRectangle(120, 15)));
		@platforms->AddBack(Platform->New(820, 450, framework->AddRectangle(210, 20)));
		@platforms->AddBack(Platform->New(1025, 545, framework->AddRectangle(210, 15)));

		@player->SetLevel(@self);
	}

	function : Collides(player : Player, obstacles : Vector) ~ Bool {
		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Platform);
        	if(obstacle->Collides(player->GetRect())) {
        		return true;
        	};
        };

        return false;
	}

	function : Collisions(player : Player, obstacles : Vector) ~ Vector {
		collides := Vector->New();

		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Platform);
        	if(obstacle->Collides(player->GetRect())) {
        		collides->AddBack(obstacle);
        	};
        };

        return collides;
    }

	method : public : GetShift() ~ Int {
		return @shift_x;
	}

	method : public : ShiftWorld(shift_x : Int) ~ Nil {
		@shift_x += shift_x;

		each(i : @platforms) {
			platform := @platforms->Get(i)->As(Platform);
			platform->AddX(shift_x);
		};
	}

	method : public : GetPlatforms() ~ Vector {
		return @platforms;
	}

	method : public : Update()  ~ Nil {
		each(i : @platforms) {
			platform := @platforms->Get(i);
#~			
			if(platform->TypeOf(MovingPlatform)) {
				@platforms->Get(i)->As(MovingPlatform)->Update();
			};
~#			
		};
	}

	method : public : Draw()  ~ Nil {
		each(i : @platforms) {
			@platforms->Get(i)->As(Platform)->Draw();
		};
	}
}

consts Game {
	PLAYER_BOX_MIN := 120,
	PLAYER_BOX_MAX := 500,
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 600,
	WIDTH_MAX := -1600
}