use Game.SDL2;
use Game.Framework;
use Collection;

class Demo {
	@framework : GameFramework;
	@base_path : String;
	@player : Player;
	@background : ImageSprite;
	@level : Level;

	function : Main(args : String[]) ~ Nil {
		Demo->New()->Run();
	}

	New() {
		@framework := GameFramework->New(Game->SCREEN_WIDTH, Game->SCREEN_HEIGHT, "Accel");
		@framework->SetClearColor(Color->New(0, 0, 255));
		@base_path := "C:\\Users\\objec\\Documents\\Code\\objeck-lang\\programs\\sdl\\engine\\tests\\media";

		background_path := String->New(@base_path);
		background_path += "/images/background.png";
		@background := @framework->AddImageSprite(background_path);
	}

	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			@player := Player->New(@framework, @base_path);
			@level := Level->New(@player, @framework, @base_path);
			
			quit := false;
			e := @framework->GetEvent();
			frame_count := 0;
			while(<>quit) {
				@framework->FrameStart();
				@framework->Clear();
				
				# process input
				while(e->Poll() <> 0) {
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					};

					ProcessInput(e);
				};

				# update
				@player->Update();
				@level->Update();

				shift := @level->GetShift();
				if(@player->GetRight() >= Game->PLAYER_BOX_MAX) {
					diff := @player->GetRight() - Game->PLAYER_BOX_MAX;
					@player->SetRight(Game->PLAYER_BOX_MAX);
					if(shift > Game->WIDTH_MAX) {
						@level->ShiftWorld(-1 * diff);
					};
		        };

		        if(@player->GetLeft() <= Game->PLAYER_BOX_MIN) {
					diff := Game->PLAYER_BOX_MIN - @player->GetLeft();
					@player->SetLeft(Game->PLAYER_BOX_MIN);
					if(shift < 0) {
						@level->ShiftWorld(diff);
					};
				};

				# draw
				Background();
				@player->Draw();
				@level->Draw(frame_count);
				
				# show
				@framework->Show();
				@framework->FrameEnd();
				
				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	method : public : Background() ~ Nil {
		shift := @level->GetShift() / 3;
		@background->Render(shift, 0);
		@background->Render(shift + @background->GetWidth(), 0);	
	}

	method : public : ProcessInput(e : Event) ~ Nil {
		if(e->GetType() = EventType->SDL_KEYDOWN) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@player->GoLeft();
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@player->GoRight();
	            }

	            label Scancode->SDL_SCANCODE_UP: {
	            	@player->Jump(true);
	            }
	        };
	    }
	    else if(e->GetType() = EventType->SDL_KEYUP) {
	       	select(e->GetKey()->GetKeysym()->GetScancode()) {
		       	label Scancode->SDL_SCANCODE_LEFT:
		        label Scancode->SDL_SCANCODE_RIGHT: {
		        	@player->Stop();
		        }

		        label Scancode->SDL_SCANCODE_UP: {
		        	@player->Jump(false);
		       	}
		    };
	    };
	}
}

class Player {
	@rect : AnimatedImageSprite;
	@loc : Vector2;
	@vel : Vector2;
	@acc : Vector2;
	@jumped : Bool;
	@jump_max : Int;
	@hit_top : Bool;
	@level : Level;
	
	New(framework : GameFramework, base_path : String) {
		clips := Vector->New();
		clips->AddBack(Rect->New(0, 0, 32, 52));
		clips->AddBack(Rect->New(32, 0, 32, 52));
		clips->AddBack(Rect->New(64, 0, 32, 52));
		clips->AddBack(Rect->New(96, 0, 32, 52));
		clips->AddBack(Rect->New(128, 0, 32, 52));
		clips->AddBack(Rect->New(160, 0, 32, 52));

		player_path := String->New(base_path);
		player_path += "/images/player.png";
		@rect := framework->AddAnimatedImageSprite(player_path, clips);
		
		@loc := Vector2->New(Game->SCREEN_WIDTH / 2, Game->SCREEN_HEIGHT / 2);
		@vel := Vector2->New();
		@acc := Vector2->New();
	}

	method : public : SetLevel(level : Level) ~ Nil {
		@level := level;
	}

	method : public : GetRect() ~ AnimatedImageSprite {
		return @rect;
	}

	method : public : GetLeft() ~ Int {
		return @loc->GetX() - @rect->GetWidth() / 2;
	}

	method : public : GetRight() ~ Int {
		return @loc->GetX() + @rect->GetWidth() / 2;
	}

	method : public : SetLeft(x : Int) ~ Nil {
		@loc->SetX(x + @rect->GetWidth() / 2);
	}

	method : public : SetRight(x : Int) ~ Nil {
		@loc->SetX(x - @rect->GetWidth() / 2);
	}

	method : public : GoLeft()  ~ Nil {
		if(@rect->GetFlip() = RendererFlip->SDL_FLIP_NONE) {
			@rect->SetFlip(RendererFlip->SDL_FLIP_HORIZONTAL);
		};

		@acc->SetX(-0.75);
	}

	method : public : GoRight()  ~ Nil {
		if(@rect->GetFlip() = RendererFlip->SDL_FLIP_HORIZONTAL) {
			@rect->SetFlip(RendererFlip->SDL_FLIP_NONE);
		};

		@acc->SetX(0.75);
	}

	method : public : Stop()  ~ Nil {
	}
	
	method : public : Jump(jumped : Bool)  ~ Nil {
		@jumped := jumped;
	}

	method : public : Update()  ~ Nil {
		# movement logic
		if(@hit_top) {
			@acc->SetY(80);
			@hit_top := false;
		}
		else {
			if(@jumped & @jump_max < 3) {
				@acc->SetY(-5.3);
				@jump_max += 1;
			}
			else {
				@acc->SetY(0.65);
			};
		};

		@acc->Add(Vector2->Mul(@vel, -0.0625));
		@vel->Add(Vector2->Mul(@acc, 0.75));
		@loc->Add(@vel);
		@acc->Zero();
		
		# collisions
		CheckPlatforms();
		CheckGems();
		CheckFloorCeiling();
	}

	method : public : CheckPlatforms() ~ Nil {
		platforms := @level->GetPlatforms();

		# reposition 'x'
		@rect->SetCenterX(@loc->GetX());
		collides := Level->PlatformCollisions(@self, platforms);
        each(i : collides) {
        	platform := collides->Get(i)->As(Platform);
			if(@vel->GetX() > 0.01) {
    			@rect->SetRight(platform->GetLeft());
    			@loc->SetX(platform->GetLeft() - @rect->GetWidth() / 2);
    			@vel->MulX(0);
            }
    		else if(@vel->GetX() < -0.01) {
            	@rect->SetLeft(platform->GetRight());
            	@loc->SetX(platform->GetRight() + @rect->GetWidth() / 2);
            	@vel->MulX(0);
            };
        };

		# reposition 'y'
		@rect->SetBottom(@loc->GetY() - @rect->GetHeight() / 2);
		collides := Level->PlatformCollisions(@self, platforms);
        each(i : collides) {
        	platform := collides->Get(i)->As(Platform);
       		if(@vel->GetY() < 0.01) {
				@rect->SetTop(platform->GetBottom());
            	@loc->SetY(platform->GetBottom() + @rect->GetHeight() / 2);
				@jump_max := 3;
				@hit_top := true;
            }
            else if(@vel->GetY() > 0.01) {
    			@rect->SetBottom(platform->GetTop());
    			@loc->SetY(platform->GetTop() + @rect->GetHeight() / 2);
				@vel->SetY(0);
				@jump_max := 0;
            };
        };
	}

	method : public : CheckGems() ~ Nil {
		collides := Level->GemsCollisions(@self, @level->GetGems());
        each(i : collides) {
    		gem := collides->Get(i)->As(Gem);
    		gem->Hide();
    	};
    }

    method : public : CheckFloorCeiling() ~ Nil {
    	if(@rect->GetBottom() > Game->SCREEN_HEIGHT) {
			@rect->SetBottom(Game->SCREEN_HEIGHT);
        	@loc->SetY(Game->SCREEN_HEIGHT + @rect->GetHeight() / 2);
			@jump_max := 0;
			@vel->SetY(0);
		}
		else if(@loc->GetY() < 0) {
			@loc->SetY(0);
			@vel->SetY(0);
		};
    }

	method : public : Draw()  ~ Nil {
		if(@jump_max > 0) {
			@rect->Render(5);
		}
		else if(@vel->GetX() > 0.075 | @vel->GetX() < -0.075) {
			offset := @level->GetShift() + @loc->GetX();
			index := Int->Abs((offset / 15)->As(Int) % 4);
			@rect->Render(index);
		}
		else {
			@rect->Render(4);
		};
	}
}

class Platform {
	@rect : Rectangle;

	New(x : Int, y : Int, rect : Rectangle) {
		@rect := rect;
		@rect->SetFill(true);
		@rect->SetColor(Color->New(50, 51, 52));
		@rect->Set(x, y);
	}

	method : public : AddX(x : Int)  ~ Nil {
		@rect->AddX(x);
	}

	method : public : GetLeft()  ~ Int {
		return @rect->GetLeft();
	}

	method : public : GetRight()  ~ Int {
		return @rect->GetRight();
	}

	method : public : GetTop()  ~ Int {
		return @rect->GetTop();
	}

	method : public : GetBottom()  ~ Int {
		return @rect->GetBottom();
	}

	method : public : Draw()  ~ Nil {
		@rect->Render();
	}

	method : public : Collides(rect : AnimatedImageSprite) ~ Bool {
		return @rect->GetPosition()->Overlaps(rect->GetPosition());
	}
}

class MovingPlatform from Platform {
	@change_x : Int;
    @change_y : Int;
    @boundary_top : Int;
    @boundary_bottom : Int;
    @boundary_left : Int;
    @boundary_right : Int;
    @player : Player;
    @level : Level;

	New(x : Int, y : Int, rect : Rectangle, player : Player, level : Level) {
		Parent(x, y, rect);
		@rect->SetColor(Color->New(207, 181, 59));
		@player := player;
		@level := level;

		@boundary_top := 400;
    	@boundary_bottom := 580;
    	@change_y := 1;
	}

	method : public : GetChangeLeft() ~ Int {
		return @change_x;
	}

	method : public : SetLevel(level : Level)  ~ Nil {
		@level := level;
	}

	method : public : native : Update()  ~ Nil {
		@rect->AddX(@change_x);

		if(Collides(@player->GetRect())) {
			if(@change_x < 0) {
                @player->GetRect()->SetRight(@rect->GetLeft());
            }
            else {
            	@player->GetRect()->SetLeft(@rect->GetRight());
 			};
		};

		@rect->AddY(@change_y);
		if(Collides(@player->GetRect())) { 
			if(@change_y < 0) {
                @player->GetRect()->SetBottom(@rect->GetTop());
			}
            else {
				@player->GetRect()->SetTop(@rect->GetBottom());
            };
		};

		if(@rect->GetBottom() > @boundary_bottom | @rect->GetTop() < @boundary_top) {
			@change_y *= -1;
		};

		cur_pos := @rect->GetLeft() - @level->GetShift();
        if(cur_pos < @boundary_left | cur_pos > @boundary_right) {
			@change_x *= -1;
        };
	}
}

class Gem {
	@rect : AnimatedImageSprite;
	@hide : Bool;

	New(x : Int, y : Int, rect : AnimatedImageSprite) {
		@rect := rect;
		@rect->Set(x, y);
	}

	method : public : Hide() ~ Nil {
		@hide := true;
	}

	method : public : AddX(x : Int)  ~ Nil {
		@rect->AddX(x);
	}

	method : public : GetLeft()  ~ Int {
		return @rect->GetLeft();
	}

	method : public : GetRight()  ~ Int {
		return @rect->GetRight();
	}

	method : public : GetTop()  ~ Int {
		return @rect->GetTop();
	}

	method : public : GetBottom()  ~ Int {
		return @rect->GetBottom();
	}

	method : public : Draw(frame_count : Int)  ~ Nil {
		if(<>@hide) {
			index := frame_count / 6 % @rect->GetClipCount();
			@rect->Render(index);
		};
	}

	method : public : Collides(rect : ImageSprite) ~ Bool {
		if(@hide) {
			return false;
		}
		else {
			return @rect->GetPosition()->Overlaps(rect->GetPosition());
		};
	}
}

class Level {
	@player : Player;
	@platforms : Vector;
	@gems : Vector;
	@shift_x : Int;

	New(player : Player, framework : GameFramework, base_path : String) {
		@player := player;
		@player->SetLevel(@self);

		@platforms := GeneratePlatforms(framework);
		@gems := GenerateGems(base_path, framework);
	}

	method : GeneratePlatforms(framework : GameFramework) ~ Vector {
		platforms := Vector->New();

		platforms->AddBack(MovingPlatform->New(200, 450, framework->AddRectangle(80, 20), @player, @self));		
		platforms->AddBack(Platform->New(620, 535, framework->AddRectangle(110, 20)));
		platforms->AddBack(Platform->New(700, 485, framework->AddRectangle(120, 15)));
		platforms->AddBack(Platform->New(820, 450, framework->AddRectangle(210, 20)));
		platforms->AddBack(Platform->New(1025, 545, framework->AddRectangle(210, 15)));

		return platforms;
	}

	method : GenerateGems(base_path : String, framework : GameFramework) ~ Vector {
		gems := Vector->New();

		gems_path := String->New(base_path);
		gems_path += "/images/gems.png";

		clips := Vector->New();
		clips->AddBack(Rect->New(0, 0, 32, 32));
		clips->AddBack(Rect->New(32, 0, 32, 32));
		clips->AddBack(Rect->New(64, 0, 32, 32));
		clips->AddBack(Rect->New(96, 0, 32, 32));
		clips->AddBack(Rect->New(128, 0, 32, 32));
		clips->AddBack(Rect->New(160, 0, 32, 32));
		
		gems->AddBack(Gem->New(600, 500, framework->AddAnimatedImageSprite(gems_path, clips)));		
		gems->AddBack(Gem->New(500, 400, framework->AddAnimatedImageSprite(gems_path, clips)));

		return gems;
	}

	method : public : GetShift() ~ Int {
		return @shift_x;
	}

	method : public : ShiftWorld(shift_x : Int) ~ Nil {
		@shift_x += shift_x;

		each(i : @platforms) {
			platform := @platforms->Get(i)->As(Platform);
			platform->AddX(shift_x);
		};

		each(i : @gems) {
			platform := @gems->Get(i)->As(Gem);
			platform->AddX(shift_x);
		};
	}

	method : public : GetPlatforms() ~ Vector {
		return @platforms;
	}

	method : public : GetGems() ~ Vector {
		return @gems;
	}

	function : PlatformCollisions(player : Player, obstacles : Vector) ~ Vector {
		collides := Vector->New();

		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Platform);
        	if(obstacle->Collides(player->GetRect())) {
        		collides->AddBack(obstacle);
        	};
        };

        return collides;
    }

    function : GemsCollisions(player : Player, obstacles : Vector) ~ Vector {
		collides := Vector->New();

		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Gem);
        	if(obstacle->Collides(player->GetRect())) {
        		collides->AddBack(obstacle);
        	};
        };

        return collides;
    }

	method : public : Update()  ~ Nil {
		each(i : @platforms) {
			platform := @platforms->Get(i);
			if(platform->TypeOf(MovingPlatform)) {
				platform->As(MovingPlatform)->Update();
			};
		};
	}

	method : public : Draw(frame_count : Int)  ~ Nil {
		each(i : @platforms) {
			@platforms->Get(i)->As(Platform)->Draw();
		};

		each(i : @gems) {
			@gems->Get(i)->As(Gem)->Draw(frame_count);
		};
	}
}

consts Game {
	PLAYER_BOX_MIN := 120,
	PLAYER_BOX_MAX := 500,
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 600,
	WIDTH_MAX := -1600
}