#~
Game demo v1
link: https://github.com/objeck/objeck-lang/blob/master/programs/sdl/engine/tests/sdl_test_1.obs
~#

use SDL2;
use SDL2.Game;

class PlatformDemo {
	@game : GameEngine;
	@game_map : Int[,];
	@game_frame : Int;

	@player : RectangleShape;
	
	@space : RectangleShape;
	@solid : RectangleShape;
	@test : RectangleShape;
	@test2 : RectangleShape;

	@player_x_position : Int;
	@player_y_position : Int;

	@player_x_direction : Int;
	@player_falling : Bool;
	@player_jumping : Bool;
	@player_jump_height : Int;

	@player_speed : Int;
	
	function : Main(args : String[]) ~ Nil {
		platform := PlatformDemo->New();
		platform->Run();
	}

	New() {
		@game := GameEngine->New(Size->SCREEN_WIDTH, Size->SCREEN_HEIGHT, "Engine One");
		@game->SetClearColor(Color->New());

		player := @game->AddRectangle(Size->BLOCK_WIDTH, Size->BLOCK_HEIGHT * 2);
		player->SetFill(true);
		player->SetColor(Color->New(12, 35, 64));
		@player := RectangleShape->New(player);
		@player_speed := Size->BLOCK_WIDTH / 4;
		
		@player_y_position := Size->SCREEN_HEIGHT - Size->BLOCK_WIDTH * 4;
		
		space := @game->AddRectangle(Size->BLOCK_WIDTH, Size->BLOCK_HEIGHT);
		space->SetColor(Color->New(0, 177, 64));
		@space := RectangleShape->New(space);

		test := @game->AddRectangle(Size->BLOCK_WIDTH, Size->BLOCK_HEIGHT);
		test->SetColor(Color->New(255, 255, 51));
		@test := RectangleShape->New(test);

		test2 := @game->AddRectangle(Size->BLOCK_WIDTH, Size->BLOCK_HEIGHT);
		test2->SetColor(Color->New(255, 255, 250));
		@test2 := RectangleShape->New(test2);

		solid := @game->AddRectangle(Size->BLOCK_WIDTH, Size->BLOCK_HEIGHT);
		solid->SetFill(true);
		solid->SetColor(Color->New(204, 0, 0));
		@solid := RectangleShape->New(solid);

		@game_map := CreateMap();
	}

	method : Run() ~ Nil {
		if(@game->IsOk()) {
			e := @game->GetEvent();
			quit := false;
			while(<>quit) {
				@game->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					};
					ProcessInput(e);
				};

				UpdateEvents();
				Render();

				@game->FrameEnd();
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@game->Quit();
		};
	}
	
	method : public : ProcessInput(e : Event) ~ Nil {
		if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@player_x_direction := -1;
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@player_x_direction := 1;
	            }

	            label Scancode->SDL_SCANCODE_SPACE: {
	            	if(<>@player_jumping & <>@player_falling) {
	            		@player_jumping := true;
	            	};
	            }
	        };
	    }
	    else if(e->GetType() = EventType->SDL_KEYUP & e->GetKey()->GetRepeat() = 0) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {
	        	label Scancode->SDL_SCANCODE_LEFT:
	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@player_x_direction := 0;
	            }
	        };
	    };   
	}

	method : native : UpdateEvents() ~ Nil {		
		@game_frame += 1;

		if(@game_frame <= @game->GetFps()) {
			if(@game_frame % 2 = 0) {
				# move left
				if(@player_x_direction = -1) {
					@player_x_position -= @player_speed;
				}
				# move right
				else if(@player_x_direction = 1) {
					@player_x_position += @player_speed;
				};

				# move up
				if(@player_jumping) {
					if(@player_jump_height < 1.5 * Size->BLOCK_HEIGHT) {
						@player_y_position -= @player_speed;
						@player_jump_height += @player_speed;
						@player_falling := false;
					}
					else {
						@player_jump_height := 0;
						@player_jumping := false;
						@player_falling := true;
					};
				};

				# falling
				if(@player_falling) {
					@player_y_position += @player_speed;
				};
			};	
		}
		else {
			@game_frame := 1;
		};
	}

	method : Render() ~ Nil {
		@game->Clear();

		RenderMap();
		
		@game->Show();
	}

	method : native : RenderMap() ~ Nil {
		dims := @game_map->Size();

		view_x_offset := @player_x_position / Size->BLOCK_WIDTH;
		view_y_offset := @player_y_position / Size->BLOCK_HEIGHT;

		y_end := dims[0];
		x_end := view_x_offset + 21; # TODO: load map from file
		if(x_end > dims[1]) {			
			x_end := dims[1];	
		};

		for(x := view_x_offset; x < x_end; x += 1;) {
			pixel_x := x * Size->BLOCK_WIDTH - @player_x_position;

			for(y := 0; y < y_end; y += 1;) {
				pixel_y := y * Size->BLOCK_HEIGHT;

				if(@game_map[y, x] > 0) {
					@solid->Set(pixel_x, pixel_y);
					@solid->Render();

					PlayerCollision(@solid);

					# TODO: enemy collisions
 					EnemyCollisions(@solid);
				}
				else {
					# left player blocks
					render_space := true;

					if(x - view_x_offset = 2) {
						if(pixel_y + Size->BLOCK_HEIGHT > @player_y_position + Size->BLOCK_HEIGHT * 2 & 
							pixel_y + Size->BLOCK_HEIGHT <= @player_y_position + Size->BLOCK_HEIGHT * 3) {
							
							if(@test->GetCenterX() = @player->GetCenterX()) {
								@player_falling := true;
							};

							render_space := false;
							@test->Set(pixel_x, pixel_y);
							@test->Render();
						};
					}
					# right player blocks
					else if(x - view_x_offset = 3) {
						if(pixel_y + Size->BLOCK_HEIGHT > @player_y_position + Size->BLOCK_HEIGHT * 2 & 
							pixel_y + Size->BLOCK_HEIGHT <= @player_y_position + Size->BLOCK_HEIGHT * 3) {
							
							render_space := false;
							@test2->Set(pixel_x, pixel_y);
							@test2->Render();
							
						};
					};
					
					if(render_space) {
						@space->Set(pixel_x, pixel_y);
						@space->Render();
					};
				};

				@player->Set(Size->BLOCK_WIDTH * 2, @player_y_position);

				@player->Render();
			};
		};
	}

	method : EnemyCollisions(block : RectangleShape) ~ Nil {

	}

	method : PlayerCollision(block : RectangleShape) ~ Bool {
		if(block->Overlaps(@player)) {
			# right
			if(@player_x_direction = 1) {
				@player_x_position -= @player_speed;
				
			}
			# left
			else if(@player_x_direction = -1) {
				@player_x_position += @player_speed;
			};

			player_bottom := @player->GetY2() - @player_speed;
			block_top := block->GetY();
			if(player_bottom <> block_top) {
				@player_x_direction := 0;
			};

			# top
			if(@player_jumping) {
				@player_y_position += @player_speed;
				@player_jumping := false;
				@player_jump_height := 0;
				@player_falling := true;
			}
			# bottom
			else if(@player_falling) {
				@player_y_position -= @player_speed; # reset
				@player_falling := false;
			};

			return true;
		};

		return false;
	}

	method : native : CreateMap() ~ Int[,] {
		game_map := [
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 1, 0, 0, 1, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,   0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
			[0, 1, 1, 1, 1, 1, 0, 0, 1, 1, 0, 1, 1, 1, 1, 1, 1, 1, 1, 1,   1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 0]
			[1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1,   1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1]
		];

		return game_map;
	}
}

class RectangleShape {
	@rect : Rectangle;
	@x : Int;
	@y : Int;

	New(rect : Rectangle) {
		@rect := rect;
	}

	method : public : GetRect() ~ Rectangle {
		return @rect;
	}

	method : public : Set(x : Int, y : Int) ~ Nil {
		@x := x; @y := y;
	}

	method : public : GetX() ~ Int {
		return @x;
	}

	method : public : GetX2() ~ Int {
		return @x + @rect->GetWidth();
	}

	method : public : GetCenterX() ~ Int {
		return GetX2() / 2;
	}

	method : public : GetY() ~ Int {
		return @y;
	}

	method : public : GetY2() ~ Int {
		return @y + @rect->GetHeight();
	}

	method : public : GetCenterY() ~ Int {
		return GetY2() / 2;
	}

	method : public : Render() ~ Nil {
		@rect->Render(@x, @y);
	}

	method : public : native : Overlaps(rect : RectangleShape) ~ Bool {
		right_a := rect->GetX();
		right_b := GetX2();

		left_a := GetX();
		left_b := rect->GetX2();

		top_a := GetY();
		top_b := rect->GetY2();

		bottom_a := rect->GetY();
		bottom_b := GetY2();

		return right_b > right_a & left_a < left_b & top_b > top_a & bottom_a < bottom_b;
	}

	method : public : Contains(x : Int, y : Int) ~ Bool {
		return false;
	}
}

consts Size {
	SCREEN_WIDTH := 640,
	SCREEN_HEIGHT := 480,
	BLOCK_WIDTH := 32,
	BLOCK_HEIGHT := 32
}