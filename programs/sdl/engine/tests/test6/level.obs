use Game.SDL2;
use Game.Framework;
use Collection;
use Data.CSV;

class Level {
	@player : Player;
	@platforms : Vector;
	@sprite_sheet : ImageSprite;
	@shift_x : Int;

	New(player : Player, framework : GameFramework, media_path : String) {
		@player := player;
		@platforms := Vector->New();

		sprite_path := String->New(media_path);
		sprite_path += "/media/tiles_spritesheet.png";
		@sprite_sheet := framework->AddImageSprite(sprite_path, Color->New(0, 0, 0));
				
		map_path := String->New(media_path);	
		map_path += "/media/foo.csv";
		LoadLevel(map_path);

		@player->SetLevel(@self);
	}

	function : Collides(player : Player, obstacles : Vector) ~ Bool {
		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Platform);
        	if(obstacle->Collides(player->GetRect())) {
        		return true;
        	};
        };

        return false;
	}

	function : Collisions(player : Player, obstacles : Vector) ~ Vector {
		collides := Vector->New();

		each(i : obstacles) {
        	obstacle := obstacles->Get(i)->As(Platform);
        	if(obstacle->Collides(player->GetRect())) {
        		collides->AddBack(obstacle);
        	};
        };

        return collides;
    }

    method : public : LoadLevel(name : String) ~ Nil {
    	block_3 := Rect->New(3 * Game->BLOCK_WIDTH, 0 * Game->BLOCK_HEIGHT, Game->BLOCK_WIDTH, Game->BLOCK_HEIGHT);
		block_6 := Rect->New(6 * Game->BLOCK_WIDTH, (0 * Game->BLOCK_HEIGHT) + (Game->BLOCK_HEIGHT / 2 - 3), Game->BLOCK_WIDTH, 6);
    	block_102 := Rect->New(6 * Game->BLOCK_WIDTH, 8 * Game->BLOCK_HEIGHT, Game->BLOCK_WIDTH, Game->BLOCK_HEIGHT);
    	block_103 := Rect->New(7 * Game->BLOCK_WIDTH, 8 * Game->BLOCK_HEIGHT, Game->BLOCK_WIDTH, Game->BLOCK_HEIGHT);
		block_104 := Rect->New(8 * Game->BLOCK_WIDTH, 8 * Game->BLOCK_HEIGHT, Game->BLOCK_WIDTH, Game->BLOCK_HEIGHT);
		block_114 := Rect->New(7 * Game->BLOCK_WIDTH, 8 * Game->BLOCK_HEIGHT, Game->BLOCK_WIDTH, Game->BLOCK_HEIGHT);
		
    	table := CsvTable->New(System.IO.File.FileReader->ReadFile(name));
    	if(table->IsParsed()) {
    		each(i : table) {
    			row := table->Get(i);
    			each(j : row) {
    				value := row->Get(j);
    				y := i * (Game->BLOCK_WIDTH - 2);
    				x := j * (Game->BLOCK_HEIGHT - 2);

					select(value->ToInt()) {
						label 3: {
    						@platforms->AddBack(Platform->New(x, y, block_3, @sprite_sheet));
						}

						label 6: {
    						@platforms->AddBack(MovingPlatform->New(x, y, block_6, @sprite_sheet, @player, @self));
						}
						
						label 102: {
	    					@platforms->AddBack(Platform->New(x, y, block_102, @sprite_sheet));
						}

						label 103: {
	    					@platforms->AddBack(Platform->New(x, y, block_103, @sprite_sheet));
						}

						label 104: {
	    					@platforms->AddBack(Platform->New(x, y, block_104, @sprite_sheet));
						}

						label 114: {
	    					@platforms->AddBack(Platform->New(x, y, block_114, @sprite_sheet));
						}

						other: {
"Missing: value={$value}, loc=[{$x},{$y}]"->PrintLine();
						}
					};
    			};
    		};
    	}
    	else {
    		"Unable to load map file!"->ErrorLine();
    	};
    }

	method : public : GetShift() ~ Int {
		return @shift_x;
	}

	method : public : ShiftWorld(shift_x : Int) ~ Nil {
		@shift_x += shift_x;

		each(i : @platforms) {
			platform := @platforms->Get(i)->As(Platform);
			platform->AddX(shift_x);
		};
	}

	method : public : GetPlatforms() ~ Vector {
		return @platforms;
	}

	method : public : Update()  ~ Nil {
		each(i : @platforms) {
			platform := @platforms->Get(i);
			if(platform->TypeOf(MovingPlatform)) {
				@platforms->Get(i)->As(MovingPlatform)->Update();
			};
		};
	}

	method : public : Draw()  ~ Nil {
		each(i : @platforms) {
			platform := @platforms->Get(i)->As(Platform);			
			if(platform->TypeOf(MovingPlatform)) {
				platform->As(MovingPlatform)->Draw();
			}
			else {
				platform->Draw();
			};
		};
	}
}