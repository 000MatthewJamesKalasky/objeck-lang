use SDL2;

class Sprite {
	@renderer : Renderer;
	@font : Font;
	@texture : Texture;
	@width : Int;
	@height : Int;

	New(renderer : Renderer, font : Font := Nil) {
		@renderer := renderer;
		@font := font;
	}

	method : public : LoadFromRenderedText(textureText : String, textColor : Color) ~ Bool {
		Free();

		textSurface := @font->RenderTextSolid(textureText, textColor);
		if(textSurface->IsNull()) {
			"Unable to render text surface!"->ErrorLine();
			return false;
		};

		texture := textSurface->CreateTexture(@renderer);
		@width := textSurface->GetW();
		@height := textSurface->GetH();
		
		@texture := texture;
		return @texture <> Nil;
	}

	method : public : LoadFromFile(path : String) ~ Bool {
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return false;
		};

		loadedSurface->SetColorKey(true, loadedSurface->GetPixelFormat()->MapRGB(0, 0xFF, 0xFF));

		texture := loadedSurface->CreateTexture(@renderer);
		if(texture->IsNull()) {
			"Unable to create texture!"->ErrorLine();
			return false;
		};

		@width := loadedSurface->GetW();
		@height := loadedSurface->GetH();
		
		loadedSurface->Free();
		@texture := texture;

		return @texture <> Nil;
	}

	method : public : Free() ~ Nil {
		if(@texture <> Nil)	{
			@texture->Destroy();
		};
	}

	method : public : SetColor(red : Int, green : Int, blue : Int) ~ Nil {
		@texture->SetColorMod(red, green, blue);
	}

	method : public : SetBlendMode(blending : BlendMode) ~ Nil {
		@texture->SetBlendMode(blending);
	}
	
	method : public : SetAlpha(alpha : Int) ~ Nil {
		@texture->SetAlphaMod(alpha );
	}

	method : public : Render(x : Int, y : Int, clip : Rect := Nil) ~ Nil {
		render_quad := Rect->New(x, y, @width, @height);

		if(clip <> Nil)	{
			render_quad->SetW(clip->GetW());
			render_quad->SetH(clip->GetH());
		};

		@renderer->Copy(@texture, clip, render_quad);
	}

	method : public : GetWidth() ~ Int {
		return @width;
	}

	method : public : GetHeight() ~ Int {
		return @height;
	}
}

consts Environment {
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 600,
	FPS := 30,
	RED_ZONE := 60
}

class Paddle {
	FRAME_RATE : static : Int;
		
	@window : Window;
	@renderer : Renderer;

	@paddle_sprite : Sprite;
	@ball_sprite : Sprite;
	@text_texture : Sprite;
	@score_color : Color;
	
	@ball_x : Int;
	@ball_y : Int;
	@ball_x_dir : Int;
	@ball_y_dir : Int;
	@ball_speed : Int;

	@paddle_x : Int;
	@paddle_y : Int;

	@playing : Bool;
	@score : Int;
	
	function : Main(args : String[]) ~ Nil {
		game := Paddle->New();
		game->Run();
	}

	New() {
	}

	method : Run() ~ Nil {
		if(<>Init()) {
			"Failed to initialize!"->ErrorLine();
			return;
		};
		
		if(<>LoadMedia()) {
			"Failed to load media!"->ErrorLine();
			return;
		};

		FRAME_RATE := 1000 / Environment->FPS->As(Int);
		
		quit := false;
		e := Event->New();

		Reset();

		frame := 0;

		while(<>quit) {
			start := Timer->GetTicks();

			if(frame = Environment->FPS->As(Int) * 10) {
				@score += 15;
				UpdateScore();

				frame := 0;
			};

			while( e->Poll() <> 0 ) {
				# User requests quit
				if(e->GetType() = EventType->SDL_QUIT) {
					quit := true;
				};
			};
			Paddle(Keyboard->GetState());
			
			Ball();
			Render();

			end := start - Timer->GetTicks();
			if(end < FRAME_RATE) {
				Timer->Delay(FRAME_RATE - end);
			};
			frame += 1;
		};

		e->Free();

		Quit();
	}

	method : native : Paddle(key_states : Byte[]) ~ Nil {
		if(@playing) {
			if(key_states[Scancode->SDL_SCANCODE_LEFT] <> 0 ) {
				if(@paddle_x > 0) {
					@paddle_x -= @ball_speed;
					if(@paddle_x < 0) {
						@paddle_x := 0;
					};
				};
			}
			else if(key_states[Scancode->SDL_SCANCODE_RIGHT] <> 0) {
				if(@paddle_x + @paddle_sprite->GetWidth() < Environment->SCREEN_WIDTH->As(Int)) {
					@paddle_x += @ball_speed;
					if(@paddle_x + @paddle_sprite->GetWidth() > Environment->SCREEN_WIDTH->As(Int)) {
						@paddle_x := Environment->SCREEN_WIDTH->As(Int) - @paddle_sprite->GetWidth();
					};
				};
			};
		}
		else if(key_states[Scancode->SDL_SCANCODE_UP] <> 0) {
			@playing := true;
		};
	}

	method : Reset() ~ Nil {
		@score := 0;
		UpdateScore();

		@ball_speed := 8;

		@ball_x_dir := 1;
		@ball_y_dir := -1;

		@ball_x := Environment->SCREEN_WIDTH->As(Int) / 2 - @ball_sprite->GetWidth() / 2;
		@ball_y := Environment->SCREEN_HEIGHT->As(Int) - @paddle_sprite->GetHeight() - 
			@ball_sprite->GetHeight() - Environment->RED_ZONE->As(Int);

		@paddle_x := Environment->SCREEN_WIDTH->As(Int) / 2 - @paddle_sprite->GetWidth() / 2;
		@paddle_y := Environment->SCREEN_HEIGHT->As(Int) - @paddle_sprite->GetHeight() - 
			Environment->RED_ZONE->As(Int);

		@playing := false;
	}

	method : native : Ball() ~ Nil {
		if(<>@playing) {
			return;
		};

		# hit top
		if(@ball_y <= 0) {
			@ball_y_dir := 1;
		}
		# hit bottom
		else if(@ball_y + @ball_sprite->GetHeight() >= Environment->SCREEN_HEIGHT->As(Int)) {
			Reset();
			return;
		};

		# hit left
		if(@ball_x <= 0) {
			@ball_x_dir := 1;
		}
		# hit right
		else if(@ball_x + @ball_sprite->GetWidth() >= Environment->SCREEN_WIDTH->As(Int)) {
			@ball_x_dir := -1;
		};

		y_bounds := Environment->SCREEN_HEIGHT->As(Int) - @paddle_sprite->GetHeight() - Environment->RED_ZONE->As(Int);
		ball_bottom := @ball_y + @ball_sprite->GetHeight();
		if(ball_bottom >= y_bounds & y_bounds + @paddle_sprite->GetHeight() > ball_bottom) {
			ball_left := @ball_x;
			ball_right := @ball_x + @ball_sprite->GetWidth();

			paddle_left := @paddle_x;
			paddle_right := @paddle_x + @paddle_sprite->GetWidth();

			if(ball_left <= paddle_right & ball_right >= paddle_left) {
				@score += 5;
				UpdateScore();

				@ball_y_dir := -1;
				
				rand_dir := (Float->Random() * 100.0)->As(Int) % 5;
				@ball_x += @ball_x_dir * @ball_speed + rand_dir;

				rand_speed := (Float->Random() * 100.0)->As(Int) % 10;
				if(rand_speed < 2) {
					@ball_speed -= 1;					
				}
				else if(rand_speed > 5) {
					@ball_speed += 2;
				};
			};
		};

		@ball_x += @ball_x_dir * @ball_speed;
		@ball_y += @ball_y_dir * @ball_speed;
	}

	method : Render() ~ Nil {
		@renderer->SetDrawColor(70, 130, 180, 0);
		@renderer->Clear();

		@ball_sprite->Render(@ball_x, @ball_y);
		@paddle_sprite->Render(@paddle_x, @paddle_y);
		@text_texture->Render(10, 10);
		
		@renderer->Present();
	}

	method : Init() ~ Bool {
		if(Core->Init(HardwareFlags->SDL_INIT_VIDEO->As(Int)) < 0) {
			"SDL could not initialize!"->ErrorLine();
			return false;
		};
		
		if(<>Hints->Set("SDL_RENDER_SCALE_QUALITY", "1")) {
			"Warning: Linear texture filtering not enabled!"->ErrorLine();
			return false;
		};

		@window := Window->New("SDL Tutorial", WindowFlags->SDL_WINDOWPOS_UNDEFINED, 			
			WindowFlags->SDL_WINDOWPOS_UNDEFINED, Environment->SCREEN_WIDTH->As(Int), 
			Environment->SCREEN_HEIGHT->As(Int), WindowFlags->SDL_WINDOW_SHOWN);
		if(@window->IsNull()) {
			"Cannot create window!"->ErrorLine();
			return false;
		};

		@renderer := Renderer->New(@window, -1, 
			(RendererFlags->SDL_RENDERER_ACCELERATED and RendererFlags->SDL_RENDERER_PRESENTVSYNC)->As(Int));
		if(@renderer->IsNull()) {
			"Cannot create renderer!"->ErrorLine();
			return false;
		};
		@renderer->SetDrawColor(0xFF, 0xFF, 0xFF, 0xFF);

		@paddle_sprite := Sprite->New(@renderer);
		@ball_sprite := Sprite->New(@renderer);

		img_flags := ImageFlags->IMG_INIT_PNG->As(Int);
		if((Image->Init(img_flags) and img_flags) = 0) {
			"SDL image could not initialize!"->ErrorLine();
			return false;
		};

		if(Font->Init() < 0) {
			"SDL_ttf could not initialize!"->ErrorLine();
			return false;
		};

		@font := Font->New("images/digital.ttf", 28);
		if(@font->IsNull()) {
			"Failed to load font!"->ErrorLine();
			return false;
		};
		@text_texture := Sprite->New(@renderer, @font);
		
		return true;
	}

	method : UpdateScore() ~ Bool {
		if(<>@text_texture->LoadFromRenderedText( "Score: {$@score}", @score_color)) {
			"Failed to render text texture!"->ErrorLine();
			return false;
		};

		return true;
	}
	
	method : LoadMedia() ~ Bool {
		if(<>@paddle_sprite->LoadFromFile("images/paddle.png")) {
			return false;
		};

		if(<>@ball_sprite->LoadFromFile("images/ball.png")) {
			return false;
		};
		@score_color := Color->New(245, 245, 245);
		
		return true;
	}
	
	method : LoadTexture(path : String) ~ Texture {
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return Nil;
		};

		texture := loadedSurface->CreateTexture(@renderer);
		if(texture->IsNull()) {
			"Unable to create texture!"->ErrorLine();
			return Nil;
		};

		loadedSurface->Free();
		
		return texture;
	}
	
	method : Quit() ~ Nil {
		@paddle_sprite->Free();
		@ball_sprite->Free();
		@text_texture->Free();
		@renderer->Destroy();
		@window->Destroy();

		Image->Quit();
		Font->Quit();
		Core->Quit();
	}
}
