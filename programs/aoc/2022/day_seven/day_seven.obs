use Collection.Generic;

class AoC {
	@debug : static : Bool;

	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			@debug := false;
			inputs := ReadInput(args[0])<String>;

			commands := Vector->New()<Command>;
			each(i : inputs) {
				commands->AddBack(ParseCommand(inputs->Get(i)));
			};

			ExecuteCommands(commands);
			"---"->PrintLine();
			WalkTree(commands->Get(0), -1);
		};
	}

	function : WalkTree(command : Command, level : IntHolder) ~ Nil {
		if(command->GetType() = Command->Type->CD) {
			if(command->GetOperand()->Equals("..")) {
				level->Sub(1);
			}
			else {
				level->Add(1);
			};
		};

		for(i := 0; i < level->Get(); i += 1;) {
			'\t'->Print();
		};

		if(command->GetType() = Command->Type->CD) {
			dir_name := command->GetOperand();
			if(dir_name->Equals("..")) {
				"\t\t[{$dir_name}]"->PrintLine();
			}
			else {
				"directory: '[{$dir_name}]'"->PrintLine();
			};
		};

		children := command->GetChildren();
		each(i : children) {
			WalkTree(children->Get(i), level);
		};

		if(command->GetType() = Command->Type->FILE) {
			file_size := command->GetOperand()->ToInt();
			file_name := command->GetOperand2();
			"\t'{$file_name}' (size={$file_size})"->PrintLine();
		};
	}

	function : ExecuteCommands(commands : Vector<Command>) ~ Nil {
		cur_dir : Command;
		dir_stack := Collection.Generic.Stack->New()<Command>;
		each(i : commands) {
			command := commands->Get(i);

			select(command->GetType()) {
				label Command->Type->CD {
					if(<>dir_stack->IsEmpty()) {
						cur_dir->AddChild(command);
					};

					dir_name := command->GetOperand();
					if(dir_name->Equals("..")) {
						dir_stack->Pop();
					}
					else {
						dir_stack->Push(command);
					};
				}

				label Command->Type->LS {
					dir_name := dir_stack->Top()->GetOperand();

					if(@debug) {
						for(j := 1; j < dir_stack->Size(); j += 1;) {
							'\t'->Print();
						};
						"At: '{$dir_name}'"->PrintLine();
					};

					cur_dir := dir_stack->Top();
				}

				label Command->Type->DIR {
					dir_name := command->GetOperand();

					if(@debug) {
						for(j := 0; j < dir_stack->Size(); j += 1;) {
							'\t'->Print();
						};
						"Directory: {$dir_name}"->PrintLine();
					};
				}

				label Command->Type->FILE {
					file_size := command->GetOperand()->ToInt();
					file_name := command->GetOperand2();
					cur_dir->AddChild(command);
					
					if(@debug) {
						for(j := 0; j < dir_stack->Size(); j += 1;) {
							'\t'->Print();
						};
						"File: {$file_name} ({$file_size})"->PrintLine();
					};					
				}
			};
		};
	}

	function : ParseCommand(line : String) ~ Command {
		tokens := line->Split(" ");

		command : Command;
		if(tokens[0]->Trim()->Equals("$")) {
			if(tokens[1]->Trim()->Equals("cd")) {
				command := Command->New(Command->Type->CD, tokens[2]);
			}
			else if(tokens[1]->Trim()->Equals("ls")) {
				command := Command->New(Command->Type->LS);
			};
		}
		else if(tokens[0]->Trim()->Equals("dir")) {
			command := Command->New(Command->Type->DIR, tokens[1]);
		}
		else {
			command := Command->New(Command->Type->FILE, tokens[0], tokens[1]);
		};

		return command;
	}

	function : ReadInput(file : String) ~ Vector<String> {
		inputs := Vector->New()<String>;

		reader := System.IO.Filesystem.FileReader->New(file);
		leaving {
			reader->Close();
		};

		line := reader->ReadLine();
		while(line <> Nil) {
			if(<>line->IsEmpty()) {
				inputs->AddBack(line);
			};
			line := reader->ReadLine();
		};

		return inputs;
	}
}

class Command {
	@type : Command->Type;
	@operand : String;
	@operand2 : String;
	@children : Vector<Command>;

	enum Type {
		CD,
		LS,
		DIR,
		FILE
	}

	New(type : Command->Type) {
		@type := type;
		@children := Vector->New()<Command>;
	}

	New(type : Command->Type, operand : String) {
		@type := type;
		@operand := operand;
		@children := Vector->New()<Command>;
	}

	New(type : Command->Type, operand : String, operand2 : String) {
		@type := type;
		@operand := operand;
		@operand2 := operand2;
		@children := Vector->New()<Command>;
	}

	method : public : GetType() ~ Command->Type {
		return @type;
	}

	method : public : GetOperand() ~ String {
		return @operand;
	}

	method : public : GetOperand2() ~ String {
		return @operand2;
	}

	method : public : AddChild(child : Command) ~ Nil {
		@children->AddBack(child);
	}

	method : public : GetChildren() ~ Vector<Command> {
		return @children;
	}

	method : public : ToString() ~ String {
		select(@type) {
			label Command->Type->CD {
				return "Change Directory: '{$@operand}'";
			}

			label Command->Type->LS {
				return "List Directory";
			}

			label Command->Type->DIR {
				return "Directory: '{$@operand}'";
			}

			label Command->Type->FILE {
				return "File: '{$@operand2}' (size={$@operand})";
			}
		};

		return "<Unknown>";
	}
}