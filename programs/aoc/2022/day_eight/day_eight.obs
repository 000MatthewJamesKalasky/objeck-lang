class AoC {
	@file : String;
	@forest : Int[,];
	@debug : Bool;

	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			AoC->New(args[0])->Brun();
		};
	}

	New(file : String) {
		@file := file;
		@debug := false;
	}

	method : public : Brun() ~ Nil {
		inputs := ReadInput(@file)<String>;
		if(MakeForest(inputs)) {
			dims := @forest->Size();
			x := dims[0]; y := dims[1];

			"Forest is {$x}x{$y}\n---"->PrintLine();
			ViewableTrees()->PrintLine();
		}
		else {
			">>> Error executing program <<<"->ErrorLine();
		};
	}

	method : ViewableTrees() ~ Int {
		dims := @forest->Size();
		x := dims[0]; y := dims[1];

		count := 0;

		each(i : x) {
			each(j : y) {
				System.IO.Standard->Print("[")->Print(@forest[i,j])->Print("]: ");
				if(ViewableTree(@forest[i,j], i, j)) {
					count += 1;
				};
			};
		};

		return count;
	}

	method : ViewableTree(check : Int, x : Int, y : Int) ~ Bool {
		dims := @forest->Size();
		max := dims[0];

		# down
		dx := x + 1;
		while(dx < max) {
# System.IO.Standard->Print("|")->Print(@forest[dx,y]);
			if(@forest[dx,y] >= check) {
				return false;
			};
			dx += 1;
		};

		# right
		dy := y + 1;
		while(dy < max) {
			if(@forest[x,dy] >= check) {
				return false;
			};
			dy += 1;
		};
		# up
		dx := x - 1;
		while(dx >= 0) {
			if(@forest[dx, y] >= check) {
				return false;
			};
			dx -= 1;
		};

		# left
		dy := y - 1;
		while(dy >= 0) {
			if(@forest[x, dy] >= check) {
				return false;
			};
			dy -= 1;
		};
		
		return true;
	}

	method : MakeForest(inputs : Collection.Generic.Vector<String>) ~ Bool {
		if(<>inputs->IsEmpty()) {
			row := inputs->Get(0);
			if(row->Size() = inputs->Size()) {
				size := inputs->Size();
				@forest := Int->New[size, size];
				each(i : size) {
					row := inputs->Get(i);
					each(j : row) {
						@forest[i,j] := row->Get(j) - '0';
					};
				};				
			};

			return true;
		};

		return false;
	}

	function : ReadInput(file : String) ~ Collection.Generic.Vector<String> {
		inputs := Collection.Generic.Vector->New()<String>;

		reader := System.IO.Filesystem.FileReader->New(file);
		leaving {
			reader->Close();
		};

		line := reader->ReadLine();
		while(line <> Nil) {
			if(<>line->IsEmpty()) {
				inputs->AddBack(line);
			};
			line := reader->ReadLine();
		};

		return inputs;
	}
}