use Web.HTTP, Collection, System.IO.Filesystem, Data.JSON, Web.HTTP.Server;

bundle API.OpenAI {
	class Test {
		function : Main(args : String[]) ~ Nil {
			if(args->Size() = 2) {
				# file := args[0];
				token := args[1];

				# Files->Upload(file, FileReader->ReadBinaryFile(file), token)->PrintLine();
				files := Files->List(token);
				each(file in files) {
					file->ToString()->PrintLine();
				};

				file := File->New("file-ZfOj8q7RASApmJTNGVd6oRMi", token);
				file->ToString()->PrintLine();
				file->Retrieve()->ToString()->PrintLine();
			};
		}

		function : GetStringValue(name : String, element : JsonElement) ~ String {
			value := "";

			value_json := element->Get(name);
			if(value_json <> Nil & <>value_json->IsNull()) {
				value := value_json->GetString();
			};

			return value;
		}

		function : DebugRequest(boundry : String) ~ Byte[] {
			boundry->Append("--------------------------OH8iaaTMuIlMnbNRt76GZo");
			cr_lf := "\r\n";
			
			buffer := "--{$boundry}{$cr_lf}";
			buffer += "Content-Disposition: form-data; name=\"purpose\"{$cr_lf}";
			buffer += "{$cr_lf}";
			buffer += "assistants{$cr_lf}";
			buffer += "--{$boundry}{$cr_lf}";
			buffer += "Content-Disposition: form-data; name=\"file\"; filename=\"bar.json\"{$cr_lf}";
			buffer += "Content-Type: application/octet-stream{$cr_lf}";
			buffer += "{$cr_lf}";
			buffer += "{ \"value\": 17 }{$cr_lf}";
			buffer += "--{$boundry}--";

			return buffer->ToByteArray();
		}
	}

	class File {
		@token : String;

		@id : String;
		@object : String;
		@bytes : Int;
		@created_at : Int;
		@filename : String;
		@purpose : String;

		New(file_json : JsonElement, token : String) {
			@token := token;

			@id := file_json->Get("id")->GetString();
			@object := file_json->Get("object")->GetString();
			@bytes := file_json->Get("bytes")->GetInt();
			@created_at := file_json->Get("created_at")->GetInt();
			@filename := file_json->Get("filename")->GetString();
			@purpose := file_json->Get("purpose")->GetString();
		}

		New(id : String, token : String) {
			@token := token;

			headers := Map->New()<String, String>;
			headers->Insert("Authorization", "Bearer sk-{$@token}");
			response := HttpsClient->QuickGet(Url->New("https://api.openai.com/v1/files/{$id}"), "application/json", headers);
			if(response <> Nil) {
				file_json := JsonParser->TextToElement(response->GetContent()->ToString());
				@id := file_json->Get("id")->GetString();
				@object := file_json->Get("object")->GetString();
				@bytes := file_json->Get("bytes")->GetInt();
				@created_at := file_json->Get("created_at")->GetInt();
				@filename := file_json->Get("filename")->GetString();
				@purpose := file_json->Get("purpose")->GetString();
			};
		}

		method : public : Retrieve() ~ Byte[] {
			headers := Map->New()<String, String>;
			headers->Insert("Authorization", "Bearer sk-{$@token}");
			return HttpsClient->QuickGet(Url->New("https://api.openai.com/v1/files/{$@id}/content"), "application/json", headers)->GetContent();
		}

		method : public : GetId() ~ String {
			return @id;
		}

		method : public : GetObject() ~ String {
			return @object;
		}

		method : public : GetBytes() ~ Int {
			return @bytes;
		}

		method : public : GetCreatedAt() ~ Int {
			return @created_at;
		}

		method : public : GetFilename() ~ String {
			return @filename;
		}

		method : public : GetPurpose() ~ String {
			return @purpose;
		}

		method : public : ToString() ~ String {
			return "[id='{$@id}', object='{$@object}', bytes={$@bytes}, created_at={$@created_at}, filename='{$@filename}', purpose='{$@purpose}'']"; 
		}
	}

	class Files {
		function : List(token : String) ~ Vector<API.OpenAI.File> {
			files := Vector->New()<API.OpenAI.File>

			headers := Map->New()<String, String>;
			headers->Insert("Authorization", "Bearer sk-{$token}");
			response := HttpsClient->QuickGet(Url->New("https://api.openai.com/v1/files"), "application/json", headers);
			if(response <> Nil) {
				root_json := JsonParser->TextToElement(response->GetContent()->ToString());
				files_json := root_json->Get("data");
				each(file_json in files_json) {
					files->AddBack(API.OpenAI.File->New(file_json, token));
				};
			};

			return files;
		}

		function : Upload(name : String, content : Byte[], token : String) ~ Bool {
			purpose_headers := Map->New()<String, String>;
			purpose_headers->Insert("Content-Disposition", "form-data; name=\"purpose\"");
			purpose_content := MultipartContent->New(purpose_headers, "assistants"->ToByteArray());

			content_headers := Map->New()<String, String>;
			content_headers->Insert("Content-Disposition", "form-data; name=\"file\"; filename=\"{$name}\"");
			content_headers->Insert("Content-Type", "application/octet-stream");
			multi_content := MultipartContent->New(content_headers, content);

			encoder := MultipartEncoder->New();
			encoder->Add(purpose_content);
			encoder->Add(multi_content);
			boundry := encoder->GetBoundary();
			data := encoder->ToByteArray();

			headers := Map->New()<String, String>;
			headers->Insert("Authorization", "Bearer sk-{$token}");
			headers->Insert("Content-Type", "multipart/form-data; boundary={$boundry}");

			response := HttpsClient->QuickPost(Url->New("https://api.openai.com/v1/files"), data, "multipart/form-data", headers);			
			return response->GetCode() = 200;
		}
	}

	class Assistant {
		@request_json : JsonElement;
		@model : String;
		@name : String;
		@description : String;
		@instructions : String;

		@tools : Vector<String>;
		@files : Vector<File>;

		New(model : String) {
			@model := model;

			@tools := Vector->New()<String>;
		}

		New(model : String, name : String, description : String) {
			@model := model;
			@name := name;
			@description := description;

			@tools := Vector->New()<String>;
		}

		method : public : IsLoaded() ~ Bool {
			return @request_json <> Nil;
		}

		method : public : SetName(name : String) ~ Nil {
			@name := name;
		}

		method : public : SetDescription(description : String) ~ Nil {
			@description := description;
		}

		method : public : SetInstructions(instructions : String) ~ Nil {
			@instructions := instructions;
		}

		method : public : AddFile(file : File) ~ Nil {
			@files->AddBack(file);
		}

		method : public : AddTool(tool : String) ~ Nil {
			@tools->AddBack(tool);
		}

		# TODO: add metadata

		method : public : Build() ~ Bool {
			if(@model = Nil) {
				return false;
			};

			builder := JsonBuilder->New();
			create_json := builder->PushObject();
			create_json->Insert("model", @model);

			if(@name <> Nil) {
				create_json->Insert("name", @name);
			};

			if(@description <> Nil) {
				create_json->Insert("description", @description);
			};

			if(@instructions <> Nil) {
				create_json->Insert("instructions", @instructions);
			};


			# tools
			tools_json := JsonElement->New(JsonElement->JsonType->ARRAY);
			each(tool in @tools) {
				tool_json := JsonElement->New(JsonElement->JsonType->OBJECT);
				tool_json->Insert("type", tool);
				tools_json->Add(tool_json);
			};
			create_json->Insert("tools", tools_json);

			@request_json := builder->PopAll();
			return true;
		}

		method : public : ToJson() ~ String {
			if(@request_json = Nil) {
				Build();
			};

			if(@request_json = Nil) {
				return "{}";
			};

			return @request_json->ToString();
		}
	}
}