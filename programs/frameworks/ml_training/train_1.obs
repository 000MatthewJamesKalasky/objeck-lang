use Data.CSV, Data.JSON, Collection;

class DatasetMaker {
	@named_tables : Hash<String, CsvTable>;
	@metadata_json : JsonElement;
	@abbreviations : Hash<String, String>;
	@output_json : JsonElement;
	@target : String;
	@file_writer : System.IO.Filesystem.FileWriter;

	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 3) {
			String->SetFloatPrecision(3);

			dir_path := args[0];
			target := args[1];
			iters := args[2]->ToInt();

			dataset_maker := DatasetMaker->New(dir_path, target);
			status := dataset_maker->Make(iters);
			"\n---\nSuccess: {$status}"->PrintLine();
		}
		else {

		};
	}

	New(dir_path : String, target : String) {
		@target := target;
		@named_tables := Hash->New()<String, CsvTable>;

		"Loading files:"->PrintLine();
		filenames := System.IO.Filesystem.Directory->List(dir_path);
		each(filename in filenames) {
			if(filename->EndsWith(".csv")) {
				"\tfile='{$filename}'"->PrintLine();

				file_path := dir_path->Copy();

				if(<>(file_path->EndsWith('/') | file_path->EndsWith('\\'))) {
					file_path += System.IO.Filesystem.Directory->GetSlash();
				};
				file_path += filename;
				
				table := CsvTable->New(System.IO.Filesystem.FileReader->ReadFile(file_path));
				if(table->IsParsed()) {
					@metadata_json := JsonElement->New(JsonElement->JsonType->OBJECT);
					@metadata_json->Insert("source", "du_late_60s_mens_basketball");

					# stat abbreviations
					@abbreviations := Hash->New()<String, String>;
					@abbreviations->Insert("TRB", "total rebound");
					@abbreviations->Insert("FGA", "field goals attempted");
					@abbreviations->Insert("FTA", "free throws attempted");
					@abbreviations->Insert("FG%", "field goal");
					@abbreviations->Insert("AST", "assists");
					@abbreviations->Insert("STL", "steals");
					@abbreviations->Insert("BLK", "blocks");
					@abbreviations->Insert("TOV", "turn overs");
					@abbreviations->Insert("PF", "personal fouls");
					@abbreviations->Insert("PTS", "total points");
					@abbreviations->Insert("FT", "free throws made");
					@abbreviations->Insert("FT%", "free throws");

					# month abbreviations
					@abbreviations->Insert("Dec", "December");
					@abbreviations->Insert("Jan", "January");
					@abbreviations->Insert("Feb", "February");
					@abbreviations->Insert("Mar", "March");

					table_name := filename->SubString(filename->Size() - 4);
					@named_tables->Insert(table_name, table);
				};
			};
		};
		"---"->PrintLine();
	}

	method : BuildElement(query : String, response : String) ~ Nil {
		if(@target->Equals("alpaca")) {
			query_response_json := JsonElement->New(JsonElement->JsonType->OBJECT);

			query_response_json->Insert("instruction", query);
			query_response_json->Insert("input", "");
			query_response_json->Insert("output", response);

			@output_json->Add(query_response_json);
		}
		else if(@target->Equals("openai")) {
			line := "{\"messages\": [{\"role\": \"system\", \"content\": \"You are a historian in the late 1960s at the Univerity of Denver.\"}, {\"role\": \"user\", \"content\": \"{$query}\"}, {\"role\": \"assistant\", \"content\": \"{$response}\"}]}";
			# line := "{\"prompt\": \"{$query}\", \"completion\": \"{$response}\"}";
			WriteToFile(line);
		};
	}

	method : WriteToFile(line : String) ~ Nil {
		@file_writer->WriteString(line);
		@file_writer->WriteString("\r\n");
	}

	method : public : Make(inters : Int) ~ Bool {
		leaving {
			@file_writer->Close();
		};

		ending : String;
		if(@target->Equals("alpaca")) {
			ending := "json";
		}
		else {
			ending := "jsonl";
		};
		
		@file_writer := System.IO.Filesystem.FileWriter->New("data/du_late60s_mens_basketball.{$ending}");
		if(@file_writer->IsOpen()) {
			avg_stats := ["TRB", "FG%", "PF", "PTS", "FT", "FT%"];
			toatl_stats := ["FGA", "FTA"];
			
			@output_json := JsonElement->New(JsonElement->JsonType->ARRAY);

			# average of 8M for 384 iterations
			count_max := 8; count := 0;
			each(i : inters) {
				count += 1;

				each(j : 5) {
					each(stat in avg_stats) {
						if(<>AverageStat(Int->Random(1966, 1968), stat)) {
							return false;
						};
					};

					each(stat in toatl_stats) {
						if(<>TotalStat(Int->Random(1966, 1968), stat)) {
							return false;
						};
					};
				};

				each(j : 8) {
					if(<>ScheduleResult(Int->Random(1966, 1968))) {
						return false;
					};
				};

				if(count > count_max) {
					System.Utility.Spinner->Next();
					count := 0;
				};
			};

			if(@target->Equals("alpaca")) {
				WriteToFile(@output_json->ToString());
			};

			System.Utility.Spinner->Last();
		};

		return true;
	}

	method : public : ScheduleResult(year : Int) ~ Bool {
		table := @named_tables->Find("schedule-results");
		if(table <> Nil) {
			table_by_year := table->Matches("Year", year->ToString());

			# games played by month
			schedule_mons := ["Dec", "Jan", "Feb", "Mar"];
			random_mon := schedule_mons[Int->Random(0, 3)];

			games_year_month := table_by_year->Contains("Date", random_mon);
			count_year_month := (games_year_month->Size() - 1);

			random_school_name := Bool->Flip() ? "DU" : "University of Denver";
			random_mon_name := @abbreviations->Find(random_mon);

			query := "In " + year + " how many games did the " + random_school_name + " men's basketball team play in " + random_mon_name + "?";
			response := "In " + year + ", the " + random_school_name + " men's basketball team played " + count_year_month + " games in " + random_mon_name + ".";
			
			# home vs. away games
			games_home := table_by_year->Contains("At-Home", "@");
			count_home := (games_home->Size() - 1);
			count_away := (table_by_year->Size() - games_home->Size());

			query := "In " + year + " how many home games did the " + random_school_name + " men's basketball team play?";
			response := "In " + year + ", the " + random_school_name + " men's basketball team played " + count_home + " home games.";
			BuildElement(query, response);

			query := "In " + year + " how many away games did the " + random_school_name + " men's basketball team play?";
			response := "In " + year + ", the " + random_school_name + " men's basketball team played " + count_away + " away games.";
			BuildElement(query, response);

			# team played with score and who won
			random_index := Int->Random(1, table_by_year->Size() - 1);
			random_opp := table_by_year->Get(random_index);
			random_opp_name := random_opp->Get("Opponent");
			found_index := random_opp_name->Find('(');
			if(found_index > 1) {
				random_opp_name := random_opp_name->SubString(found_index - 1);
			};

			team_score := random_opp->Get("Tm")->ToInt();
			opp_score := random_opp->Get("Opp")->ToInt();
			game_date := random_opp->Get("Date");
			game_date := game_date->SubString(5, game_date->Size() - 5);

			query := "What was the score of the " + year + " game between Denver and " + random_opp_name  + " on " + game_date + "?";
			response := "The score of the game on " + game_date + " between Denver and " + random_opp_name  + " was " +  team_score->ToString() + 
				" to " + opp_score->ToString() + ", Denver " + (team_score > opp_score ? "won.": "lost.");
			BuildElement(query, response);

			return true;
		};

		return false;
	}

	method : public : AverageStat(year : Int, stat_key : String) ~ Bool {
		# TODO: YEAR!!!

		table := @named_tables->Find("avg-games");
		if(table <> Nil) {
			random_index := Int->Random(1, table->Size() - 1);

			player := table->Get(random_index)->Get("Player");
			if(Bool->Flip()) {
				split_index := player->Find(' ');
				if(split_index > 0) {
					split_index += 1;
					player := player->SubString(split_index, player->Size() - split_index);
				};
			};
			stat_value := table->Get(random_index)->Get(stat_key);
			stat_word := @abbreviations->Find(stat_key);

			if(stat_key->Equals("FG%")) {
				stat_value_str := (stat_value->ToFloat() * 100.0)->ToString();
				query := "In " + year + " what was " + player + " field goal percentage for the University of Denver?";
				response := "" + player + " field goal percentage in " + year + " was " + stat_value_str + "% for the University of Denver.";
				BuildElement(query, response);
			}
			else if(stat_key->Equals("FT%")) {
				stat_value_str := (stat_value->ToFloat() * 100.0)->ToString();
				query := "In " + year + " what was " + player + " free throw percentage for the University of Denver?";
				response := "" + player + " free throw percentage in " + year + " was " + stat_value_str + "% for the University of Denver.";
				BuildElement(query, response);
			}
			else {
				query := "In " + year + " how many " + stat_word + " did " + player + " average for the University of Denver?";
				response := "" + player + " averaged " + stat_value + " " + stat_word + " in " + year + " for the University of Denver.";
				BuildElement(query, response);
			};

			if(stat_key->Equals("FG%")) {
				stat_value_str := (stat_value->ToFloat() * 100.0)->Round->ToString();
				query := "What was " + player + " field goal percentage in " + year + " for the University of Denver?";
				response := "In " + year + " " + player  + " field goal percentage " + stat_value_str + "% for the University of Denver.";
				BuildElement(query, response);
			}
			else if(stat_key->Equals("FT%")) {
				stat_value_str := (stat_value->ToFloat() * 100.0)->Round->ToString();
				query := "What was " + player + " free throw percentage in " + year + " for the University of Denver?";
				response := "In " + year + " " + player  + " free throw percentage " + stat_value_str + "% for the University of Denver.";
				BuildElement(query, response);
			}
			else {
				query := "How many " + stat_word + " did " + player + " average in " + year + " for the University of Denver?";
				response := "In " + year + " " + player + " averaged " + stat_value + " " + stat_word + " for the University of Denver.";
				BuildElement(query, response);
			}

			return true;
		};

		return false;
	}

	method : public : TotalTeam(year : Int, stat_key : String) ~ Bool {
		# TODO: YEAR!!!
		
		table := @named_tables->Find("avg-team-opp");
		if(table <> Nil) {
			random_index := Int->Random(1, table->Size() - 1);

			team_desc : String;
			team := table->Get(random_index)->Get("Side");
			if(team->Equals("Team")) {
				team_desc := "University of Denver";
			}
			else {
				team_desc := "opponent";
			};

			stat_value := table->Get(random_index)->Get(stat_key);
			stat_word := @abbreviations->Find(stat_key);

			query := "In " + year + " how many " + stat_word + " did the " + team_desc + " total?";
			response := "The " + team_desc + " totaled " + stat_value + " " + stat_word + " in " + year + ".";
			BuildElement(query, response);

			query := "How many " + stat_word + " did the " + team_desc + " total in " + year + "?";
			response := "In " + year + " the " + team_desc + " totaled " + stat_value + " " + stat_word + ".";
			BuildElement(query, response);

			return true;
		};

		return false;
	}

	method : public : AverageTeam(year : Int, stat_key : String) ~ Bool {
		table := @named_tables->Find("avg-team-opp");
		if(table <> Nil) {
			random_index := Int->Random(1, table->Size() - 1);

			team_desc : String;
			team := table->Get(random_index)->Get("Side");
			if(team->Equals("Team")) {
				team_desc := "University of Denver";
			}
			else {
				team_desc := "opponent";
			};

			stat_value := table->Get(random_index)->Get(stat_key);
			stat_word := @abbreviations->Find(stat_key);

			query := "In " + year + " how many " + stat_word + " did the " + team_desc + " average?";
			response := "The " + team_desc + " averaged " + stat_value + " " + stat_word + " in " + year + ".";
			BuildElement(query, response);

			query := "How many " + stat_word + " did the " + team_desc + " average in " + year + "?";
			response := "In " + year + " the " + team_desc + " averaged " + stat_value + " " + stat_word + ".";
			BuildElement(query, response);

			return true;
		};

		return false;
	}

	method : public : TotalStat(year : Int, stat_key : String) ~ Bool {
		table := @named_tables->Find("total-games");
		if(table <> Nil) {
			random_index := Int->Random(1, table->Size() - 1);

			player := table->Get(random_index)->Get("Player");
			if(Bool->Flip()) {
				split_index := player->Find(' ');
				if(split_index > 0) {
					split_index += 1;
					player := player->SubString(split_index, player->Size() - split_index);
				};
			};
			stat_value := table->Get(random_index)->Get(stat_key);
			stat_word := @abbreviations->Find(stat_key);

			query := "In " + year + " how many " + stat_word + " did " + player + " total for the University of Denver?";
			response := "" + player + " totaled " + stat_value + " " + stat_word + " in " + year + " for the University of Denver.";
			BuildElement(query, response);

			query := "How many total " + stat_word + " did " + player + " total in " + year + " for the University of Denver?";
			response := "In " + year + " " + player + " totaled " + stat_value + " " + stat_word + " for the University of Denver.";
			BuildElement(query, response);

			return true;
		};

		return false;
	}
}