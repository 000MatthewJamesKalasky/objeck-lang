#~
# compile: obc -src rss_https_xml_15.obs -lib finder.obl,xml.obl,collect.obl -dest rss_https_xml_15.obe
# run: obr rss_https_xml_15.obe "https://www.techmeme.com/feed.xml" <filename>
~#

use Web.HTTP;

class RSS {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() > 0) {
			xml := FetchXml(args[0]);
			if(xml <> Nil) {	
				parser := XmlParser->New(xml);
				if(parser->Parse()) {
					html := "<html><body>";
					GetMetaData(html, parser);
					GetPosts(html, parser);
					html += "</body></html>";
					if(args->Size() = 2) {
						WriteHtml(html, args[1]);
					}
					else {
						html->PrintLine();
					};
				};
			}
			else {
				"--- Unable to fetch XML ---"->ErrorLine();
			};
		};
	}
	
	function : WriteHtml(html : String, file : String) ~ Nil {
		out := FileWriter->New(file);
		leaving {
			out->Close();
		};
		out->WriteString(html);
		"Wrote file: {$file}..."->PrintLine();
	}

	function : GetMetaData(html : String, parser : XmlParser) ~ Nil {
		title := GetMatch("/rss/channel/title", parser);
		description := GetMatch("/rss/channel/description", parser);
		link := GetMatch("/rss/channel/link", parser);

		html += "<b><a href='";
		html += link;
		html += "'>";
		html += title;
		html += "</a></b></br><p>";
		html += description;
		html += "</p>";
	}

	function : GetPosts(html : String, parser : XmlParser) ~ Nil {
		items := parser->FindElements("/rss/channel/item");
		each(i : items) {
			item := items->Get(i)->As(XmlElement);
			title := GetContent(item->GetFirstChild("title"));
			link := GetContent(item->GetFirstChild("link"));
			description := GetContent(item->GetFirstChild("description"));
			pubDate := GetContent(item->GetFirstChild("pubDate"));

			if(description <> Nil & (description->Find("href") > -1 | description->Find("HREF") > -1)) {
				html += "<p><h3>";
				html += pubDate;
				html += "</h3>";
				html += description;
				html += "</p>";
			}
			else if(description <> Nil) {
				html += "<p><h3>";
				html += pubDate;
				html += "</h3><b><a href='";
				html += link;
				html += "'>";
				html += title;
				html += "</a></b></br>";
				html += description;
				html += "</p>";
			}
			else {
				html += "<p><h3>";
				html += pubDate;
				html += "</h3><b><a href='";
				html += link;
				html += "'>";
				html += title;
				html += "</a></b></br></p>";
			};
		};
	}

	function : GetMatch(path : String, parser : XmlParser) ~ String {
		matches := parser->FindElements(path);
		if(matches->Size() > 0) {			
			return GetContent(matches->Get(0)->As(XmlElement));
		};
		
		return Nil;
	}

	function : GetContent(elem : XmlElement) ~ String {
		if(elem = Nil) {
			return Nil;
		};

		cdata := elem->GetFirstChild("[cdata]");
		if(cdata <> Nil) {
			return cdata->GetContent();
		}
		else {
			return XmlElement->DecodeString(elem->GetContent());
		};	
	}

	function : FetchXml(path : String) ~ String {
		xml := "";

		if(path->StartsWith("http:")) {
			xml := HttpClient->New()->GetAll(path);
		}
		else if(path->StartsWith("https:")) {
			xml := HttpsClient->New()->GetAll(path);
		}
		else {
			xml := FileReader->ReadFile(path);
		};

		return xml;
	}
}
